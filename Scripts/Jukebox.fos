namespace Jukebox
{

#if SERVER

// Author: rifleman17
// Музыкальный автомат. В предмет бросается монетка, он проигрывает одну случайную мелодию

#define SILENCE_TIME   (Time::Minutes(3))
#define STR_OUTOFORDER (25)
#define STR_LOWMONEY   (26)
#define PLAY_RADIUS    (25)
#define PRICE          (10)

const string[] trackList = {"akiss.acm", "Maybe.ogg", "ccboogie.ogg", "ppmamma.ogg"};

void _JukeBoxInit(Item item, bool firstTime)
{
    item.OnCritterUseOnSelf.Subscribe(_UseOnJukeBox);
}

bool _UseOnJukeBox(Item item, Critter cr, Item usedItem, int param)
{
    if (valid(usedItem) && usedItem.ProtoId == Content::Item::bottle_caps) {
        if (usedItem.Count < PRICE) {
            // Не хватает денег
            Messaging::Info(cr, TextPackName::Text, STR_LOWMONEY, "$price" + PRICE);
            return true;
        }
        else {
            if (item.LastUsedTime != ZERO_SYNCTIME && item.LastUsedTime + SILENCE_TIME > Game.SynchronizedTime) {
                // Недавно использовался
                Messaging::Info(cr, TextPackName::Text, STR_OUTOFORDER);
                return true;
            }
            else {
                int idx = Game.Random(0, trackList.length() - 1);
                mpos hex;
                Map map = item.GetMapPosition(hex);
                if (valid(map)) {
                    item.IsShowAnim = true;
                    Game.StartTimeEvent(Time::Seconds(30), OffJukeBox, item.Id); //SavedDeferredCall
                    Critter[] critters = map.GetCritters(hex, PLAY_RADIUS, CritterFindType::Players);
                    Game.DestroyItem(usedItem, 10);
                    item.LastUsedTime = Game.SynchronizedTime;
                    for (int i = 0; i < critters.length(); i++) {
                        Media::PlayMusic(critters[i], trackList[idx], 0, 0);
                    }
                    return true;
                }
            }
        }
    }
    return false;
}

void OffJukeBox(any value)
{
    Item item = Game.GetItem(value);
    if (valid(item)) {
        item.IsShowAnim = false;
    }
}

// Покупка автоматов в Нью-Рено

#define ENTRY_JB (142)

bool d_JukeboxesCheck(Critter player, Critter npc)
{
    Map map = Game.GetMap(Content::Map::newr_eld);
    if (valid(map)) {
        if (map.GetItems(Content::Item::jukebox_off).length() > 0) {
            return true;
        }
    }
    return false;
}

bool d_NotJukeboxesCheck(Critter player, Critter npc)
{
    return !d_JukeboxesCheck(player, npc);
}

bool d_CanPlaceJukeBox(Critter player, Critter npc)
{
    Location loc = Game.GetLocation(QuestWarehouse::GetLocId(player));
    if (valid(loc)) {
        Map map = loc.GetMapByIndex(0);
        if (valid(map)) {
            if (Entrance::MapCountEntry(map, ENTRY_JB) == 1) {
                if (map.GetItems(Content::Item::jukebox_off).length() == 0) {
                    return true;
                }
            }
        }
    }
    return false;
}

bool d_CanNotPlaceJukeBox(Critter player, Critter npc)
{
    return !d_CanPlaceJukeBox(player, npc);
}

bool d_AlreadyHasJukeBox(Critter player, Critter npc)
{
    Location loc = Game.GetLocation(QuestWarehouse::GetLocId(player));
    if (valid(loc)) {
        Map map = loc.GetMapByIndex(0);
        if (valid(map)) {
            if (Entrance::MapCountEntry(map, ENTRY_JB) == 1) {
                if (map.GetItems(Content::Item::jukebox).length() > 0) {
                    return true;
                }
            }
        }
    }
    return false;
}

void r_SellJukeBox(Critter player, Critter npc)
{
    Location loc = Game.GetLocation(QuestWarehouse::GetLocId(player));
    if (valid(loc)) {
        Map map = loc.GetMapByIndex(0);
        if (valid(map)) {
            if (Entrance::MapCountEntry(map, ENTRY_JB) == 1) {
                if (map.GetItems(Content::Item::jukebox_off).isEmpty()) {
                    mpos hex;
                    if (Entrance::MapGetEntryCoords(map, ENTRY_JB, 0, hex)) {
                        map.AddItem(hex, Content::Item::jukebox, 1);
                        Map mapEld = Game.GetMap(Content::Map::newr_eld);
                        if (valid(mapEld)) {
                            Item[] items = mapEld.GetItems(Content::Item::jukebox_off);
                            if (!items.isEmpty()) {
                                Game.DestroyItem(items[0]);
                            }
                        }
                    }
                }
            }
        }
    }
}

#endif

}
