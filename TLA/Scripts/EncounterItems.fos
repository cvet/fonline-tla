// FOS Server
// Author: rifleman17

#include "Tla"
#include "EncounterItemsTable"

#define ENTIRE_ITEM      ( 0 )                                    // Гекс для расположения итема
#define TABLE_COLUMNS    ( 6 )                                    // число колонок в таблице
#define TABLE_ITEMS      ( items_table.length() / TABLE_COLUMNS ) // всего определений итемов

#define MAX_TRIES        ( ETYPE_DEFAULT )                        // максимальное число попыток найти подходящую по типу энкаунтера строку в таблице итемов
#define MIN_ITEMS        ( 0 )                                    // минимальное число итемов на энке
#define MAX_ITEMS        ( 8 )                                    // Максимальное

#define _GetType         # (idx)    ( items_table[ TABLE_COLUMNS * ( 0 + idx ) + 1 ] )
#define _GetPid          # (idx)    ( items_table[ TABLE_COLUMNS * ( 0 + idx ) + 0 ] )
#define _GetQty          # (idx)    ( Random( items_table[ TABLE_COLUMNS * ( 0 + idx ) + 2 ], items_table[ TABLE_COLUMNS * ( 0 + idx ) + 3 ] ) )
#define _GetChance       # (idx)  ( items_table[ TABLE_COLUMNS * ( 0 + idx ) + 4 ] )
#define _GetGex          # (idx)     ( items_table[ TABLE_COLUMNS * ( 0 + idx ) + 5 ] )
#define _IsCave          # (mapPid)  ( mapPid == Content::Map::e_cave0_in || mapPid == Content::Map::e_cave1_in || mapPid == Content::Map::e_cave2_in || mapPid == Content::Map::e_cave3_in || mapPid == Content::Map::e_cave4_in || mapPid == Content::Map::e_cave5_in || mapPid == Content::Map::e_coast5_cave )

// Фильтр таблицы по типу энка и гексу
uint16[] @ GetItemsInfo( uint8 eType, uint8 hex )
{
    uint16[] infos;
    for( uint16 i = 0; i < TABLE_ITEMS; i++ )
    {
        if( ( _GetType( i ) == eType || _GetType( i ) == ETYPE_ANY ) && _GetGex( i ) == hex )
        {
            // Log("eType:"+eType+"_GetType:"+_GetType(i));
            infos.insertLast( i );
        }
    }

    return infos;
}

uint8 GetEncounterType( Map& map )
{
    uint mapPid = map.ProtoId;
    // Log("mapPid"+mapPid);
    if( _IsCave( mapPid ) )
        return ETYPE_CAVERN;
    // Log("not caverb");
    Location@ loc = map.GetLocation();
    if( !valid( loc ) )
        return ETYPE_DEFAULT;
    uint16 pid = loc.ProtoId;
    if( pid >= Content::Location::desert_1 && pid <= Content::Location::desert_12 )
        return ETYPE_DESERT;
    if( pid >= Content::Location::mountain_1 && pid <= Content::Location::mountain_6 )
        return ETYPE_MOUNTAINS;
    if( pid >= Content::Location::cavern_0 && pid <= Content::Location::cavern_5 )
        return ETYPE_MOUNTAINS;
    if( pid >= Content::Location::city_1 && pid <= Content::Location::city_8 )
        return ETYPE_CITY;
    if( pid >= Content::Location::coast_1 && pid <= Content::Location::coast_11 )
        return ETYPE_COAST;
    if( pid >= Content::Location::ocean_1 && pid <= Content::Location::ocean_3 )
        return ETYPE_OCEAN;

    return ETYPE_DEFAULT;
}

void GenerateItems( Map& map, Critter@[] @  grp )
{
    int minLuck = 8;
    if( valid( grp ) )
    {
        for( uint i = 0, l = grp.length() - 1; i < l; i++ )
        {
            Critter@ cr = grp[ i ];
            if( !valid( cr ) )
                continue;
            if( minLuck > cr.Luck )
                minLuck = cr.Luck;
        }
    }
    uint8 itemz = Random( MIN_ITEMS, CLAMP( minLuck, 3, MAX_ITEMS ) );
    uint8 mapType = GetEncounterType( map );

    Entire[] entires;
    for( uint i = 0, l = gex_table.length(); i < l; i++ )
    {
        ParseEntires( map, entires, gex_table[ i ] );
    }
    uint len = entires.length();
    if( len == 0 )
        return;

    int8 tries = 30;
    uint[] foundedEntires;
    uint cur = 0;
    int  idx = 0;
    while( tries > 0 )
    {
        cur = Random( 0, len - 1 );
        if( !Present( cur, foundedEntires ) )
        {
            foundedEntires.insertLast( cur );
        }
        if( foundedEntires.length() > itemz )
            break;
        tries--;
    }

    bool found = false;
    for( uint i = 0, l = foundedEntires.length(); i < l; i++ )
    {
        Entire@ hex = entires[ foundedEntires[ i ] ];
        if( valid( hex ) )
        {
            uint16[] @ rows = GetItemsInfo( mapType, hex.Number );
            if( rows.length() == 0 )
                continue;

            idx = rows[ Random( 0, rows.length() - 1 ) ];
            if( idx < 0 )
                continue;

            if( Random( 1, 99 ) < int( _GetChance( idx ) ) )
            {
                Item@[] items;
                if( map.GetItems( hex.HexX, hex.HexY, 2, 0, items ) > 0 )
                {
                    for( uint j = 0; j < items.length(); j++ )
                    {
                        if( items[ j ].Type == Container )
                        {
                            uint qty = _GetQty( idx );
                            if( qty > 0 )
                                items[ j ].AddItem( _GetPid( idx ), qty, 0 );
                            found = true;
                            break;
                        }
                    }
                }
                if( !found )
                    map.AddItem( hex.HexX, hex.HexY, _GetPid( idx ), _GetQty( idx ) );

                // Log("added items"+_GetPid(idx)+" x="+hex.HexX+" y="+hex.HexY +" eType"+hex.Number);
                // Log("table len^"+items_table.length());
            }
        }
    }
}
