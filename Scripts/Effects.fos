namespace Effects
{

///@ RemoteCall Client DoFadeScreen(ucolor fromColor, ucolor toColor, timespan time)
///@ RemoteCall Client DoQuakeScreen(int32 noise, timespan time)
///@ RemoteCall Client DoRunEffect(hstring effectPid, mpos hex, int32 radius)
///@ RemoteCall Client DoRunFlyEffect(hstring effectPid, ident crId, ident targetId, mpos hex, mpos targetHex)
///@ RemoteCall Client DoPlaySound(hstring soundName)
///@ RemoteCall Client DoAnimate(ident crId, CritterStateAnim state, CritterActionAnim action)

#if SERVER

// Default values
const int QuakeNoise = 8;
const timespan QuakeTime = Time::Milliseconds(800);

void FadeScreen(Critter cr, bool fadeOut, timespan time)
{
    cr.PlayerClientCall.DoFadeScreen(fadeOut ? ucolor(0, 0, 0, 255) : ZERO_UCOLOR, fadeOut ? ZERO_UCOLOR : ucolor(0, 0, 0, 255), time);
}

void FadeScreen(Critter cr, ucolor fromColor, ucolor toColor, timespan time)
{
    cr.PlayerClientCall.DoFadeScreen(fromColor, toColor, time);
}

void FadeScreen(Map map, bool fadeOut, timespan time)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoFadeScreen(fadeOut ? ucolor(0, 0, 0, 255) : ZERO_UCOLOR, fadeOut ? ZERO_UCOLOR : ucolor(0, 0, 0, 255), time);
    }
}

void FadeScreen(Map map, ucolor fromColor, ucolor toColor, timespan time)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoFadeScreen(fromColor, toColor, time);
    }
}

void QuakeScreen(Critter cr)
{
    cr.PlayerClientCall.DoQuakeScreen(QuakeNoise, QuakeTime);
}

void QuakeScreen(Critter cr, int noise, timespan time)
{
    cr.PlayerClientCall.DoQuakeScreen(noise, time);
}

void QuakeScreen(Map map)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoQuakeScreen(QuakeNoise, QuakeTime);
    }
}

void QuakeScreen(Map map, int noise, timespan time)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoQuakeScreen(noise, time);
    }
}

void RunEffect(Map map, hstring effectPid, mpos hex, int radius = 0)
{
    Critter[] critters = map.GetCrittersWhoSeeHex(hex, radius, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunEffect(effectPid, hex, radius);
    }
}

void RunFlyEffect(Map map, hstring effectPid, Critter cr, Critter target)
{
    Critter[] critters = map.GetCrittersWhoSeePath(cr.Hex, target.Hex, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunFlyEffect(effectPid, cr.Id, target.Id, cr.Hex, target.Hex);
    }
}

void RunFlyEffect(Map map, hstring effectPid, Critter cr, mpos targetHex)
{
    Critter[] critters = map.GetCrittersWhoSeePath(cr.Hex, targetHex, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunFlyEffect(effectPid, cr.Id, ZERO_IDENT, cr.Hex, targetHex);
    }
}

void RunFlyEffect(Map map, hstring effectPid, mpos hex, mpos targetHex)
{
    Critter[] critters = map.GetCrittersWhoSeePath(hex, targetHex, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunFlyEffect(effectPid, ZERO_IDENT, ZERO_IDENT, hex, targetHex);
    }
}

void PlaySound(Map map, hstring soundName, mpos hex, int radius = 0)
{
    Critter[] critters = map.GetCrittersWhoSeeHex(hex, radius, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlaySound(soundName);
    }
}

void PlaySound(Map map, string soundName, mpos hex, int radius = 0)
{
    PlaySound(map, hstring(soundName), hex, radius);
}

void PlaySound(Map map, hstring soundName)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlaySound(soundName);
    }
}

void PlaySound(Map map, string soundName)
{
    PlaySound(map, hstring(soundName));
}

void PlaySound(Critter cr, hstring soundName)
{
    cr.PlayerClientCall.DoPlaySound(soundName);

    Critter[] critters = cr.GetCritters(CritterSeeType::WhoSeeMe, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlaySound(soundName);
    }
}

void PlaySound(Critter cr, string soundName)
{
    PlaySound(cr, hstring(soundName));
}

void Animate(Critter cr, CritterStateAnim state, CritterActionAnim action)
{
    Critter[] critters = cr.GetCritters(CritterSeeType::WhoSeeMe, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoAnimate(cr.Id, state, action);
    }
}

#endif

#if CLIENT

void DoFadeScreen(ucolor fromColor, ucolor toColor, timespan time)
{
    Game.FadeScreen(fromColor, toColor, time, true);
}

void DoQuakeScreen(int noise, timespan time)
{
    Game.QuakeScreen(noise, time);
}

void DoRunEffect(hstring effectPid, mpos hex, int radius)
{
    Item effect = CurMap.CreateLocalItem(effectPid, hex);
    effect.PlayAnim(EMPTY_HSTRING, false, false);
    effect.StartTimeEvent(Time::Milliseconds(100), Time::Milliseconds(100), EffectDeferredDestroy);

    for (int i = 0; i < radius; i++) {
        for (uint8 dir = 0; dir < 6; dir++) {
            mpos hex2 = hex;

            if (CurMap.MoveHexByDir(hex2, dir, i + 1) != i + 1) {
                break;
            }

            for (int j = 0; j < i + 1; j++) {
                effect = CurMap.CreateLocalItem(effectPid, hex2);
                effect.PlayAnim(EMPTY_HSTRING, false, false);
                effect.StartTimeEvent(Time::Milliseconds(100), Time::Milliseconds(100), EffectDeferredDestroy);

                if (!CurMap.MoveHexByDir(hex2, (dir + 2) % 6)) {
                    break;
                }
            }
        }
    }
}

void EffectDeferredDestroy(Item item)
{
    if (!item.IsAnimPlaying()) {
        item.Finish();
        Game.StopCurrentTimeEvent();
    }
}

void DoRunFlyEffect(hstring effectPid, ident crId, ident targetId, mpos hex, mpos targetHex)
{
    Critter cr = Game.GetCritter(crId);
    Critter target = Game.GetCritter(targetId);

    hex = cr != null ? cr.Hex : hex;
    targetHex = target != null ? target.Hex : targetHex;

    Item effect = CurMap.CreateLocalItem(effectPid, hex);
    effect.SetAnimDir(Game.GetDirection(hex, targetHex));
    effect.MoveToHex(targetHex, 1.0f);
    effect.StartTimeEvent(Time::Milliseconds(100), Time::Milliseconds(100), FlyEffectDeferredDestroy);
}

void FlyEffectDeferredDestroy(Item item)
{
    if (!item.IsMoving()) {
        item.Finish();
        Game.StopCurrentTimeEvent();
    }
}

void DoPlaySound(hstring soundName)
{
    Game.PlaySound(soundName);
}

void DoAnimate(ident crId, CritterStateAnim state, CritterActionAnim action)
{
    Critter cr = Game.GetCritter(crId);

    if (cr != null) {
        cr.Animate(state, action);
    }
}

/* soundType
'R' - перезарядка
'A' - атака
'O' - отсутствие зарядов
'F' - полет заряда
'H' - попадание в цель*/
void PlayWeaponSound(int soundType, int soundId, int mode)
{
    if (soundId == 0) {
        return;
    }
    string soundName = "WHD1XXX1";
    soundName.rawSet(1, soundType);
    soundName.rawSet(2, soundId);
    soundName.rawSet(3, mode);
    // Возможны два варианта одного звука
    // WAD1XXX1.acm
    // WAD1XXX2.acm
    soundName[7] = "" + Game.Random(1, 2);
    if (Game.PlaySound("sound/SFX/" + soundName + ".ACM")) {
        return;
    }
    soundName.rawSet(7, '1');
    if (Game.PlaySound("sound/SFX/" + soundName + ".acm")) {
        return;
    }
    if (!Game.PlaySound("sound/SFX/" + soundName + ".ACM")) {
        Game.Log("Weapon sound not found: " + soundName);
    }
}

#endif

}
