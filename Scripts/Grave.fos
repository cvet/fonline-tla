// FOS Server
// Author: cvet
// Script attached to all graves, in protos.
// Graves pids: 344..347, 374..376.

void _GraveInit(Item grave, bool firstTime)
{
    grave.OnCritterUseSkill.Subscribe(_GraveSkill);
    grave.OnCritterUseItem.Subscribe(_GraveUseOnMe);
}

bool _GraveSkill(Item grave, Critter cr, CritterProperty skill)
{
    if (skill == SKILL_PICK_ON_GROUND || skill == CritterProperty::SkillLockpick) {
        if (grave.Opened)
            Lockers::ShowContainer(cr, null, grave, TransferTypes::HexContDown);
        else
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrUseNothing);
        return false;
    }
    return true;
}

bool _GraveUseOnMe(Critter cr, Item usedItem, Critter onCritter, Item grave, const Item onScenery, uint param)
{
    if (valid(usedItem) && usedItem.ProtoId == Content::Item::shovel) {
        Lockers::SwitchLocker(grave, !grave.Opened);
        if (grave.Opened) {
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrGraveUncovered);
            cr.Karma -= 5;
            cr.KarmaPerkGraveDigger = 1;
        }
        else {
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrGraveCovered);
        }
        return true;
    }
    return false;
}
