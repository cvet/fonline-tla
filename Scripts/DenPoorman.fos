namespace DenPoorman
{

#if SERVER

// Author: cvet

///@ Property Critter PrivateServer ident DenPoormanItemId

const hstring[] EatPid = {Content::Item::mutated_fruit,
                          Content::Item::iguana_on_a_stick,
                          Content::Item::meat_on_a_stick,
                          Content::Item::beer,
                          Content::Item::box_of_noodles,
                          Content::Item::frozen_dinner,
                          Content::Item::meat_jerky,
                          Content::Item::cheezy_poofs,
                          Content::Item::cookie,
                          Content::Item::smitty_meal,
                          Content::Item::meat};

void _PoormanInit(Critter poorman, bool firstTime)
{
    poorman.OnItemOnMapAppeared.Subscribe(_PoormanShowItemOnMap);
    poorman.OnItemOnMapDisappeared.Subscribe(_PoormanHideItemOnMap);
    poorman.DenPoormanItemId = ZERO_IDENT; // Тут будет хранится Id итема, который мы хотим съесть.
}

void _PoormanShowItemOnMap(Critter poorman, Item item, Critter dropper)
{
    // Можем ли мы сейчас подойти.
    if (poorman.DenPoormanItemId != ZERO_IDENT) {
        return;
    }
    if (not NpcPlanes::IsNoPlanes(poorman)) {
        return;
    }

    // Смотрим, съедобен ли итем.
    for (uint i = 0; i < EatPid.length(); i++) {
        if (item.ProtoId != EatPid[i]) {
            continue;
        }

        Messaging::SayOnHead(poorman, "Ням-ням, вкусняшка.");
        NpcPlanes::AddPickPlane(poorman, 0, item, ZERO_IDENT, true);
        poorman.DenPoormanItemId = item.Id;
    }
}

void _PoormanHideItemOnMap(Critter poorman, Item item, Critter picker)
{
    // Проверим, может кто-то поднял нашу еду.
    if (poorman.DenPoormanItemId != item.Id) {
        return;
    }

    poorman.DenPoormanItemId = ZERO_IDENT;

    if (not valid(picker)) {
        return;
    }

    // Ругаем поднявшего.
    if (picker.Id != poorman.Id) {
        _NpcEraseAllPlanesPick(poorman);
        CritterState::Wait(poorman, 3000);
        Messaging::SayOnHead(poorman, "Подлец.");
    }
    // Это мы сами подняли, едим.
    else {
        poorman.DestroyItem(item.ProtoId, item.Count);
        Messaging::SayOnHead(poorman, "Ммм, вкусно!");
        CritterState::Wait(poorman, 3000);
    }
}

#endif

}
