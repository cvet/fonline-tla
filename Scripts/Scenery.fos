namespace Scenery
{

#if SERVER

// Author: cvet, heX, Tab10id

void ModuleInit()
{
    Game.OnStaticItemWalk.Subscribe(OnMapExit);
}

void OnMapExit(StaticItem item, Critter cr, bool isIn, uint8 moveDir)
{
    if (!isIn) {
        return;
    }
    if (item.Type != ItemType::Grid) {
        return;
    }
    if (!cr.ControlledByPlayer) {
        return;
    }
    if (IS_TIMEOUT(cr.TimeoutBattle) || IS_TIMEOUT(cr.TimeoutTransfer)) {
        return;
    }

    hstring mapProto = item.Grid_ToMap;
    if (mapProto != EMPTY_HSTRING) {
        Map map = cr.GetMap().GetLocation().GetMap(mapProto);
        if (map == null) {
            ThrowException("Invalid exit map.", cr, mapProto);
        }

        if (!map.GetLocation().Hidden) {
            auto knownLocations = cr.KnownLocations.clone();
            if (knownLocations.find(map.GetLocation().Id) == -1) {
                knownLocations.insertLast(map.GetLocation().Id);
                cr.KnownLocations = knownLocations;
            }
        }

        Obsolete::CritterTransitToMapEntry(cr, map.Id, item.Grid_ToMapEntry);
    }
    else {
        cr.TransitToGlobal();
    }
}

bool s_Dialog(Critter player, StaticItem scenery, Item item, any skill) // 691 revision CritterProperty skill )
{
    // Запускаем диалог, если игрок не применял к сценери скилл или итем
    if (skill == CritterProperty::SpecialSkillPickOnGround && not valid(item)) {
        hstring dialogId = scenery.SceneryParams[0];
        return Game.RunDialog(player, dialogId, scenery.HexX, scenery.HexY, false);
    }
    return false;
}

bool s_DoorControl(Critter player, StaticItem scenery, Item item, any skill)
{
    auto params = scenery.SceneryParams;
    hstring entryNum = params[0];
    int toOpen = params[1].uhash;

    if (toOpen == 1) {
        Stdlib::DoorControl(player, entryNum, true);
    }
    else {
        Stdlib::DoorControl(player, entryNum, false);
    }
    return true;
}

bool s_TransitToMap(Critter player, StaticItem scenery, Item item, any skill)
{
    auto params = scenery.SceneryParams;
    hstring locPid = params[0];
    uint mapIndex = params[1].uhash;
    uint entry = params[2].uhash;
    return Stdlib::TransitToMap(player, locPid, mapIndex, entry);
}

#endif

}
