namespace SmokeGrenade // Sort 2
{

#if SERVER

// Author: rifleman17

///@ Property Item PrivateServer ident SmokeGrenadeOwnerId

#define MAX_STEP            (5)
#define SMOKE_TOHIT_PENALTY #(attacker)(attacker.ControlledByPlayer ? 80 : 40)
#define GAS_TOHIT_PENALTY   #(attacker)(attacker.ControlledByPlayer ? 50 : 25)
#define RND                 #(v1, v2)(Game.Random(v1, v2) == 1)

int GetSmokePenalty(Map map, Critter attacker, Critter target)
{
    if (map != null) {
        if (valid(attacker) && valid(map.GetItem(attacker.Hex, Content::Item::smoke))) {
            return SMOKE_TOHIT_PENALTY(attacker);
        }
        if (valid(target) && valid(map.GetItem(target.Hex, Content::Item::smoke))) {
            return SMOKE_TOHIT_PENALTY(attacker);
        }
        if (valid(attacker) && valid(map.GetItem(attacker.Hex, Content::Item::mustard_gas))) {
            return GAS_TOHIT_PENALTY(attacker);
        }
        if (valid(target) && valid(map.GetItem(target.Hex, Content::Item::mustard_gas))) {
            return GAS_TOHIT_PENALTY(attacker);
        }
    }
    return 0;
}

void SmokeBlast(Map map, mpos hex, hstring smokePid, ident ownerId)
{
    any[] values = {map.Id, hex.x, hex.y, 0, smokePid.hash, ownerId};
    Game.StartTimeEvent(ZERO_TIMESPAN, DoSmokeBlast, values);
}

void DoSmokeBlast(any[] values)
{
    Map map = Game.GetMap(ident(values[0]));
    mpos hex = mpos(values[1], values[2]);
    int step = values[3];
    hstring pid = hstring_fromHash(values[4]);
    ident ownerId = values[5];
    int penaltyLowSkill = 0;
    if (ownerId != ZERO_IDENT) {
        Critter cr = Game.GetCritter(ownerId);
        if (valid(cr)) {
            penaltyLowSkill = 4 - int(CLAMP(cr.SkillThrowing, 0, 100) / 25);
        }
    }
    if (step > (MAX_STEP - penaltyLowSkill)) {
        return;
    }

    ident[] smokeIds = {};
    map.PlaySound("gas.wav", hex, 25);
    AddSmoke(map, hex, (step == 0 || (step < 2 && RND(1, 2))), pid, ownerId, smokeIds);

    for (int curStep = 1; curStep < step; curStep++) {
        hex = mpos(values[1], values[2]);
        map.MoveHexByDir(hex, 4, curStep);
        for (int i = 0; i < 6; i++) {
            for (int j = 0; j < curStep; j++) {
                map.MoveHexByDir(hex, i, 1);
                AddSmoke(map, hex, (curStep + 2 > step) || ((curStep + 3 > step) && RND(1, 2)) || RND(1, 3), pid, ownerId, smokeIds);
            }
        }
    }

    if (smokeIds.length() > 0) {
        any[] smokeIdsToDel = {};
        for (int i = 0; i < smokeIds.length(); i++) {
            smokeIdsToDel.insertLast(smokeIds[i]);
        }

        Game.StartTimeEvent(Tla::GameSeconds(Game.Random(4, 6) * (12 - step) * (5 - penaltyLowSkill) / 5), DeleteSmokes,
                            smokeIdsToDel); //SavedDeferredCall
    }

    values[3] = int(values[3]) + 1;

    Game.StartTimeEvent(Tla::GameSeconds(1), DoSmokeBlast, values);
}

void DeleteSmokes(any[] values)
{
    for (int i = 0, l = values.length(); i < l; i++) {
        if (values[i] == 0) {
            continue;
        }
        Item item = Game.GetItem(values[i]);
        if (valid(item)) {
            Game.DestroyItem(item);
        }
    }
}

void _SmokeInit(Item item, bool firstTime)
{
    if (!firstTime) {
        Game.DestroyItem(item);
    }
    else {
        item.NoBlock = true;
        item.ShootThru = true;
        if (item.ProtoId == Content::Item::mustard_gas) {
            item.OnCritterWalk.Subscribe(_MustardGasWalk);
        }
        // item.OnFinish.Subscribe(_SmokeFinish);
    }
}

void _MustardGasWalk(Item mine, Critter cr, bool entered, uint8 dir)
{
    if (entered) {
        if (cr.ControlledByPlayer && !cr.IsNoPvp) {
            Poison::AffectPoison(cr, Game.Random(3, 5));
        }
        else {
            if (!cr.IsInvulnerable) {
                Combat::InjureCritter(cr, Game.Random(1, 2), DamageTypes::Normal, 0, mine.SmokeGrenadeOwnerId);
            }
        }
    }
}

void AddSmoke(Map map, mpos hex, bool place, hstring pid, ident ownerId, ident[] ids)
{
    if (valid(map) && place && (map.IsHexMovable(hex) || valid(map.GetCritter(hex)))) {
        Item item = map.AddItem(hex, pid, 1);
        if (valid(item)) {
            item.SmokeGrenadeOwnerId = ownerId;
            ids.insertLast(item.Id);
            item.SetupScript(_SmokeInit);
        }
    }
}

void SmokeBlast(Critter cr, hstring pid)
{
    SmokeBlast(cr.GetMap(), cr.Hex, pid, cr.Id);
}

#endif

}
