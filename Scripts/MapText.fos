namespace MapText
{

/// Setting Client uint TextDelay
///@ Setting Client string PlayerOffAppendix

///@ Property Critter PrivateClient string TextOnHead Temporary
///@ Property Critter PrivateClient uint TextOnHeadEndTime Temporary

#if CLIENT

void ModuleInit()
{
    Game.OnPreRenderIface.Subscribe(OnPreRenderIface);
}

void OnPreRenderIface()
{
    if (CurMap != null) {
        Critter[] critters = CurMap.GetCritters(CritterFindType::NonDead);
        critters = Game.SortCrittersByDeep(critters);

        for (uint i = 0; i < critters.length(); i++) {
            Critter cr = critters[i];
            DrawInfo(cr);
        }
    }
}

void DrawInfo(Critter cr)
{
    int headX = 0;
    int headY = 0;

    if (!cr.GetTextPos(ipos(headX, headY))) {
        return;
    }

    bool drawSpeech = Game.GetTick() < cr.TextOnHeadEndTime;
    string text = drawSpeech ? cr.TextOnHead : (cr.IsOffline() ? cr.Name + Settings.PlayerOffAppendix : cr.Name);

    int textFont = FONT_DEFAULT;
    uint textFlags = FT_CENTERX | FT_BOTTOM | FT_BORDERED;
    int textWidth = 0;
    int textHeight = 0;
    int textLines = 0;
    Game.GetTextInfo(text, isize(200, 500), textFont, textFlags, isize(textWidth, textHeight), textLines);

    ucolor textColor = drawSpeech ? COLOR_TEXT : (cr.ControlledByPlayer ? COLOR_PLAYER_NAME : COLOR_CRITTER_NAME);
    uint8 crAlpha = cr.GetAlpha();
    ucolor spriteColor = COLOR_NEUTRAL;
    textColor.alpha = crAlpha;
    spriteColor.alpha = crAlpha;

    Game.DrawText(text, ipos(headX - textWidth / 2, headY - textHeight), isize(textWidth, textHeight), textColor, textFont, textFlags);
}

#endif

}
