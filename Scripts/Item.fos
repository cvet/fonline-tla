namespace Item // Sort 2
{

#if SERVER

// Author: rifleman17

///@ Property Critter PrivateServer ident=>int32 GuardedItemSkill

///@ Property Item PrivateServer int32 DoorAutoCloseTime
///@ Property Item PrivateServer hstring DoorAutoDialog

// Дверь, которая зпкроется через 2 игровые минуты илии через некоторое число реальных секунд , указанное в DoorAutoCloseTime
void _DoorAutoCloseInit(Item door, bool firstTime)
{
    if (firstTime) {
        door.DoorAutoCloseTime = door.Val3;
    }
    door.OnCritterUseSkill.Subscribe(e_UseAutoCloseDoor);
}

bool e_UseAutoCloseDoor(Item door, Critter cr, CritterProperty skill)
{
    Game.StartTimeEvent(Time::Seconds(door.DoorAutoCloseTime == 0 ? 2 * 60 : door.DoorAutoCloseTime), AutoCloseDoor, door.Id); //SavedDeferredCall
    return false;
}

void AutoCloseDoor(any value)
{
    Item door = Game.GetItem(value);
    if (not valid(door)) {
        return;
    }
    if (!door.Opened) {
        return;
    }

    mpos hex;
    Map map = door.GetMapPosition(hex);
    if (not valid(map)) {
        return;
    }

    Critter cr = map.GetCritter(hex);
    if (valid(cr)) {
        if (cr.IsAlive()) {
            Game.StartTimeEvent(Time::Seconds(3), AutoCloseDoor, value); //SavedDeferredCall
            return;
        }
        else {
            // Попытка сдвинуть труп. Если не получится, дверь поверх трупа закроется
            cr.TransferToMap(map, mpos(hex.x - 1, hex.y), cr.Dir);
        }
    }

    if (!Lockers::SwitchLocker(door, false)) {
        Game.StartTimeEvent(Time::Seconds(3), AutoCloseDoor, value); //SavedDeferredCall
    }
}

// Дверь, которая не открывается стандартными способами
void _ClosedDoorInit(Item door, bool firstTime)
{
    door.OnCritterUseSkill.Subscribe(_UseDoor);
}

bool _UseDoor(Item item, Critter cr, CritterProperty skill)
{
    return false;
}

// Дверь, при использовании которой открывается диалог
void _DialogDoorInit(Item door, bool firstTime)
{
    if (firstTime) {
        door.DoorAutoDialog = hstring_fromHash(door.Val3);
    }
    door.OnCritterUseSkill.Subscribe(_UseDialogDoor);
}

bool _UseDialogDoor(Item door, Critter cr, CritterProperty skill)
{
    Dialogs::RunDialog(cr, door.DoorAutoDialog, false);
    return true;
}

// Голодиск с номером, присвоенном в Val0
void _HoloInit(Item item, bool firstTime)
{
    if (item.ProtoId != Content::Item::holodisk) {
        return;
    }
    item.HolodiskNum = item.Val0;
    item.SetupScript(null);
}

// Настройка автоматического удаления предмета через икс часов
void SetDestroyItem(Item item, int delHours)
{
    Game.StartTimeEvent(Time::GameHours(delHours), DeferredDestroyItem, item.Id); //SavedDeferredCall
}

void DeferredDestroyItem(any value)
{
    Item item = Game.GetItem(value);
    if (valid(item)) {
        Game.DestroyItem(item);
    }
}

// Охраняемая дверь или другой предмет
// в Val0 - пишется время автоматического закрытия двери, в реальных минутах, если больше 0, дверь будет автоматически закрываться
// обратите внимание, игрок может застрять за дверью, если пройдет внутрь, а она захлопнется
// в Val1 прописывается номер роли НПЦ которые будут говорить, должно быть отличным от ноля
// если скрипт только на одно использование - в Val2 - нужно указать любое значение, это может использоваться для охраняемых предметов, например оружия
#define STR_DONTOUCH (540)

void _GuardedItemInit(Item item, bool firstTime)
{
    item.OnCritterUseSkill.Subscribe(_GuardedItemSkill);
}

bool _GuardedItemSkill(Item item, Critter cr, CritterProperty skill)
{
    Map map = cr.GetMap();
    if (!valid(map)) {
        return false;
    }

    int role = item.Val1;
    bool reset = (item.Val2 > 0 ? true : false);
    if (cr.ControlledByPlayer) {
        Critter[] guards = map.GetCritters(CritterProperty::NpcRole, role, CritterFindType::NonDeadNpc);
        if (guards.isEmpty()) {
            if (reset) {
                item.OnCritterUseSkill.Unsubscribe(_GuardedItemSkill);
                item.SetupScript(null);
            }

            if (item.Type == ItemType::Door || item.Type == ItemType::Container) {
                if (item.Val0 > 0) {
                    Game.StartTimeEvent(Time::Seconds(item.Val0 == 0 ? 2 * 60 : item.Val0), AutoCloseDoor, item.Id); //SavedDeferredCall
                }
            }

            return false;
        }

        Critter guard = guards[Game.Random(0, guards.length() - 1)];
        auto guardedItemSkill = guard.GuardedItemSkill.clone();
        guardedItemSkill.setIfNotExist(cr.Id, 0);
        if (guardedItemSkill[cr.Id] < 2) {
            NpcPlanes::NpcPlane[] planes = {};
            if (NpcPlanes::GetPlanes(guard, AI_PLANE_ATTACK, planes) > 0) {
                for (int i = 0, l = planes.length(); i < l; i++) {
                    if (planes[i].Attack_TargId == cr.Id) {
                        if (reset) {
                            item.OnCritterUseSkill.Unsubscribe(_GuardedItemSkill);
                            item.SetupScript(null);
                        }

                        return false;
                    }
                }
            }

            guardedItemSkill[cr.Id] = guardedItemSkill[cr.Id] + 1;
            guard.GuardedItemSkill = guardedItemSkill;
            Messaging::SayShout(guard, TextPackName::Text, STR_DONTOUCH, "$pname" + Obsolete::GetPlayerName(cr.Id));
        }
        else {
            guardedItemSkill[cr.Id] = 0;
            guard.GuardedItemSkill = guardedItemSkill;
            NpcPlanes::AddAttackPlane(guard, 0, cr);
        }

        return true;
    }
    else {
        // Нпц такие двери открыть тоже не смогут
        return true;
    }
}

Item ChangeProto(Item item, hstring newProto)
{
    // Todo: improve changing proto by deletion old item and create new
    return item;
}

#endif

}
