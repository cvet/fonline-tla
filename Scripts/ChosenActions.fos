namespace ChosenActions
{

///@ RemoteCall Server PickItem(mpos hex, hstring pid)
///@ RemoteCall Server PickCritter(ident crId)
///@ RemoteCall Server MoveInvItem(ident itemId, CritterItemSlot slot)
///@ RemoteCall Server DropInvItem(ident itemId, int32 count)
///@ RemoteCall Server ReloadWeapon(ident itemId, ident ammoId)
///@ RemoteCall Server OperateContainer(bool take, bool all, ident itemId, int32 itemCount)
///@ Property Critter Common nanotime ApRegenerationTick Mutable NoSync
///@ Property Critter Common int32 ApRegenerationTime Mutable OwnerSync Persistent
///@ RemoteCall Server UseSkill(CritterProperty skill, ident targetCrId, ident targetItemId, hstring sceneryPid, mpos hex)
///@ RemoteCall Server UseItemOn(ident usedItemId, ident targetCrId, ident targetItemId, hstring sceneryPid, mpos hex, int32 timer)
///@ RemoteCall Server Attack(ident targetId, int32 mode)
/// RemoteCall Server Attack(ident targetId, int32 mode, mpos hex)
/// RemoteCall Server UseSneak(bool enable)
///@ Event Common Game OnClientOperateContainer(Critter cr, Entity container, Item item, bool take)
///@ Event Server Game OnCritterPickItem(Critter cr, Item item)
///@ Event Server Game OnCritterPickScenery(Critter cr, StaticItem scenery)

#if CLIENT

any[] ChosenActions = {};

void ModuleInit()
{
    Game.OnLoop.Subscribe(ChosenProcess);
}

int GetChosenActions(any[] & actions)
{
    if (actions !is null) {
        actions = ChosenActions.clone();
    }
    return ChosenActions.length();
}

void SetChosenActions(any[] actions)
{
    ChosenActions.resize(0);
    if (actions !is null) {
        Assert(actions.length() % 7 == 0);
        ChosenActions = actions.clone();

        Critter chosen = Game.GetChosen();
        if (chosen is null) {
            return;
        }
        int actionType = actions[0];
        if (actionType == Tla::ChosenMove) {
            chosen.StopMove();
        }
    }
}

void SetAction(any[] action, bool append, bool toBeginning)
{
    Assert(action.length() == 7);

    if (!append) {
        ChosenActions.resize(0);
    }
    if (toBeginning) {
        for (int i = int(action.length()) - 1; i >= 0; i--) {
            ChosenActions.insertFirst(action[i]);
        }
    }
    else {
        for (int i = 0; i < action.length(); i++) {
            ChosenActions.insertLast(action[i]);
        }
    }
}

#define CHECK_NEED_AP \
    #(needAp) if (chosen.ActionPoints < int(needAp)) \
    { \
        Messaging::Info(Game.ReplaceText(Game.GetText(TextPackName::Combat, MsgStr::StrCombatNeedAp), "%d", needAp)); \
        break; \
    } \
    if (chosen.CurrentAp / AP_DIVIDER < int(needAp)) \
        return;

#define CHECK_NEED_AP2 \
    #(needAp) if (chosen.ActionPoints < int(needAp)) \
    { \
        Messaging::Info(Game.ReplaceText(Game.GetText(TextPackName::Combat, MsgStr::StrCombatNeedAp), "%d", needAp)); \
        return true; \
    } \
    if (chosen.CurrentAp / AP_DIVIDER < int(needAp)) \
        return false;

void ChosenProcess()
{
    Critter chosen = Game.GetChosen();
    if (chosen is null) {
        return;
    }

    if (CurMap !is null) {
        // Roof
        CurMap.SetHiddenRoof(chosen.Hex);

        // Hidden mode
        chosen.SetAlpha(chosen.InSneakMode ? 130 : 255);

        // Shoot borders
        bool shootBorders = Settings.Cursor == CursorType::UseWeapon;
        int attackDist = Parameters::GetActiveItemAttackDistance(chosen);
        CurMap.SetShootBorders(shootBorders, attackDist);

        if (chosen.IsMoving()) {
            return;
        }
    }

    // Actions
    if (!IsFree(chosen)) {
        return;
    }

    // Ap regeneration
    if (chosen.CurrentAp < chosen.ActionPoints * AP_DIVIDER) {
        nanotime tick = Game.FrameTime;
        if (chosen.ApRegenerationTick == ZERO_NANOTIME) {
            chosen.ApRegenerationTick = tick;
        }

        timespan delta = tick - chosen.ApRegenerationTick;
        if (delta >= Time::Milliseconds(500)) {
            chosen.ApRegenerationTick = tick;

            int apRegeneration = chosen.ApRegenerationTime;
            if (apRegeneration == 0) {
                apRegeneration = Settings.ApRegeneration;
            }

            int maxAp = chosen.ActionPoints * AP_DIVIDER;
            chosen.CurrentAp = chosen.CurrentAp + maxAp * delta.milliseconds / apRegeneration;
            if (chosen.CurrentAp > int(maxAp)) {
                chosen.CurrentAp = maxAp;
            }
        }
    }

    if (chosen.CurrentAp / AP_DIVIDER > chosen.ActionPoints) {
        chosen.CurrentAp = chosen.ActionPoints * AP_DIVIDER;
    }
    if (ChosenActions.length() == 0) {
        return;
    }
    if (!chosen.IsAlive()) {
        ChosenActions.clear();
        return;
    }

    any[] curAction = ChosenActions.clone();
    curAction.resize(7);
    int actionType = curAction[0];

    // Force end move
    if (actionType != Tla::ChosenMove && actionType != Tla::ChosenMoveToCrit) {
        chosen.StopMove();
    }

    switch (actionType) {
    case Tla::ChosenMoveToCrit:
    case Tla::ChosenMove: {
        mpos hex;
        bool isRun = (ChosenActions[3] != 0);
        int cut = ChosenActions[4];
        int startTick = ChosenActions[6];

        if (CurMap is null) {
            break;
        }

        // Move to critter
        if (actionType == Tla::ChosenMoveToCrit) {
            Critter cr = Game.GetCritter(ChosenActions[1]);
            if (cr !is null) {
                hex = cr.Hex;
            }
        }
        else {
            hex = mpos(ChosenActions[1], ChosenActions[2]);
        }

        // Already here
        if (Game.GetDistance(chosen.Hex, hex) <= cut) {
            break;
        }

        // Can't move
        if (chosen.IsNoMove) {
            Messaging::Info(TextPackName::Game, MsgStr::StrCritterCantMove);
            break;
        }

        // Check running
        if (isRun) {
            if (!Settings.RunOnCombat && Time::IsTimeoutActive(chosen.TimeoutBattle)) {
                isRun = false;
            }
            else if (!Settings.RunOnTransfer && Time::IsTimeoutActive(chosen.TimeoutTransfer)) {
                isRun = false;
            }
            else if (chosen.IsDamagedRightLeg || chosen.IsDamagedLeftLeg || chosen.IsOverweight) {
                isRun = false;
            }
            else if (chosen.IsNoRun) {
                isRun = false;
            }
        }

        // Find path
        auto path = CurMap.GetPath(chosen.Hex, hex, cut);
        if (path.isEmpty() && Game.GetDistance(chosen.Hex, hex) > cut + 1) {
            path = CurMap.GetPath(chosen.Hex, hex, ++cut);
        }

        // Move to
        if (!path.isEmpty()) {
            hex = chosen.Hex;
            for (int i = 0; i < path.length(); i++) {
                CurMap.MoveHexByDir(hex, path[i], 1);
            }

            chosen.MoveToHex(hex, ipos(0, 0), isRun ? chosen.WalkSpeed * 2 : chosen.WalkSpeed);

            // chosen.ApRegenerationTick = 0;
        }
        if (chosen.IsMoving()) {
            return;
        }
    } break;
    case Tla::ChosenDir: {
        bool cw = (ChosenActions[1] == 0);

        if (CurMap is null) {
            break;
        }

        uint8 dir = chosen.Dir;
		if(ChosenActions[2] != 0)
		{
			dir = ChosenActions[3];
		}else{
			if (cw) {
				dir++;
				if (dir >= 6) {
					dir = 0;
				}
			}
			else {
				dir--;
				if (dir < 0) {
					dir = 5;
				}
			}
		}

        chosen.ChangeDir(dir);
    } break;
	case Tla::ChosenUnloadWeapon:
	{
		ident weaponId = ChosenActions[1];
		auto weapon = chosen.GetItem(weaponId);
		if(weapon is null || weapon.Type != ItemType::Weapon || weapon.Weapon_MaxAmmoCount == 0 || weapon.AmmoCount == 0)
		{
			break;
		}
		int apCost = Parameters::GetItemUseApCost(chosen, weapon, USE_RELOAD);
        CHECK_NEED_AP(apCost);
		CurPlayer.ServerCall.ReloadWeapon(weapon.Id, ZERO_IDENT);
        chosen.CurrentAp -= apCost * AP_DIVIDER;
	} break;
    case Tla::ChosenUseItem: {
        ident itemId = ChosenActions[1];
        int targetType = ChosenActions[3];
        ident targetId = ChosenActions[4];
        int rate = ChosenActions[5];
        int param = ChosenActions[6];
        int use = (rate & 0xF);
        int aim = (rate >> 4);

        // Find item
        AbstractItem item = (itemId != ZERO_IDENT ? cast<AbstractItem>(chosen.GetItem(itemId)) : CritterItem::GetSlotItemProto(chosen));
        if (item is null) {
            break;
        }

        Item realItem = cast<Item>(item);
        Item mainslotItem = chosen.GetItem(ItemProperty::CritterSlot, CritterItemSlot::Main);
        bool isMainItem = (realItem == null || (mainslotItem != null && realItem.Id == mainslotItem.Id));

        // Find target
        Critter targetCr;
        Item targetItem;
        Item targetSelfItem;
        if (targetType == TARGET_SELF) {
            targetCr = chosen;
        }
        else if (targetType == TARGET_SELF_ITEM) {
            targetSelfItem = chosen.GetItem(targetId);
        }
        else if (targetType == TARGET_CRITTER) {
            targetCr = Game.GetCritter(targetId);
        }
        else if (targetType == TARGET_ITEM) {
            targetItem = Game.GetItem(targetId);
        }
        else {
            break;
        }

        if (targetType == TARGET_CRITTER && chosen is targetCr) {
            targetType = TARGET_SELF;
        }
        if (targetType == TARGET_SELF) {
            targetId = chosen.Id;
        }

        // Check
        if (targetType == TARGET_CRITTER && targetCr is null) {
            break;
        }
        if (targetType == TARGET_ITEM && targetItem is null) {
            break;
        }
        if (targetType != TARGET_CRITTER && realItem == null) {
            break;
        }

        // Parse use
        bool isAttack = (targetType == TARGET_CRITTER && isMainItem && item.Type == ItemType::Weapon && use < MAX_USES);
        bool isReload = (targetType == TARGET_SELF_ITEM && use == USE_RELOAD && item.Type == ItemType::Weapon);
        bool isSelf = (targetType == TARGET_SELF || targetType == TARGET_SELF_ITEM);

        // Calculate ap cost
        int apCost = Parameters::GetItemUseApCost(chosen, item, rate);
        CHECK_NEED_AP(apCost);

        // Check weapon
        if (isAttack) {
            // ChosenActions::SetChosenActions(array<any> = {Tla::ChosenUseItem, useItem.Id, 0, TARGET_CRITTER, cr.Id, isAttack ? mode : USE_USE, 0});

            if (CurMap is null) {
                break;
            }
            if (isSelf) {
                break;
            }
            if (item.Type != ItemType::Weapon) {
                break;
            }
            if (targetCr.IsDead()) {
                break;
            }
            if (!chosen.IsUnlimitedAmmo && item.Weapon_MaxAmmoCount > 0 && item.AmmoCount == 0) {
                int sound = (use == 0 ? item.Weapon_SoundId_0 : (use == 1 ? item.Weapon_SoundId_1 : item.Weapon_SoundId_2));
                Effects::PlayWeaponSound( SOUND_WEAPON_EMPTY, sound, '1' );
				
                break;
            }
            if (item.Weapon_IsTwoHanded && (chosen.IsDamagedRightArm || chosen.IsDamagedLeftArm)) {
                Messaging::Info(TextPackName::Combat, MsgStr::StrCombatNeedDmgArm);
                break;
            }
            if (chosen.IsDamagedRightArm && chosen.IsDamagedLeftArm && realItem != null) {
                Messaging::Info(TextPackName::Combat, MsgStr::StrCombatNeedDmgTwoArms);
                break;
            }
            if (item.Deteriorable && item.IsBroken) {
                Messaging::Info(TextPackName::Game, MsgStr::StrDeteriorationWeaponBroken);
                break;
            }
        }
        else if (isReload) {
            if (!isSelf) {
                break;
            }
            CHECK_NEED_AP(apCost);
            if (isMainItem) {
                Parameters::SetWeaponMode(item, USE_PRIMARY);
            }

            Gui::Screen screen = Gui::GetActiveScreen();
            if (screen == null || screen.Num == GuiScreen::None || screen.Num == GuiScreen::Game) {
                Settings.Cursor = CursorType::UseWeapon;
            }

            if (item.Weapon_MaxAmmoCount == 0) {
                break; // No have holder
            }

            if (targetSelfItem is null) { // Reload
                if (item.AmmoCount >= item.Weapon_MaxAmmoCount) {
                    break; // Is full
                }
                if (item.Weapon_Caliber != 0) {
                    hstring ammoPid = item.AmmoPid != EMPTY_HSTRING ? item.AmmoPid : item.Weapon_DefaultAmmoPid;
                    Item ammoCaliber;
                    Item[] items = chosen.GetItems(ItemProperty::Type, ItemType::Ammo);
                    for (int i = 0; i < items.length(); i++) {
                        if (items[i].ProtoId == ammoPid) {
                            targetSelfItem = items[i];
                            break;
                        }

                        if (ammoCaliber == null && items[i].Ammo_Caliber == item.Weapon_Caliber) {
                            ammoCaliber = items[i];
                        }
                    }
                    if (targetSelfItem == null) {
                        if (ammoCaliber != null) {
                            targetSelfItem = ammoCaliber;
                        }
                        else {
                            break;
                        }
                    }
                    targetId = targetSelfItem.Id;
                }
                /*if( item.Weapon_Caliber )
                   {
                    targetSelfItem = chosen->GetAmmoAvialble( item );
                    if( !targetSelfItem )
                        break;
                    targetId = targetSelfItem->GetId();
                   }*/
            }
            else // Load
            {
                if (item.Weapon_Caliber != targetSelfItem.Ammo_Caliber) {
                    break; // Different caliber
                }
                if (item.AmmoPid == targetSelfItem.ProtoId && item.AmmoCount == item.Weapon_MaxAmmoCount) {
                    break; // Is full
                }
            }

            CurPlayer.ServerCall.ReloadWeapon(realItem.Id, targetId);
            chosen.CurrentAp -= apCost * AP_DIVIDER;
        }
        else // Use
        {
            if (use != USE_USE) {
                break;
            }
        }

        // Find Target
        if (!isSelf && CurMap !is null) {
            int hx, hy;
            if (targetCr !is null) {
                hx = targetCr.Hex.x;
                hy = targetCr.Hex.y;
            }
            else {
                hx = targetItem.Hex.x;
                hy = targetItem.Hex.y;
            }

            int maxDist = 0;
            if (isAttack) {
                AbstractItem weap = CritterItem::GetActive(chosen);
                // TODO: проверить, что работает для рукопашных ударов.
                maxDist = Parameters::GetCritterAttackDistance(chosen, weap, weap.Mode);
            }
            else {
                maxDist = 1 + chosen.Multihex;
            }
            if (targetCr !is null) {
                maxDist += targetCr.Multihex;
            }

            // Target find
            int dist = Game.GetDistance(chosen.Hex, mpos(hx, hy));
            bool needMove = dist > maxDist;
            // if( isAttack )
            //    needMove = !CurMap->TraceBullet( chosen.HexX, chosen.HexY, hx, hy, maxDist, 0.0f, targetCr, false, nullptr, 0, nullptr, nullptr, nullptr, true
            //    );
            // else
            //    needMove = !CheckDist( chosen.HexX, chosen.HexY, hx, hy, maxDist );
            // TODO: r17 trace
            if (needMove) {
                if (dist > maxDist) {
                    bool isRun = false; // 814 ( Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist );
                    if (targetCr !is null) {
                        SetAction(array<any> = {Tla::ChosenMoveToCrit, targetCr.Id, 0, isRun ? 1 : 0, maxDist, 0, 0}, false, false);
                    }
                    else {
                        SetAction(array<any> = {Tla::ChosenMove, hx, hy, isRun ? 1 : 0, maxDist, 0, 1}, false, false);
                    }

                    if (CurMap.GetPathLength(chosen, mpos(hx, hy), maxDist) > 0) {
                        SetAction(curAction, true, false);
                    }

                    return;
                }

                Messaging::Info(TextPackName::Game, MsgStr::StrFindpathAimblock);
                break;
            }

            // Refresh orientation
            CHECK_NEED_AP(apCost);
            uint8 dir = Game.GetDirection(chosen.Hex, mpos(hx, hy));
            if (Game.GetDistance(chosen.Hex, mpos(hx, hy)) >= 1 && chosen.Dir != dir) {
                chosen.ChangeDir(dir);
            }
        }

        // Use
        CHECK_NEED_AP(apCost);

        /*if( targetItem && targetItem->IsGenericOrGrid() )
            Net_SendUseItem( apCost, itemId, rate, targetType, ( targetItem.HexX << 16 ) | ( targetItem.HexY & 0xFFFF ), targetItem->GetProtoId(), param );
           else
            Net_SendUseItem( apCost, itemId, rate, targetType, targetId, 0, param );  // Item or critter
         */
        if (use != USE_RELOAD && !isAttack) {
            if (targetType == TARGET_ITEM || targetType == TARGET_SELF_ITEM) {
                if (targetType == TARGET_SELF_ITEM) {
                    targetItem = targetSelfItem;
                }

                if (targetItem.IsScenery) {
                    CurPlayer.ServerCall.UseItemOn(itemId, ZERO_IDENT, ZERO_IDENT, targetItem.ProtoId, targetItem.Hex, param);
                }
                else {
                    CurPlayer.ServerCall.UseItemOn(itemId, ZERO_IDENT, targetId, EMPTY_HSTRING, ZERO_MPOS, param);
                }
            }
            else {
                CurPlayer.ServerCall.UseItemOn(itemId, targetId, ZERO_IDENT, EMPTY_HSTRING, ZERO_MPOS, param);
            }
        }
        if (use >= USE_PRIMARY && use <= USE_THIRD) {
            Game.OnCritterActionEx.Fire(true, chosen, CritterAction::UseWeapon, rate, item);
        }
        else if (use == USE_RELOAD) {
            Game.OnCritterActionEx.Fire(true, chosen, CritterAction::ReloadWeapon, 0, item);
        }
        else {
            Game.OnCritterActionEx.Fire(true, chosen, CritterAction::UseItem, 0, item);
        }
        chosen.CurrentAp -= apCost * AP_DIVIDER;
        if (isAttack && valid(targetCr)) {
            CurPlayer.ServerCall.Attack(targetCr.Id, rate);
        }
        /*if( isAttack && !aim && Keyb::ShiftDwn )               // Continue battle after attack
           {
           SetAction( curAction, false, false );
           return;
           }*/
    } break;
    case Tla::ChosenMoveItem: {
        ident itemId = ChosenActions[1];
        int itemCount = ChosenActions[2];
        CritterItemSlot toSlot = CritterItemSlot(int(ChosenActions[3]));
        bool isSecondTry = (ChosenActions[5] != 0);

        Assert(itemCount > 0);

        Item item = chosen.GetItem(itemId);
        if (item is null) {
            break;
        }

        CritterItemSlot fromSlot = item.CritterSlot;
        if (fromSlot == toSlot) {
            break;
        }

        if (toSlot == CritterItemSlot::Outside) {
            if (!Game.OnItemCheckMove.Fire(item, itemCount, chosen, CurMap)) {
                break;
            }
        }

        Item itemSwap = null;
        if (toSlot != CritterItemSlot::Inventory && toSlot != CritterItemSlot::Outside) {
            itemSwap = chosen.GetItem(ItemProperty::CritterSlot, toSlot);
        }

        if (!Game.OnCritterCheckMoveItem.Fire(chosen, item, toSlot)) {
            // Gameplay swap workaround
            if (itemSwap !is null && !isSecondTry) {
                // Add actions
                if (Game.OnCritterCheckMoveItem.Fire(chosen, itemSwap, CritterItemSlot::Inventory) && Game.OnCritterCheckMoveItem.Fire(chosen, item, toSlot)) {
                    ChosenActions.reverse();
                    ChosenActions.resize(ChosenActions.length() - 7);
                    ChosenActions.reverse();

                    SetAction(array<any> = {Tla::ChosenMoveItem, itemId, itemCount, toSlot, 0, 1, 0}, false, true);
                    SetAction(array<any> = {Tla::ChosenMoveItem, itemSwap.Id, itemSwap.Count, CritterItemSlot::Inventory, 0, 1, 0}, false, true);
                    return;
                }
            }
            break;
        }

        // Action points
        int apCost = Settings.RtApCostMoveItemInventory;
        if ((fromSlot == CritterItemSlot::Main && toSlot == CritterItemSlot::Secondary) ||
            (fromSlot == CritterItemSlot::Secondary && toSlot == CritterItemSlot::Main)) {
            apCost = 0;
        }
        if (toSlot == CritterItemSlot::Outside) {
            apCost = Settings.RtApCostDropItem;
        }
        CHECK_NEED_AP(apCost);

        // Game.CustomCall( "MoveItem " + apCost + " " + itemCount + " " + itemId + " " + ( itemSwap !is null ? itemSwap.Id : 0 ) + " " + toSlot );

        //Game.CustomCall("MoveItem " + itemCount + " " + itemId + " " + (itemSwap != null ? itemSwap.Id : ZERO_IDENT) + " " +
        //               (toSlot == CritterItemSlot::Outside ? "-1" : "" + toSlot));
        chosen.CurrentAp -= apCost;

        if (toSlot != CritterItemSlot::Outside) {
            CurPlayer.ServerCall.MoveInvItem(itemId, toSlot);
        }
        else {
            CurPlayer.ServerCall.DropInvItem(itemId, itemCount);
        }

    } break;
    case Tla::ChosenMoveItemCont: {
        ident itemId = ChosenActions[1];
        int itemCont = ChosenActions[2];
        int count = ChosenActions[3];

        Assert(itemCont == ITEMS_PICKUP || itemCont == ITEMS_PICKUP_FROM);
        Assert(count > 0);

        Item @[] cont = ClientItems::RefreshItemsCollection(itemCont);
        Item item;
        for (int i = 0; i < cont.length() && item is null; i++) {
            if (cont[i].Id == itemId) {
                item = cont[i];
            }
        }

        if (count > item.Count) {
            break;
        }

        Entity contEntity;
        TransferTypes transferType = chosen.TransferType;
        ident transferContainerId = chosen.TransferContainerId;
        if (transferContainerId != ZERO_IDENT) {
            if (transferType == TransferTypes::CritLoot || transferType == TransferTypes::CritSteal || transferType == TransferTypes::FarCrit) {
                contEntity = Game.GetCritter(transferContainerId);
            }
            else if (transferType == TransferTypes::HexContUp || transferType == TransferTypes::HexContDown || transferType == TransferTypes::SelfCont ||
                     transferType == TransferTypes::FarCont) {
                contEntity = Game.GetItem(transferContainerId);
            }
        }
        else {
            // Container disappeared
            break;
        }

        auto colType = ClientItems::GetCollectionFromType(itemCont);
        bool take = (colType == ClientItems::Collection::Loot);
        Entity from = (itemCont == ITEMS_PICKUP ? cast<Entity>(chosen) : contEntity);
        Entity to = (itemCont == ITEMS_PICKUP ? contEntity : cast<Entity>(chosen));
        if (!Game.OnItemCheckMove.Fire(item, count, from, to)) {
            break;
        }

        int apCost = Settings.RtApCostMoveItemContainer;
        CHECK_NEED_AP(apCost);

        if (take || cast<Critter>(to) != null) {
            Critter toCr = take ? chosen : cast<Critter>(to);
            if (int(toCr.ItemsWeight) + item.Weight * count > toCr.CarryWeight) {
                Messaging::Info(TextPackName::Game, (take ? MsgStr::StrOverweight : MsgStr::StrOvervolume));
                break;
            }
        }

        // Game.CustomCall( "MoveItemCont " + apCost + " " + count + " " + itemId + " " + itemCont );
        ClientItems::RemoveItem(colType, item, count);

        Game.OnCritterActionEx.Fire(true, chosen, CritterAction::OperateContainer, transferType, item);

        CurPlayer.ServerCall.OperateContainer(take, false, item.Id, count);
    } break;
    /*case Tla::ChosenTakeAll:
       {
        CHECK_NEED_AP( chosen->GetApCostMoveItemContainer() );

        if( PupCont2Init.empty() )
            break;

        int c, w, v;
        ContainerCalcInfo( PupCont2Init, c, w, v, MAX_INT, false );
        if( chosen->GetFreeWeight() < (int) w )
        {
            AddMess( FOMB_GAME, CurLang.Msg[ TextPackName::Game ].GetStr( MsgStr::StrBarterOverweight ) );
            break;
        }
        else if( chosen->GetFreeVolume() < (int) v )
        {
            AddMess( FOMB_GAME, CurLang.Msg[ TextPackName::Game ].GetStr( MsgStr::StrBarterOversize ) );
            break;
        }

        Net_SendItemCont( PupTransferType, PupContId, 0, 0, CONT_GETALL );
        chosen->Action( CritterAction::OperateContainer, PupTransferType * 10 + 1, nullptr );
        chosen->SubAp( chosen->GetApCostMoveItemContainer() );
       }
       break;*/
    /*case Tla::ChosenPutAll:
       {
        CHECK_NEED_AP( chosen->GetApCostMoveItemContainer() );

        if( InvContInit.empty() )
            break;

        Net_SendItemCont( PupTransferType, PupContId, 0, 0, CONT_PUTALL );
        chosen->Action( CritterAction::OperateContainer, PupTransferType * 10 + 3, nullptr );
        chosen->SubAp( chosen->GetApCostMoveItemContainer() );
       }
       break;*/
    case Tla::ChosenSneak: {
        int apCost = Settings.RtApCostUseSkill;
        CHECK_NEED_AP(apCost);

        // Game.CustomCall( "UseCritterSkill " + CritterProperty::UseSneak + " " + apCost + " " + chosen.Id );
    } break;
    case Tla::ChosenUseSklOnItem: {
        CritterProperty skill = CritterProperty(int(ChosenActions[1]));
        ident itemId = ChosenActions[2];
        bool isInv = (ChosenActions[3] > 0);

        Item itemAction = null;

        if (isInv) {
            Item item = chosen.GetItem(itemId);
            if (!valid(item)) {
                break;
            }
            itemAction = item;
            if (item.IsHolodisk) {
                // TODO: запись и чтение голодисков. см CHOSEN_USE_SKL_ON_ITEM cr.cpp
                break;
            }
            CurPlayer.ServerCall.UseSkill(skill, ZERO_IDENT, itemAction.Id, EMPTY_HSTRING, ZERO_MPOS);
        }
        else {
            Item item = Game.GetItem(itemId);
            if (!valid(item)) {
                break;
            }
            itemAction = item;

            if (valid(CurMap)) {
                int hx = itemAction.Hex.x;
                int hy = itemAction.Hex.y;
                int dist = Game.GetDistance(chosen.Hex, mpos(hx, hy));
                int useDist = 1 + chosen.Multihex;
                if (dist > useDist) {
                    bool isRun = false; // 814 ( Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist );
                    SetAction(array<any> = {Tla::ChosenMove, hx, hy, isRun ? 1 : 0, useDist, 0, 1}, false, false);
                    if (CurMap.GetPathLength(chosen, mpos(hx, hy), useDist) > 0) {
                        SetAction(array<any> = {Tla::ChosenUseSklOnItem, int(skill), itemId, ChosenActions[3], 0, 0, 0}, true, false);
                    }

                    return;
                }
            }
            int apCost = Settings.RtApCostUseSkill;
            CHECK_NEED_AP(apCost);
            CurPlayer.ServerCall.UseSkill(skill, ZERO_IDENT, itemAction.Id, EMPTY_HSTRING, ZERO_MPOS);
        }
    } break;
    case Tla::ChosenUseSklOnScen: {
        CritterProperty skill = CritterProperty(int(ChosenActions[1]));
        hstring sceneryProto = hstring_fromHash(ChosenActions[2]);
        int hx = ChosenActions[3];
        int hy = ChosenActions[4];
        if (!UseSkillOnScenerySuccess(skill, chosen, sceneryProto, hx, hy)) {
            return;
        }
    } break;

    case Tla::ChosenUseSklOnCritter: {
        CritterProperty skill = CritterProperty(int(ChosenActions[1]));
        ident crId = ChosenActions[2];

        Critter cr = (crId != ZERO_IDENT ? Game.GetCritter(crId) : chosen);

        if (cr.Id != chosen.Id) {
            if (CurMap is null) {
                break;
            }

            int dist = Game.GetDistance(chosen.Hex, cr.Hex);
            int useDist = 1 + chosen.Multihex;
            if (dist > useDist) {
                bool isRun = false; // 814 ( Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist );
                SetAction(array<any> = {Tla::ChosenMoveToCrit, cr.Id, 0, isRun ? 1 : 0, useDist, 0, 0}, false, false);
                // Возможна ситуация, когда до цели нельзя дойти.
                if (CurMap.GetPathLength(chosen, cr.Hex, useDist) > 0) {
                    SetAction(array<any> = {Tla::ChosenUseSklOnCritter, int(skill), cr.Id, 0, 0, 0, 0}, true, false);
                }

                return;
            }
        }

        int apCost = Settings.RtApCostUseSkill;
        CHECK_NEED_AP(apCost);

        // Refresh orientation
        uint8 dir = Game.GetDirection(chosen.Hex, cr.Hex);
        if (Game.GetDistance(chosen.Hex, cr.Hex) >= 1 && chosen.Dir != dir) {
            chosen.ChangeDir(dir);
        }

        chosen.CurrentAp = chosen.CurrentAp - apCost * AP_DIVIDER;

        Game.OnCritterActionEx.Fire(true, chosen, CritterAction::UseSkill, 0, null);
        CurPlayer.ServerCall.UseSkill(skill, cr.Id, ZERO_IDENT, EMPTY_HSTRING, ZERO_MPOS);
    } break;
    case Tla::ChosenTalkNpc: {
        ident crId = ChosenActions[1];

        Critter cr = Game.GetCritter(crId);
        if (cr is null) {
            break;
        }
        if (cr.IsDead()) {
            break;
        }

        int dist = Game.GetDistance(chosen, cr);
        int talkDistance = (cr.TalkDistance > 0 ? cr.TalkDistance : Settings.TalkDistance) + chosen.Multihex;

        if (dist > talkDistance) {
            bool isRun = false; // 814 ( Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist );
            SetAction(array<any> = {Tla::ChosenMoveToCrit, cr.Id, 0, isRun ? 1 : 0, talkDistance, 0, 0}, false, false);
            int hx = cr.Hex.x;
            int hy = cr.Hex.y;
            if (CurMap.GetPathLength(chosen, mpos(hx, hy), talkDistance) > 0) {
                SetAction(array<any> = {Tla::ChosenTalkNpc, cr.Id, 0, 0, 0, 0, 0}, true, false);
            }

            return;
        }

        // if( !CurMap->TraceBullet( chosen.GetHexX(), chosen.HexY, cr.HexX, cr.HexY, talkDistance + chosen.Multihex, 0, cr, false, nullptr, 0, nullptr,
        // nullptr, nullptr, true ) )
        // {
        //    AddMess( FOMB_GAME, CurLang.Msg[ TextPackName::Game ].GetStr( MsgStr::StrFindpathAimblock ) );
        //    break;
        // }

        // Refresh orientation
        uint8 dir = Game.GetDirection(chosen.Hex, cr.Hex);
        if (Game.GetDistance(chosen.Hex, cr.Hex) >= 1 && chosen.Dir != dir) {
            chosen.ChangeDir(dir);
        }

        CurPlayer.ServerCall.StartDialog(cr.Id);
    } break;
    case Tla::ChosenPickItem: {
        hstring pid = hstring_fromHash(ChosenActions[1]);
        int hx = ChosenActions[2];
        int hy = ChosenActions[3];
        ident itemId = ChosenActions[4];

        if (CurMap is null) {
            break;
        }

        int dist = Game.GetDistance(chosen.Hex, mpos(hx, hy));
        int useDist = 1 + chosen.Multihex;
        if (dist > useDist) {
            bool isRun = false; // 814 ( Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist );
            SetAction(array<any> = {Tla::ChosenMove, hx, hy, isRun ? 1 : 0, useDist, 0, 1}, false, false);

            if (CurMap.GetPathLength(chosen, mpos(hx, hy), useDist) > 0) {
                SetAction(array<any> = {Tla::ChosenPickItem, pid.hash, hx, hy, 0, 0, 0}, true, false);
            }
            return;
        }
        int apCost = Settings.RtApCostPickItem;
        CHECK_NEED_AP(apCost);

        // Refresh orientation
        uint8 dir = Game.GetDirection(chosen.Hex, mpos(hx, hy));
        if (Game.GetDistance(chosen.Hex, mpos(hx, hy)) >= 1 && chosen.Dir != dir) {
            chosen.ChangeDir(dir);
        }

        chosen.CurrentAp = chosen.CurrentAp - apCost * AP_DIVIDER;
        // Game.CustomCall( "PickItem " + apCost + " " + pid + " " + hx + " " + hy );
        CurPlayer.ServerCall.PickItem(mpos(hx, hy), pid);

        if (itemId != ZERO_IDENT) {
            Item item = Game.GetItem(itemId);
            if (item != null) {
                Game.OnCritterActionEx.Fire(true, chosen, CritterAction::PickItem, 0, item);
            }
        }
    } break;
    case Tla::ChosenPickCrit: {
        ident crId = ChosenActions[1];
        bool isLoot = (ChosenActions[2] == 0);

        if (CurMap is null) {
            break;
        }

        Critter cr = Game.GetCritter(crId);
        if (cr is null) {
            break;
        }

        if (isLoot && (!cr.IsDead() || cr.IsNoLoot)) {
            break;
        }
        if (!isLoot && (!cr.IsAlive() || cr.IsNoPush)) {
            break;
        }

        int dist = Game.GetDistance(chosen.Hex, cr.Hex);
        int pickDist = 1 + chosen.Multihex + cr.Multihex;
        if (dist > pickDist) {
            bool isRun = false; // 814 ( Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist );
            SetAction(array<any> = {Tla::ChosenMoveToCrit, cr.Id, 0, isRun ? 1 : 0, pickDist, 0, 0}, false, false);
            if (CurMap.GetPathLength(chosen, cr.Hex, pickDist) > 0) {
                SetAction(curAction, true, false);
            }
            return;
        }

        int apCost = Settings.RtApCostPickCritter;
        CHECK_NEED_AP(apCost);

        // Refresh orientation
        uint8 dir = Game.GetDirection(chosen.Hex, cr.Hex);
        if (Game.GetDistance(chosen.Hex, cr.Hex) >= 1 && chosen.Dir != dir) {
            chosen.ChangeDir(dir);
        }

        //Game.CustomCall("PickCritter " + apCost + " " + crId + " " + isLoot);

        CurPlayer.ServerCall.PickCritter(crId);
    } break;
    }

    if (ChosenActions.length() > 0) {
        ChosenActions.reverse();
        ChosenActions.resize(ChosenActions.length() - 7);
        ChosenActions.reverse();
    }
}

bool UseSkillOnScenerySuccess(CritterProperty skill, Critter chosen, hstring sceneryProto, int hx, int hy)
{
    if (valid(CurMap) && valid(chosen)) {
        int dist = Game.GetDistance(chosen.Hex, mpos(hx, hy));
        int useDist = 1 + chosen.Multihex;
        if (dist > useDist) {
            bool isRun = Settings.AlwaysRun && dist >= Settings.AlwaysRunUseDist;
            SetAction(array<any> = {Tla::ChosenMove, hx, hy, isRun ? 1 : 0, useDist, 0, 1}, false, false);

            if (CurMap.GetPathLength(chosen, mpos(hx, hy), useDist) > 0) {
                SetAction(array<any> = {Tla::ChosenUseSklOnScen, int(skill), sceneryProto.uhash, hx, hy, 0, 0}, true, false);
            }
            return false;
        }
        int apCost = Settings.RtApCostUseSkill;
        CHECK_NEED_AP2(apCost);

        Game.OnCritterActionEx.Fire(true, chosen, CritterAction::UseSkill, 0, null);
        CurPlayer.ServerCall.UseSkill(skill, ZERO_IDENT, ZERO_IDENT, sceneryProto, mpos(hx, hy));
    }
    return true;
}

/*
   void UseSneak( Critter client, bool enable )
   {
    if( cr.InSneakMode == enable )
        return;

    if( !cr.IsAlive() )
        ThrowException( "Client is not alive", client );
    if( enable && Time::IsTimeoutActive( cr.TimeoutSneak ) )
        ThrowException( "Sneak timeout", client );

    if( enable )
        cr.TimeoutSneak = SNEAK_TIMEOUT( client );
    cr.InSneakMode = !cr.InSneakMode;
    CritterState::Wait(cr,  Settings.Breaktime );
   }*/

bool IsBusy(Critter cr)
{
    return Game.FrameTime < cr.WaitEndTick;
}

bool IsFree(Critter cr)
{
    return !IsBusy(cr);
}

#endif

#if SERVER

void PickCritter(Player player, ident crId)
{
    CritterActions::PickCritter(player.GetControlledCritter(), Game.GetCritter(crId));
}

void PickItem(Player player, mpos hex, hstring pid)
{
    CritterActions::PickItem(player.GetControlledCritter(), hex, pid);
}

void DropInvItem(Player player, ident itemId, int count)
{
    CritterActions::DropInvItem(player.GetControlledCritter(), itemId, count);
}

void UseSkill(Player player, CritterProperty skill, ident targetCrId, ident targetItemId, hstring sceneryPid, mpos hex)
{
    CritterActions::UseSkill(player.GetControlledCritter(), skill, targetCrId, targetItemId, sceneryPid, hex.x, hex.y);
}

void MoveInvItem(Player player, ident itemId, CritterItemSlot slot)
{
    CritterActions::MoveInvItem(player.GetControlledCritter(), itemId, slot);
}

void OperateContainer(Player player, bool take, bool all, ident itemId, int itemCount)
{
    CritterActions::OperateContainer(player.GetControlledCritter(), take, all, itemId, itemCount);
}

void ReloadWeapon(Player player, ident weaponId, ident ammoId)
{
    CritterActions::ReloadWeapon(player.GetControlledCritter(), weaponId, ammoId);
}

void Attack(Player player, ident targetId, int mode)
{
    CritterActions::Attack(player.GetControlledCritter(), targetId, mode);
}

void UseItemOn(Player player, ident usedItemId, ident targetCrId, ident targetItemId, hstring sceneryPid, mpos hex, int timer)
{
    CritterActions::UseItemOn(player.GetControlledCritter(), usedItemId, targetCrId, targetItemId, sceneryPid, hex.x, hex.y, timer);
}

#endif

}
