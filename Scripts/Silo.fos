namespace Silo // Sort 2
{

#if SERVER

// Author: rifleman17
// Квест "Рискованное дельце"

///@ Property Critter Common uint8 BHRocketBase Mutable OwnerSync Persistent Group = Quests Quest = 3610 Max = 5

///@ Property Location Server uint8 SiloMissileLaunched Mutable Persistent Max = 2

///@ Property Game Server int32 MissilesCanada Mutable Persistent
///@ Property Game Server int32 MissilesKishinev Mutable Persistent
///@ Property Game Server int32 MissilesBaku Mutable Persistent
///@ Property Game Server int32 MissilesTokio Mutable Persistent
///@ Property Game Server int32 MissilesEburg Mutable Persistent
///@ Property Game Server int32 MissilesVladik Mutable Persistent
///@ Property Game Server int32 MissilesRay Mutable Persistent
///@ Property Game Server int32 MissilesFukusima Mutable Persistent

#define ROLE (12)

void r_Attack(Critter player, Critter npc)
{
    Map map = player.GetMap();
    Critter[] aliveNpc = map.GetCritters(CritterProperty::NpcRole, ROLE, CritterFindType::NonDeadNpc);
    for (int i = 0; i < aliveNpc.length(); i++) {
        NpcPlanes::AddAttackPlane(aliveNpc[i], 0, player);
    }
}

void _HoloInit(Item item, bool firstTime)
{
    item.HolodiskNum = 109;
    // подписываемся на использование голодиска клешней
    item.OnCritterUse.Subscribe(_HoloUse);
}

bool _HoloUse(Item item, Critter cr, int param)
{
    if (cr.BHRocketBase > 1 && cr.BHRocketBase < 4) {
        cr.BHRocketBase = 4;
        Game.DestroyItem(item);
        cr.Experience += 5000;
        Location loc = Location::CreateLocation(Content::Location::silo_base,
                                                ipos(829 + Game.Random(-1 * int(Settings.GlobalMapZoneLength), int(Settings.GlobalMapZoneLength)),
                                                     720 + Game.Random(-1 * int(Settings.GlobalMapZoneLength), int(Settings.GlobalMapZoneLength))),
                                                null);
        if (loc != null) {
            Location::SetKnownLoc(cr, loc);
        }
    }
    return true;
}

void _AmbushInit(Map map, bool firstTime)
{
    map.OnCritterDead.Subscribe(_AmbushDead);
    map.OnCritterIn.Subscribe(_AmbushInCritter);
}

void _AmbushInCritter(Map map, Critter cr)
{
    if (cr.ControlledByPlayer) {
        map.GetLocation().AutoGarbage = true;
    }
}

void _AmbushDead(Map map, Critter cr, Critter killer)
{
    if (map.GetCritters(CritterProperty::NpcRole, ROLE, CritterFindType::NonDeadNpc).isEmpty()) {
        map.GetLocation().AutoGarbage = true;
    }
}

void BlockedDoorInit(Item item, bool firstTime)
{
    item.OnCritterUseSkill.Subscribe(_DoorSkill);
}

void _BlockedDoorInit(Item item, bool firstTime)
{
    item.OnCritterUseSkill.Subscribe(_DoorSkill);
}

bool _DoorSkill(Item item, Critter cr, CritterProperty skilly)
{
    Messaging::Info(cr, TextPackName::Text, 7110);
    return true;
}

void t_Transit(Critter player, StaticItem trigger, bool entered, uint8 dir)
{
    Location loc = player.GetMap().GetLocation();
    if (!valid(loc)) {
        return;
    }

    int entry = trigger.SceneryParams[0].hash;
    int idx = (loc.SiloMissileLaunched == 2 ? 3 : 2);
    Map map = loc.GetMapByIndex(idx);
    if (valid(map)) {
        Obsolete::CritterTransferToMapEntry(player, map.Id, HASHI(entry));
    }
}

void r_Launch(Critter player, Critter npc)
{
    Map map = player.GetMap();
    Location loc = map.GetLocation();
    Map mapFrom = loc.GetMapByIndex(2);
    Map mapTo = loc.GetMapByIndex(3);
    Effects::PlaySound(map, "AIRSIREN.OGG");
    Effects::PlaySound(mapFrom, "AIRSIREN.OGG");
    Critter[] critters = mapFrom.GetCritters(CritterFindType::Any);
    Effects::QuakeScreen(mapFrom, 50, Time::Seconds(1));
    for (int i = 0; i < critters.length(); i++) {
        Critter cr = critters[i];
        Effects::FadeScreen(cr, COLOR_BLACK, COLOR_BLACK, Time::Seconds(5));
        cr.TransferToMap(mapTo, cr.Hex, cr.Dir);
    }
    Effects::QuakeScreen(mapTo, 50, Time::Seconds(2));
    Effects::QuakeScreen(map, 50, Time::Seconds(3));
}

void dlg_Stat(Critter player, Critter npc, string& lexems)
{
    if (IS_DIALOG_END(lexems) or IS_DIALOG_SAY_MODE(lexems)) {
        return;
    }
    lexems = "$when" + GameTime::SynchronizedTimeToString(Game.SynchronizedTime);
    lexems += "$canada" + Game.MissilesCanada;
    lexems += "$kishinev" + Game.MissilesKishinev;
    lexems += "$baku" + Game.MissilesBaku;
    lexems += "$tokio" + Game.MissilesTokio;
    lexems += "$eburg" + Game.MissilesEburg;
    lexems += "$vladik" + Game.MissilesVladik;
    lexems += "$ray" + Game.MissilesRay;
    lexems += "$fukusima" + Game.MissilesFukusima;
}

/*
   lex when Последние зафиксированные пуски МБР.
   Канада: lex canada.
   Кишинев: lex kishinev.
   Баку: lex baku.
   Токио: lex tokio.
   Екатеринбург: lex eburg.
   Челябинск: 0.
   Владивосток: lex vladik.
   Райчихинск: lex ray.
   Фукусима: lex fukusima.
 */

#endif

}
