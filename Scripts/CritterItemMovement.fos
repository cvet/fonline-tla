// FOS Server Client
// Author: cvet

///@ Event Common Game OnCritterDropItem(Critter critter, Item item)
///@ Event Server Item OnCritterDrop(Critter critter)
///@ Event Server Critter OnDropItem(Item item)
///@ Event Server Critter OnSomeCritterDropItem(Critter fromCr, Item item)
///@ Event Server Critter OnSomeCritterMoveItem(Critter fromCr, Item item, uint8 itemMode)

void ModuleInit()
{
    Game.OnItemCheckMove.Subscribe(item_check_move);
    Game.OnItemCheckMoveEx.Subscribe(item_check_move);
    Game.OnCritterCheckMoveItem.Subscribe(critter_check_move_item);
    Game.OnCritterCheckMoveItemEx.Subscribe(critter_check_move_item);
#ifdef __SERVER
    Game.OnCritterMoveItem.Subscribe(critter_move_item);
    Game.OnCritterDropItem.Subscribe(critter_drop_item);
#endif
}

bool item_check_move(Item cr, uint count, Entity from, Entity to)
{
    return true;
}

#ifdef __SERVER
bool critter_drop_item(Critter cr, Item item)
{
    item.OnCritterDrop.Fire(cr);
    cr.OnDropItem.Fire(item);

    Critter[] other = cr.GetCritters(true, CritterFindType::Alive);
    for (uint i = 0; i < other.length(); i++) {
        other[i].OnSomeCritterDropItem.Fire(cr, item);
    }

    return true;
}
#endif

// //////////////////////////////////////////////////////////////////////////////////////////////////
// Call on something critter want move item from one slot to another.
// Return true to allow transaction, false to disallow.
// Function must exist on server and client side.
bool critter_check_move_item(Critter cr, Item item, uint8 toSlot)
{
    uint8 fromSlot = item.CritterSlot;

    // Drop
    if (toSlot == SLOT_GROUND) {
        return true;
    }

    // If busy, then allow only hands item swapping
    // bool handsSwap = ( ( fromSlot == SLOT_HAND1 && toSlot == SLOT_HAND2 ) || ( fromSlot == SLOT_HAND2 && toSlot == SLOT_HAND1 ) );
    // if( valid( itemSwap )  !handsSwap )
    //    return false;

    // Weapon animation
    if (toSlot == SLOT_HAND1) {
        if (item.Type == ItemType::Weapon && !CritterAnims::IsCritterAnim1(cr, uint(item.Weapon_Anim1))) {
            Game.Log("Can not use weapon: Critter " + cr.ModelName + "; anim: " + item.Weapon_Anim1);
            return false;
        }
    }

    // Armor animation
    if (toSlot == SLOT_ARMOR) {
        if (item.Type != ItemType::Armor) {
            return false;
        }
        if (item.CritterSlot != SLOT_INV) {
            return false;
        }
        if (cr.IsNoArmor) {
            return false;
        }
    }

    // Custom slot checks
    if (toSlot > SLOT_ARMOR) {
        if (toSlot != item.CritterSlot) {
            return false;
        }
    }

    // Allow moving
    return true;
}

#ifdef __SERVER
// //////////////////////////////////////////////////////////////////////////////////////////////////
// Call on critter item moved.
void critter_move_item(Critter critter, Item item, uint8 fromSlot)
{
    Critter cr = Game.GetCritter(critter.Id);
    uint8 toSlot = item.CritterSlot;

    // Armor changing
    if (item.Type == ItemType::Armor && (fromSlot == SLOT_ARMOR || toSlot == SLOT_ARMOR)) {
        // Default dude
        bool isMale = (cr.Gender == GenderType::Male);
        hstring crModel = cr.ModelNameBase;
        if (crModel == hstring()) {
            crModel = (isMale ? CRTYPE_DEFAULT_M : CRTYPE_DEFAULT_F);
        }
        if (toSlot == SLOT_ARMOR) {
            crModel = (isMale ? item.Armor_CrTypeMale : item.Armor_CrTypeFemale);
        }

        // Long hair dude redirects
        if (cr.ModelNameBase == CRTYPE_LHD_JUMPSUIT && isMale) {
            crModel = CRTYPE_LHD_JUMPSUIT;
            if (toSlot == SLOT_ARMOR) {
                crModel = item.Armor_CrTypeMale;
                if (crModel == CRTYPE_DW_LEATHER_ARMOR_M) {
                    crModel = CRTYPE_LHD_LEATHER_ARMOR;
                }
                else if (crModel == CRTYPE_DW_LEATHER_JACKET_M) {
                    crModel = CRTYPE_LHD_LEATHER_JACKET;
                }
                else if (crModel == CRTYPE_DW_METAL_ARMOR_M) {
                    crModel = CRTYPE_LHD_METAL_ARMOR;
                }
            }
        }

        if (cr.ModelName != crModel) {
            Assert(crModel != hstring());
            cr.ModelName = crModel;
        }

        // Armor modifier
        cr.CurrentArmorPerk = (toSlot == SLOT_ARMOR ? item.Armor_Perk : 0);
    }

    Critter[] other = cr.GetCritters(true, CritterFindType::Alive);
    for (uint i = 0; i < other.length(); i++) {
        other[i].OnSomeCritterMoveItem.Fire(cr, item, fromSlot);
    }

    uint anim1;
    if (fromSlot == SLOT_HAND1) {
        anim1 = Anim1::Unarmed;
    }
    else if (toSlot == SLOT_HAND1) {
        anim1 = (item.Type == ItemType::Weapon ? item.Weapon_Anim1 : Anim1::Unarmed);
    }
    else {
        return;
    }

    if (anim1 != cr.Anim1Alive) {
        Game.DeferredCall(0, CritterSetAnim1, array<uint> = {cr.Id, anim1});
    }
    // Slot modifiers
    // SLOT_PERK_MIRROR_SHADES // +1 charisma
    // SLOT_PERK_COSMETIC_CASE // +1 charisma to female
    // SLOT_PERK_MOTION_SENSOR // +20 outdoorsman
    // SLOT_PERK_STEALTH_BOY   // +20 sneak
}

void CritterSetAnim1(uint[] data)
{
    Critter cr = Game.GetCritter(data[0]);
    if (cr != null) {
        cr.SetConditionAnims(CritterCondition::Alive, Anim1(data[1]), 0);
    }
}
#endif
