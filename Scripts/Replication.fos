namespace Replication // Sort 2
{

#if SERVER

// Author: cvet

///@ Property Critter PrivateServer int32 ReplicationTime
///@ Property Critter PrivateServer int32 HellVisits
///@ Property Game PrivateServer ident[] PermanentDeath

const hstring HellMapPid = Content::Map::repl1; // TODO: вернуть ад Content::Map::repl_hell;
const hstring[] ReplicatorsMapPids = {Content::Map::repl1, Content::Map::repl2, Content::Map::repl3, Content::Map::repl4};
int ReplicatorPos = 0;

bool IsPermanentDeath(ident id)
{
    return Game.PermanentDeath.exists(id);
}

void ClearPermanentDeath()
{
    Game.PermanentDeath = array<ident>();
}

void AddPermanentDeath(ident id)
{
    auto permanentDeath = Game.PermanentDeath.clone();
    if (!permanentDeath.exists(id)) {
        permanentDeath.insertLast(id);
        Game.PermanentDeath = permanentDeath;
    }
}

Map GetStartReplicatorMap()
{
    return Game.GetMap(ReplicatorsMapPids[0]);
}

Map GetNearReplicatorMap(Critter cr)
{
    Map nearMap = null;
    int nearDist = 0;
    for (int i = 0, j = ReplicatorsMapPids.length(); i < j; i++) {
        Map map = Game.GetMap(ReplicatorsMapPids[i]);
        if (valid(map)) {
            Location loc = map.GetLocation();
            int dx = int(cr.WorldPos.x) - int(loc.WorldPos.x);
            int dy = int(cr.WorldPos.y) - int(loc.WorldPos.y);
            int dist = int(math::sqrt(dx * dx + dy * dy));

            // GameEventReplicator.fos Restrict replication4 when event is on
            if (loc.ProtoId == Content::Location::replication_4 && Game.GEReplStationStatus) {
                continue;
            }

            if (not valid(nearMap) || dist < nearDist) {
                nearMap = map;
                nearDist = dist;
            }
        }
    }
    return nearMap;
}

void SetStartLocation(Critter cr)
{
    Map replicator = GetStartReplicatorMap();
    if (not valid(replicator)) {
        return;
    }

    // Hidden fog on default player position
    int zoneX = cr.WorldPos.x / Settings.GlobalMapZoneLength;
    int zoneY = cr.WorldPos.y / Settings.GlobalMapZoneLength;
    GlobalMapFog::SetFog(cr, zoneX, zoneY, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX - 1, zoneY - 1, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX, zoneY - 1, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX + 1, zoneY - 1, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX - 1, zoneY, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX + 1, zoneY, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX - 1, zoneY + 1, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX, zoneY + 1, GM_FOG_FULL);
    GlobalMapFog::SetFog(cr, zoneX + 1, zoneY + 1, GM_FOG_FULL);

    Obsolete::CritterTransitToMapEntry(cr, replicator.Id, ENTRY_REPLICATION);

    Location::SetKnownLoc(cr, replicator.GetLocation());
    Location::SetKnownLoc(cr, Content::Location::replication_hell);
    hstring replPid = replicator.GetLocation().ProtoId;
    if (replPid == Content::Location::replication_1) {
        Location::SetKnownLoc(cr, Content::Location::den);
        Location::SetKnownLoc(cr, Content::Location::klamath);
        Location::SetKnownLoc(cr, Content::Location::arroyo); // TODO: remove arroyo after test s
    }
    else if (replPid == Content::Location::replication_2) {
        Location::SetKnownLoc(cr, Content::Location::modoc);
    }
    else if (replPid == Content::Location::replication_3) {
        Location::SetKnownLoc(cr, Content::Location::redding);
    }
    else if (replPid == Content::Location::replication_4) {
        Location::SetKnownLoc(cr, Content::Location::broken_hills);
        Location::SetKnownLoc(cr, Content::Location::new_reno);
    }

    zoneX = cr.WorldPos.x / Settings.GlobalMapZoneLength;
    zoneY = cr.WorldPos.y / Settings.GlobalMapZoneLength;
    GlobalMapFog::SetFog(cr, zoneX, zoneY, GM_FOG_NONE);
    GlobalMapFog::SetFog(cr, zoneX - 1, zoneY - 1, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX, zoneY - 1, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX + 1, zoneY - 1, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX - 1, zoneY, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX + 1, zoneY, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX - 1, zoneY + 1, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX, zoneY + 1, GM_FOG_HALF);
    GlobalMapFog::SetFog(cr, zoneX + 1, zoneY + 1, GM_FOG_HALF);
}

void SetReplicationTime(Critter cr)
{
    int replTime = cr.ReplicationTime;
    if (replTime < 0) {
        return; // No respawn
    }
    if (replTime == 0) // Take default values
    {
        if (cr.ControlledByPlayer) {
            replTime = 3; // 3 minutes
        }
        else {
            replTime = Game.Random(90, 120); // 90-120 minutes
        }
    }
    cr.TimeoutReplication = Game.SynchronizedTime + Time::Minutes(replTime);
}

bool IsHellMap(hstring mapPid)
{
    return mapPid == HellMapPid;
}

void ReplicateCritter(Critter cr)
{
    Map dieMap = cr.GetMap();
    Map map = null;
    uint16 hx = 0, hy = 0;

    // Check car
    Item[] cars = cr.GetItems(ItemProperty::Type, ItemType::Car);
    if (cars.length() > 0) {
        // Transit to random encounter
        bool transferred = false;
        int encounterDescriptor = int(-1);
        bool waitForAnswer = false;
        Worldmap::FindEncounter(GlobalmapGroupExtended::GetGlobalGroup(cr), cars[0], cr.WorldPos.x, cr.WorldPos.y, encounterDescriptor, waitForAnswer, true);
        if (encounterDescriptor != int(-1)) {
            ident mapId;
            uint16 hexX = 0;
            uint16 hexY = 0;
            uint8 dir = 0;
            Worldmap::InviteToEncounter(GlobalmapGroupExtended::GetGlobalGroup(cr), cars[0], encounterDescriptor, 0, mapId, hexX, hexY, dir);
            if (mapId != ZERO_IDENT && Obsolete::CritterTransitToMapCoords(cr, mapId, hexX, hexY, dir)) {
                transferred = true;
            }
        }

        // Encounter not founded or transfer fail, delete cars
        if (!transferred) {
            for (int i = 0; i < cars.length(); i++) {
                Game.DestroyItem(cars[i]);
            }
        }

        // Continue dead
        cr.TimeoutReplication = Game.SynchronizedTime + Time::Minutes(1);
        return;
    }

    if (cr.ControlledByPlayer) {
        bool dieMapIsHell = (valid(dieMap) && IsHellMap(dieMap.ProtoId));
        bool customReplMap = false;
        if (cr.NextReplicationMap != ZERO_IDENT) {
            // Нестандартная репликация в Чистилище
            Purgatory::NeedReplicateCritterToBattle(cr);
            customReplMap = true; // репликация на других картах
        }

        if (not customReplMap && cr.ReplicationMoney < 0) {
            map = Game.GetMap(HellMapPid);

            if (not dieMapIsHell) {
                cr.HellVisits += 1;

                // Show hell video
                if (cr.HellVisits == 1) {
                    Media::PlayVideo(cr, "TheLifeAfterHell.ogv|TheLifeAfterHell.ogg", false);
                }

                // Permanent death
                if (Settings.PermanentDeath > 0 && cr.HellVisits > Settings.PermanentDeath) {
                    AddPermanentDeath(cr.Id);
                    Messaging::Info(cr, TextPackName::Game, MsgStr::StrNetPermanentDeath);
                    cr.Disconnect();
                }
            }
        }
        else {
            if (customReplMap) {
                map = Game.GetMap(cr.NextReplicationMap);
            }
            else {
                map = GetNearReplicatorMap(cr);
            }
        }

        if (not valid(map) || not Entrance::GetEntryFreeHex(map, (customReplMap ? cr.NextReplicationEntry : ENTRY_REPLICATION), hx, hy)) {
            // Continue dead
            cr.TimeoutReplication = Game.SynchronizedTime + Time::Minutes(1);
            return;
        }

        if (_CritCanDropItemsOnDead(cr)) {
            Map dropMap = cr.GetMap();
            Item[] items = cr.GetItems();

            // Disable drop of hidden items and delete gag items (sandbags)
            for (int i = 0; i < items.length(); i++) {
                if (items[i].IsGag) {
                    Game.DestroyItem(items[i]);
                    items[i] = null;
                }
                else if (items[i].Hidden) {
                    items[i] = null;
                }
            }

            // Drop
            if (valid(dropMap)) {
                ItemMovement::MoveItems(items, dropMap, cr.Hex.x, cr.Hex.y);
            }
            else {
                Game.DestroyItems(items);
            }
        }

        if (!customReplMap) {
            if (not dieMapIsHell) {
                cr.ReplicationMoney -= cr.ReplicationCost;
                cr.AddItem(Content::Item::flower, 1);
                if (cr.ReplicationMoney >= 100) {
                    cr.AddItem(Content::Item::radio, 1); // Add radio
                }
            }
            cr.ReplicationCount += 1;
        }
        else {
            // Очистка данных по нестандартной репликации
            cr.NextReplicationMap = ZERO_IDENT;
            cr.NextReplicationEntry = EMPTY_HSTRING;
        }
    }
    else {
        NpcPlanes::DropPlanes(cr);
        EnemyStack::ClearEnemyStackNpc(cr);
        cr.LastWeaponId = ZERO_IDENT;
        cr.LastWeaponNotFound = false;
        map = cr.GetMap();
        if (not valid(map)) // On global, delete
        {
            Game.DestroyCritter(cr);
            return;
        }
        hx = cr.Hex.x;
        hy = cr.Hex.y;

        if (cr.DeadBlockerId != ZERO_IDENT) {
            Item block = Game.GetItem(cr.DeadBlockerId);
            if (valid(block)) {
                Game.DestroyItem(block);
            }
            cr.DeadBlockerId = ZERO_IDENT;
        }

        if (not map.IsHexMovable(mpos(hx, hy))) {
            bool founded = false;
            for (int x = -1; x <= 1; x++) {
                for (int y = -1; y <= 1; y++) {
                    if (x == 0 && y == 0) {
                        continue; // Skip direct position
                    }

                    if (Settings.MapHexagonal) {
                        if ((hx % 2) == 1 && ((x == -1 && y == 1) || (x == 1 && y == 1))) {
                            continue;
                        }
                        if ((hx % 2) == 0 && ((x == -1 && y == -1) || (x == 1 && y == -1))) {
                            continue;
                        }
                    }

                    if (map.IsHexMovable(mpos(hx + x, hy + y))) {
                        hx += x;
                        hy += y;
                        founded = true;
                        break;
                    }
                }
                if (founded) {
                    break;
                }
            }

            if (not founded) {
                // Continue dead
                cr.TimeoutReplication = Game.SynchronizedTime + Time::Minutes(1);
                return;
            }
        }
    }

    Obsolete::CritterTransitToMapCoords(cr, map.Id, hx, hy, Game.Random(0, 5));
    cr.IsDamagedEye = false;
    cr.IsDamagedRightArm = false;
    cr.IsDamagedLeftArm = false;
    cr.IsDamagedRightLeg = false;
    cr.IsDamagedLeftLeg = false;
    if (cr.NextCrType != EMPTY_HSTRING) {
        // cr.ChangeCrType( cr.NextCrType );
        cr.ModelNameBase = cr.NextCrType;
        cr.NextCrType = EMPTY_HSTRING;
    }
    CritterState::ToAlive(cr);
    cr.CurrentHp = cr.MaxLife;
    cr.CurrentAp = cr.ActionPoints * AP_DIVIDER;
    Poison::DropPoison(cr);
    Radiation::DropRadiation(cr);
    if (!cr.ControlledByPlayer || map.ProtoId != HellMapPid) {
        Drugs::DropDrugEffects(cr); // Note: This function clear effects of all active drugs/radiation/poison!
    }
    GlobalmapGroupExtended::DropTimers(cr);
    for (int i = 0; i < CritterPropertyGroup::Timeouts.length(); i++) {
        if (CritterPropertyGroup::Timeouts[i] != CritterProperty::TimeoutKarmaVoting) {
            cr.SetAsInt(CritterPropertyGroup::Timeouts[i], 0);
        }
    }
}

//
// Turret
//
void _TurretInit(Critter turret, bool firstTime)
{
    turret.PerceptionBase = 10;
    turret.SkillBigGuns = 300;
    turret.SkillEnergyWeapons = 300;
    turret.IsNoEnemyStack = true;
    turret.IsUnlimitedAmmo = true;
}

//
// Hubologist
//
void _HubologistInit(Critter hubologist, bool firstTime)
{
    hubologist.IsNoEnemyStack = true;
}

#endif

}
