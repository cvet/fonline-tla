namespace EnergyBarier // Sort 2
{

#if SERVER

// Author: Tab10id
// Скрипт приписывается итему в прототипе, во время инициализации вокруг итема выставлются итемы, играющие роль триггеров.
// При переходе через итем-триггер, у игрока проверяется право на проход, если его нет, то в некотором радиусе ищутся охранники,
// если они найдены, игрок останавливается и начинается диалог с охраной.
// Под широкими барьерами кладутся дополнительные итемы-блокирующие проход в запертом состоянии.

///@ Property Critter Server mpos EnergyBarierTerminalHex Mutable Persistent
///@ Property Critter Server int32 EnergyBarierNetNum Mutable Persistent
///@ Property Critter Server int32 EnergyBarierHackBonus Mutable Persistent
///@ Property Critter Server int32 EnergyBarierHitBonus Mutable Persistent

///@ Property Map Server int32=>int32[] EnergyBarierHitBonus Mutable Persistent
///@ Property Map Server int32=>any[] EnergyBarierTerminal Mutable Persistent
///@ Property Map Server int32=>any[] EnergyBarierTerminalInfo Mutable Persistent

#define DL #(str)
// #define DL #(str) Game.Log(str)

#define BS_ON                      (0)
#define BS_OFF_TIMED               (1)
#define BS_OFF_PERMA               (2)

#define MODE_STANDART              (1)
#define MODE_HACK                  (2)
#define MODE_USE_CARD              (3)

#define AE_DEFAULT                 (0)
#define AE_ON                      (1)
#define AE_OFF                     (2)

#define BM_OFF                     (1)
#define BM_ON                      (0)
#define BM_HALF_ON                 (2)
#define BM_OFF_NO_GUARD            (3)

#define TIME_CARD_VALID            (Time::GameDays(1))
#define TIME_BROKEN                (Time::GameHours(3))
#define TIME_NOT_ACCESS            (Time::GameHours(3))
#define TIME_BARIER_OPEN           (Time::GameMinutes(5))

#define FD_NORMAL                  (2)
#define FD_HACK_NOT_SUCC           (5)
#define FD_HACK_SUCC               (6)
#define FD_TERM_BROKEN             (7)
#define FD_TERM_NOT_ACCESS         (8)
#define FD_TURN_OFF_FIELD          (9)
#define FD_NOT_VALID_CARD          (10)
#define FD_BARIER_OPEN             (12)
#define FD_DISABLED                (12)

#define COORD_X                    (0)
#define COORD_Y                    (1)

#define MIN_DMG_USE_LOW            (5)
#define MAX_DMG_USE_LOW            (15)
#define MIN_DMG_USE_HI             (10)
#define MAX_DMG_USE_HI             (20)
#define MIN_DMG_SPATIAL            (15)
#define MAX_DMG_SPATIAL            (30)
#define DAMAGE_TYPE                (DamageTypes::Electricity)

#define MSG_SHOCK_PAIN_FIELD_USE   (5300)
#define MSG_SHOCK_FULL_FIELD_USE   (5301)
#define MSG_REPAIR_FAIL_OFF        (5302)
#define MSG_REPAIR_FAIL_ON         (5311)
#define MSG_REPAIR_WIN_OFF         (5303)
#define MSG_REPAIR_WIN_ON          (5304)
#define MSG_FIELD_SCIENCE_USE_WIN  (5308)
#define MSG_FIELD_SCIENCE_USE_FAIL (5309)

#define EXP_DISABLE_FORCE_FIELD    (50)
#define SCIENCE_PENALTY            (-40)
#define TOOL_BONUS                 (SCIENCE_PENALTY + 20)
#define SUPER_TOOL_BONUS           (SCIENCE_PENALTY + 40)
#define SKILL_SUCCESS              #(cr, sk, bonus)(Stdlib::IsSuccessRvS(Stdlib::RollVsSkill(cr, sk, bonus)))

#define RADIUS                     (10)

// clang-format off

const hstring[][] EBHalfOnMode = {
    { Content::Item::eb_long_hor, Content::Item::eb_long_vert, Content::Item::eb_short_hor1,     Content::Item::eb_short_vert1,     Content::Item::eb_short_hor2,     Content::Item::eb_short_vert2,     Content::Item::eb_short_hor3,     Content::Item::eb_short_vert3      },
    { EMPTY_HSTRING,                  EMPTY_HSTRING,                   Content::Item::eb_short_hor1_red, Content::Item::eb_short_vert1_red, Content::Item::eb_short_hor2_red, Content::Item::eb_short_vert2_red, Content::Item::eb_short_hor3_red, Content::Item::eb_short_vert3_red, }
};

const int[][][] EBBlockers = {
    /* EB_LONG_HOR */
    {{4, 3, 4}, {0, 1, 0}},
    /* EB_LONG_VERT */
    {{5}, {2, 2}}};

const int[][] EBTriggers = {
    /* EB_LONG_HOR */
    {1, 1, 1, 1, 2, 2, 2, 3, 3, 3, 3, 4, 4, 4, 4, 5, 5, 5, 0, 0, 0, 0},
    /* EB_LONG_VERT */
    {1, 1, 1, 2, 2, 3, 3, 3, 4, 4, 4, 5, 5, 0, 0, 0},
    /* EB_SHORT_HOR1 */
    /* EB_SHORT_HOR2 */
    /* EB_SHORT_HOR3 */
    {1, 1, 2, 2, 3, 3, 4, 4, 5, 5, 0, 0},
    /* EB_SHORT_VERT1 */
    /* EB_SHORT_VERT2 */
    /* EB_SHORT_VERT3 */
    {2, 2, 3, 3, 3, 4, 5, 5, 5, 0, 0, 0, 1, 2}};

const int[][] EBEmitterCoords = {
    /* EB_EMITTER_HOR1 */
    /* EB_EMITTER_HOR2 */
    /* EB_EMITTER_HOR3 */
    {2, 0},
    /* EB_EMITTER_VERT1 */
    /* EB_EMITTER_VERT2 */
    /* EB_EMITTER_VERT3 */
    {0, 2}};

// clang-format on

int MakeTerminalKey(mpos hex, int num = 0)
{
    return ((hex.x << 16) | hex.y) + num;
}

void InitBariers(Item barier)
{
    int[][] blockers = {{}};
    int[] triggers = {};
    int[] temp = {};
    int trigDir;
    int trigDest;
    hstring proto = barier.ProtoId;
    bool guarded = true;
    hstring haveHalfOnMode;

    if (barier.MapId == ZERO_IDENT) {
        Game.StartTimeEvent(ZERO_TIMESPAN, InitBarier, barier.Id);
        return;
    }

    Map map = Game.GetMap(barier.MapId);
    if (valid(map)) {
        DeleteBlockersAndTriggers(map, barier.Val1);
    }

    if (proto == Content::Item::eb_long_hor) {
        DL("Горизонтальный барьер");
        blockers.grow(2);
        blockers.last() = array<int>();
        push_back_array(blockers[0], EBBlockers[0][0]);
        push_back_array(blockers[1], EBBlockers[0][1]);
        trigDir = 5;
        trigDest = 4;
        if (barier.Val3 != 0) {
            push_back_array(triggers, EBTriggers[0]);
        }
        else {
            guarded = false;
        }
        haveHalfOnMode = EBHalfOnMode[1][0];
    }
    else if (proto == Content::Item::eb_long_vert) {
        DL("Вертикальный барьер");
        blockers.grow(2);
        blockers.last() = array<int>();
        push_back_array(blockers[0], EBBlockers[1][0]);
        push_back_array(blockers[1], EBBlockers[1][1]);
        trigDir = 5;
        trigDest = 2;
        if (barier.Val3 != 0) {
            push_back_array(triggers, EBTriggers[1]);
        }
        else {
            guarded = false;
        }
        haveHalfOnMode = EBHalfOnMode[1][1];
    }
    else if (proto == Content::Item::eb_short_hor1 || proto == Content::Item::eb_short_hor2 || proto == Content::Item::eb_short_hor3) {
        blockers.grow(2);
        blockers.last() = array<int>();
        trigDir = 5;
        trigDest = 2;
        if (barier.Val3 != 0) {
            push_back_array(triggers, EBTriggers[2]);
        }
        else {
            guarded = false;
        }

        if (proto == Content::Item::eb_short_hor1) {
            haveHalfOnMode = EBHalfOnMode[1][2];
        }
        else if (proto == Content::Item::eb_short_hor2) {
            haveHalfOnMode = EBHalfOnMode[1][4];
        }
        else if (proto == Content::Item::eb_short_hor3) {
            haveHalfOnMode = EBHalfOnMode[1][6];
        }
    }
    else if (proto == Content::Item::eb_short_vert1 || proto == Content::Item::eb_short_vert2 || proto == Content::Item::eb_short_vert3) {
        blockers.grow(2);
        blockers.last() = array<int>();
        trigDir = 0;
        trigDest = 2;
        if (barier.Val3 != 0) {
            push_back_array(triggers, EBTriggers[3]);
        }
        else {
            guarded = false;
        }

        if (proto == Content::Item::eb_short_vert1) {
            haveHalfOnMode = EBHalfOnMode[1][3];
        }
        else if (proto == Content::Item::eb_short_vert2) {
            haveHalfOnMode = EBHalfOnMode[1][5];
        }
        else if (proto == Content::Item::eb_short_vert3) {
            haveHalfOnMode = EBHalfOnMode[1][7];
        }
    }
    CBarier(barier, blockers, triggers, trigDir, trigDest, guarded, haveHalfOnMode);
}

void InitEmitter(Item emitter)
{
    emitter.OnCritterUseSkill.Subscribe(_UseEmitter);
    emitter.OnCritterUseOnSelf.Subscribe(_UseOnEmitter);
}

int GetFieldCoord(int coord, hstring proto, int type)
{
    if (proto == Content::Item::eb_emitter_hor1 || proto == Content::Item::eb_emitter_hor2 || proto == Content::Item::eb_emitter_hor3) {
        return EBEmitterCoords[0][type];
    }
    else if (proto == Content::Item::eb_emitter_vert1 || proto == Content::Item::eb_emitter_vert2 || proto == Content::Item::eb_emitter_vert3) {
        return EBEmitterCoords[1][type];
    }
    return 0;
}

class CBarier
{
    Item Barier;            // Ссылка на итем, отображающий сам барьер
    Item[] Blockers = {};   // Массив итемов блокираторов под основным итемом
    Item[] Triggers = {};   // Массив триггеров
    bool Guarded;           // Охраняемый ли барьер
    int BarierStat;         // Текущий режим работы барьера (Закрытый, открытый, снижена мощность)
    hstring HaveHalfOnMode; // Имеет ли барьер режим половины мощности (если имеет, то свойство имеет прототип итема хранящего изображение данного барьера в
                            // данном режиме)
    int FieldMode;          // Текущий режим работы

    CBarier(Item barier, int[][] blockers, int[] triggers, int trigDir, int trigDest, bool guarded, hstring modeHalfOn)
    {
        DL("CBarier: barier.val1=" + barier.Val1 + ", blockers: [" + blockers[0].length() + "][" + blockers[1].length() + "], triggers: " + triggers.length() +
           ", trigDir: " + trigDir + ", trigDest: " + trigDest + ", medeHalfOn: " + modeHalfOn);

        mpos baseHex;
        mpos newHex;
        Item temp;
        Map map;
        Location loc;

        HaveHalfOnMode = modeHalfOn;
        baseHex = barier.Hex;
        Barier = barier;
        Guarded = guarded;
        Blockers.insertLast(barier);

        if (barier.MapId != ZERO_IDENT) {
            map = Game.GetMap(barier.MapId);
        }

        if (!valid(map)) {
            Game.Log("Map not valid!");
            return;
        }

        for (int k = 0; k < blockers.length(); k++) {
            newHex = baseHex;
            for (int i = 0; i < blockers[k].length(); i++) {
                map.MoveHexByDir(newHex, blockers[k][i], 1);
                Critter crit = map.GetCritter(newHex);
                if (map.IsHexMovable(newHex) or valid(crit)) {
                    temp = map.GetItem(newHex, Content::Item::eb_blocker);
                    if (not valid(temp) or temp.Val1 != Barier.Val1) {
                        temp = map.AddItem(newHex, Content::Item::eb_blocker, 1);
                        Blockers.insertLast(temp);
                    }
                }
            }
        }

        newHex = baseHex;
        map.MoveHexByDir(newHex, trigDir, trigDest);
        if (guarded) {
            DL("Триггеры для барьера №" + Blockers[0].Val1 + ": " + triggers.length());
            for (int k = 0; k < triggers.length(); k++) {
                map.MoveHexByDir(newHex, triggers[k], 1);
                Critter crit = map.GetCritter(newHex);
                if (map.IsHexMovable(newHex) or valid(crit)) {
                    temp = map.GetItem(newHex, Content::Item::eb_trigger);
                    if (not valid(temp) or temp.Val1 != Barier.Val1) {
                        temp = map.AddItem(newHex, Content::Item::eb_trigger, 1);
                        Triggers.insertLast(temp);
                    }
                }
            }
        }

        InitBlockers(this);
        InitTriggers(this);

        loc = map.GetLocation();
        if (!valid(loc)) {
            Game.Log("Error: location not valid");
            return;
        }

        CBariersNet net = GetBariersNet(Barier.Val1, loc.Id);
        if (!valid(net)) {
            net = CBariersNet(Barier.Val1, loc.Id, Barier.Val2);
            if (!valid(net)) {
                Game.Log("ForceField not created!");
                return;
            }
            BariersNets.insertLast(net);
        }

        net.AddBarier(this);
    }

    void ChangeMode(int mode)
    {
        FieldMode = mode;
        if (mode == BM_HALF_ON) {
            if (HaveHalfOnMode != EMPTY_HSTRING) {
                DL("Меняем прототип");
                Blockers[0] = Item::ChangeProto(Blockers[0], HaveHalfOnMode);
            }
        }
        else {
            if (mode == BM_ON) {
                hstring proto = GetMainProto();
                if (proto != EMPTY_HSTRING) {
                    Blockers[0] = Item::ChangeProto(Blockers[0], proto);
                }
            }
        }
        if ((mode == BM_ON) or (mode == BM_HALF_ON)) {
            for (int i = 0; i < Blockers.length(); i++) {
                Blockers[i].Hidden = false;
            }
        }
        else {
            for (int i = 0; i < Blockers.length(); i++) {
                Blockers[i].Hidden = true;
            }
        }
        for (int i = 0; i < Blockers.length(); i++) {
            if (mode == BM_ON) {
                Blockers[i].NoBlock = false;
            }
            else {
                if (mode == BM_HALF_ON) {
                    Blockers[i].OnCritterWalk.Subscribe(_DamageCritter);
                }
                else {
                    Blockers[i].OnCritterWalk.Unsubscribe(_DamageCritter);
                }

                if (HaveHalfOnMode == EMPTY_HSTRING && mode == BM_HALF_ON) {
                    Blockers[i].NoBlock = false;
                }
                else {
                    Blockers[i].NoBlock = true;
                }
            }
        }
    }

    int GetBarierNum()
    {
        return Barier.Val1;
    }

    int GetGuardNum()
    {
        return Barier.Val3;
    }

    hstring GetMainProto()
    {
        for (int i = 0; i < EBHalfOnMode[1].length(); i++) {
            if (Barier.ProtoId == EBHalfOnMode[1][i]) {
                return EBHalfOnMode[0][i];
            }
        }
        return EMPTY_HSTRING;
    }

    int GetFieldMode()
    {
        DL("Mode: " + FieldMode);
        return FieldMode;
    }
};

class CBariersNet
{
    CBarier[] Bariers = {};
    int NetNum;
    ident LocId;
    int NetState;
    bool TerminalsDisabled;

    CBariersNet(int netNum, ident locId, int netState)
    {
        NetNum = netNum;
        LocId = locId;
        NetState = netState;
        TerminalsDisabled = false;
    }

    void AddBarier(CBarier barier)
    {
        Bariers.insertLast(barier);
    }

    void ChangeNetMode(int mode, int autoEnable)
    {
        CBarier barier;
        bool autoOn;
        bool autoOnHalf;
        for (int i = 0; i < Bariers.length(); i++) {
            barier = Bariers[i];
            autoOn = (barier.Blockers[0].Val4 == 0) ? true : false;
            autoOnHalf = (barier.Blockers[0].Val5 == 0) ? true : false;
            if (((((mode == BM_OFF) and (autoOn)) or ((mode == BM_HALF_ON) and (autoOnHalf))) or (autoEnable == AE_ON)) and (autoEnable != AE_OFF)) {
                any[] values = {};
                values.insertLast(LocId);
                values.insertLast(NetNum);
                values.insertLast(BM_ON);
                Game.StartTimeEvent(TIME_BARIER_OPEN, TurnOnBariers, values); //SavedDeferredCall
            }
            barier.ChangeMode(mode);
            NetState = mode;
        }
    }
};

CBariersNet[] BariersNets = {};

CBariersNet GetBariersNet(int netNum, ident locId)
{
    for (int i = 0; i < BariersNets.length(); i++) {
        if ((BariersNets[i].NetNum == netNum) and (BariersNets[i].LocId == locId)) {
            return BariersNets[i];
        }
    }
    return null;
}

CBarier GetBarier(ident mainItemId)
{
    for (int i = 0; i < BariersNets.length(); i++) {
        if (valid(BariersNets[i])) {
            for (int k = 0; k < BariersNets[i].Bariers.length(); k++) {
                if (valid(BariersNets[i].Bariers[k])) {
                    CBarier barier = BariersNets[i].Bariers[k];
                    if (barier.Barier.Id == mainItemId) {
                        return barier;
                    }
                }
            }
        }
    }
    return null;
}

void push_back_array(int[] arr, int[] target)
{
    for (int i = 0; i < target.length(); i++) {
        arr.insertLast(target[i]);
    }
}

void InitBlockers(CBarier barier)
{
    if (barier.Blockers[0].Val2 == BM_ON) {
        barier.Blockers[0].OnCritterUseSkill.Subscribe(_DamageCritterUse);
        barier.Blockers[0].Hidden = false;
        for (int i = 0; i < barier.Blockers.length(); i++) {
            barier.Blockers[i].NoBlock = false;
            if (i > 0) {
                barier.Blockers[i].Val1 = barier.Blockers[0].Val1;
            }
        }
        barier.FieldMode = BM_ON;
    }
    else {
        if (barier.Blockers[0].Val2 == BM_OFF) {
            barier.FieldMode = BM_OFF;
            barier.Blockers[0].Hidden = true;
            for (int i = 0; i < barier.Blockers.length(); i++) {
                barier.Blockers[i].NoBlock = true;
                barier.Blockers[i].Hidden = true;
                if (i > 0) {
                    barier.Blockers[i].Val1 = barier.Blockers[0].Val1;
                }
            }
        }
        else {
            barier.FieldMode = BM_HALF_ON;
            barier.Blockers[0].OnCritterUseSkill.Subscribe(_DamageCritterUse);
        }
    }
}

bool CheckInven(Critter master)
{
    Item[] items = master.GetItems(Content::Item::energy_barier_access_card);
    for (int i = 0; i < items.length(); i++) {
        if (CheckCard(items[i], master)) {
            return true;
        }
    }
    return false;
}

bool CheckCard(Item card, Critter master)
{
    if ((card.Val4 < Game.SynchronizedTime - TIME_CARD_VALID) || (card.Val5 != master.Id)) {
        return false;
    }
    else {
        return true;
    }
}

int HackRoll(Critter master)
{
    int res = Stdlib::RollVsSkill(master, CritterProperty::SkillScience, SCIENCE_PENALTY + master.EnergyBarierHackBonus);
    return res;
}

bool BarierOpen(Critter master)
{
    Map map = master.GetMap();
    if (!valid(map)) {
        return false;
    }
    Location loc = map.GetLocation();
    if (!valid(loc)) {
        return false;
    }
    if (GetNetState(loc.Id, master.EnergyBarierNetNum) == BM_OFF) {
        return true;
    }
    else {
        return false;
    }
}

void InitBarier(any value)
{
    Item barier = Game.GetItem(value);
    if (valid(barier)) {
        InitBariers(barier);
    }
    else {
        Game.Log("Barier item not valid");
    }
}

void InitTriggers(CBarier barier)
{
    DL("Инициализация триггеров. Количество: " + barier.Triggers.length());
    for (int i = 0; i < barier.Triggers.length(); i++) {
        barier.Triggers[i].OnCritterWalk.Subscribe(_WalkOnTrigg);
        barier.Triggers[i].Val1 = barier.GetGuardNum();
        barier.Triggers[i].Val2 = barier.Barier.Id;
        barier.Triggers[i].Val3 = barier.Barier.Val1;
    }
}

void _WalkOnTrigg(Item item, Critter cr, bool entered, uint8 dir) // TODO
{
    int netNum = 0;

    if (!cr.ControlledByPlayer) {
        return;
    }

    if (!entered) {
        return;
    }

    for (int i = 0; i < BariersNets.length(); i++) {
        for (int k = 0; k < BariersNets[i].Bariers.length(); k++) {
            if (BariersNets[i].Bariers[k].Barier.Id == item.Val2) {
                netNum = BariersNets[i].Bariers[k].Barier.Val1;
                if (BariersNets[i].Bariers[k].FieldMode == BM_OFF_NO_GUARD) {
                    return;
                }
            }
        }
    }

    Map map = cr.GetMap();
    if (not valid(map)) {
        return;
    }

    Critter[] npcs = map.GetCritters(cr.Hex, RADIUS, CritterFindType::NonDeadNpc);
    for (int i = 0; i < npcs.length(); i++) {
        if (npcs[i].NpcRole != item.Val1) {
            npcs.removeAt(i);
            i--;
        }
    }

    if (!npcs.isEmpty()) {
        CGuards g = GetGuards(npcs, netNum, item.Val2);
        if (!valid(g)) {
            return;
        }
        if (not g.CritIsAccessible(cr)) {
            CritterState::Wait(cr, 0);
            // cr.MoveToDir((cr.Dir+3)%6);
            int i = Game.Random(0, npcs.length() - 1);
            Dialogs::RunDialog(cr, Content::Dialog::all_energy_barier_guards, npcs[i], true);
        }
    }
}

void DeleteBlockersAndTriggers(Map map, int netNum)
{
    Item[] items = map.GetItems(Content::Item::eb_blocker);
    for (int i = 0; i < items.length(); i++) {
        if (int(items[i].Val1) == netNum) {
            Game.DestroyItem(items[i]);
        }
    }

    items = map.GetItems(Content::Item::eb_trigger);
    for (int i = 0; i < items.length(); i++) {
        if (int(items[i].Val1) == netNum) {
            Game.DestroyItem(items[i]);
        }
    }
}

bool CritIsAccessible(Critter cr)
{
    return CheckInven(cr);
}

int GetNetState(ident locId, int netNum)
{
    CBariersNet net = GetBariersNet(netNum, locId);
    if (valid(net)) {
        return net.NetState;
    }
    return 0;
}

void _DamageCritter(Item item, Critter cr, bool entered, uint8 dir)
{
    if (entered) {
        CritterState::Wait(cr, 0);
        Combat::InjureCritter(cr, Game.Random(MIN_DMG_SPATIAL, MAX_DMG_SPATIAL), DAMAGE_TYPE, Game.Random(0, 5), ZERO_IDENT, 0);
    }
}

bool _DamageCritterUse(Item item, Critter cr, CritterProperty skill)
{
    CBarier barier = GetBarier(item.Id);
    if (!valid(barier)) {
        return true;
    }
    else {
        if (skill != CritterProperty::SkillScience or !SKILL_SUCCESS(cr, CritterProperty::SkillScience, 0)) {
            if (barier.GetFieldMode() == BM_HALF_ON) {
                Combat::InjureCritter(cr,
                                      Game.Random(MIN_DMG_USE_LOW, MAX_DMG_USE_LOW),
                                      DAMAGE_TYPE,
                                      Game.GetDirection(item.Hex, cr.Hex),
                                      ZERO_IDENT,
                                      HF_BYPASS_ARMOR);
                if (skill == CritterProperty::SkillScience) {
                    Messaging::Info(cr, TextPackName::Text, MSG_FIELD_SCIENCE_USE_FAIL);
                }
                else {
                    Messaging::Info(cr, TextPackName::Text, MSG_SHOCK_PAIN_FIELD_USE);
                }
            }
            else {
                Combat::InjureCritter(cr,
                                      Game.Random(MIN_DMG_USE_HI, MAX_DMG_USE_HI),
                                      DAMAGE_TYPE,
                                      Game.GetDirection(item.Hex, cr.Hex),
                                      ZERO_IDENT,
                                      HF_BYPASS_ARMOR);
                if (skill == CritterProperty::SkillScience) {
                    Messaging::Info(cr, TextPackName::Text, MSG_FIELD_SCIENCE_USE_FAIL);
                }
                else {
                    Messaging::Info(cr, TextPackName::Text, MSG_SHOCK_FULL_FIELD_USE);
                }
            }
        }
        else {
            Messaging::Info(cr, TextPackName::Text, MSG_FIELD_SCIENCE_USE_WIN);
        }
    }
    return true;
}

bool _UseEmitter(Item item, Critter cr, CritterProperty skill)
{
    CBarier field;
    bool autoOn;
    int fieldMode = GetFieldMode(item);
    if (skill == CritterProperty::SkillRepair) {
        if (Time::IsTimeoutActive(cr.TimeoutSkRepair)) {
            Messaging::Info(cr, TextPackName::Game, MsgStr::StrSkillWeariness);
            return true;
        }
        if (SKILL_SUCCESS(cr, CritterProperty::SkillRepair, SCIENCE_PENALTY + int(item.Val0))) {
            field = GetBarier(item.Hex.x, item.Hex.y, item.ProtoId);
            if (!valid(field)) {
                return false;
            }
            if ((fieldMode == BM_ON) or (fieldMode == BM_HALF_ON)) {
                Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_WIN_OFF);
                autoOn = (field.Blockers[0].Val4 == 0) ? true : false;
                if (autoOn) {
                    DL("Автовключение");
                    any[] values = {};
                    values.insertLast(field.Blockers[0].Id);
                    Game.StartTimeEvent(TIME_BARIER_OPEN, TurnOnBarier, values); //SavedDeferredCall
                }
                field.ChangeMode(BM_OFF);
            }
            else {
                Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_WIN_ON);
                field.ChangeMode(BM_ON);
            }
            if (!GaveExp(cr, item.Id)) {
                cr.Experience += EXP_DISABLE_FORCE_FIELD;
            }
        }
        else {
            field = GetBarier(item.Hex.x, item.Hex.y, item.ProtoId);
            if ((field.GetFieldMode() == BM_ON) or (field.GetFieldMode() == BM_HALF_ON)) {
                Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_FAIL_OFF);
            }
            else {
                Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_FAIL_ON);
            }
        }
        cr.TimeoutSkRepair = REPAIR_TIMEOUT(cr);
    }
    else {
        return false;
    }
    return true;
}

bool _UseOnEmitter(Item item, Critter cr, Item usedItem, int param)
{
    if (!valid(usedItem)) {
        return true;
    }

    if ((usedItem.ProtoId == Content::Item::multi_tool) or (usedItem.ProtoId == Content::Item::super_tool_kit)) {
        return ObjectUse(cr, item, usedItem.ProtoId);
    }
    else {
        return false;
    }
}

bool GaveExp(Critter cr, ident itemId) // TODO
{
    return false;
}

bool ObjectUse(Critter cr, Item item, hstring proto)
{
    CBarier field;
    bool autoOn;
    int fieldMode = GetFieldMode(item);
    int bonus = 0;
    if (proto == Content::Item::super_tool_kit) {
        bonus = SUPER_TOOL_BONUS;
    }
    else if (proto == Content::Item::multi_tool) {
        bonus = TOOL_BONUS;
    }

    if (Time::IsTimeoutActive(cr.TimeoutSkScience)) {
        Messaging::Info(cr, TextPackName::Game, MsgStr::StrSkillWeariness);
        return true;
    }

    if (SKILL_SUCCESS(cr, CritterProperty::SkillScience, bonus + int(item.Val0))) {
        field = GetBarier(item.Hex.x, item.Hex.y, item.ProtoId);
        if (!valid(field)) {
            return false;
        }

        if ((fieldMode == BM_ON) or (fieldMode == BM_HALF_ON)) {
            Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_WIN_OFF);
            autoOn = (field.Blockers[0].Val4 == 0) ? true : false;
            if (autoOn) {
                DL("Автовключение");
                any[] values = {};
                values.insertLast(field.Blockers[0].Id);
                Game.StartTimeEvent(TIME_BARIER_OPEN, TurnOnBarier, values); //SavedDeferredCall
            }
            field.ChangeMode(BM_OFF);
        }
        else {
            Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_WIN_ON);
            field.ChangeMode(BM_ON);
        }

        if (!GaveExp(cr, item.Id)) {
            cr.Experience += EXP_DISABLE_FORCE_FIELD;
        }

        cr.TimeoutSkScience = SCIENCE_TIMEOUT(cr);
    }
    else {
        field = GetBarier(item.Hex.x, item.Hex.y, item.ProtoId);
        if (field.GetFieldMode() == BM_ON or field.GetFieldMode() == BM_HALF_ON) {
            Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_FAIL_OFF);
        }
        else {
            Messaging::Info(cr, TextPackName::Text, MSG_REPAIR_FAIL_ON);
        }

        cr.TimeoutSkScience = SCIENCE_TIMEOUT(cr);
    }
    return true;
}

int GetFieldMode(Item item)
{
    CBarier field = GetBarier(item.Hex.x, item.Hex.y, item.ProtoId);
    if (valid(field)) {
        return field.GetFieldMode();
    }
    return 0;
}

CBarier GetBarier(int hexX, int hexY, hstring proto)
{
    CBarier barier;
    for (int i = 0; i < BariersNets.length(); i++) {
        for (int k = 0; k < BariersNets[i].Bariers.length(); k++) {
            barier = BariersNets[i].Bariers[k];
            if ((int(barier.Barier.Hex.x) == (hexX + GetFieldCoord(hexX, proto, COORD_X))) and
                (int(barier.Barier.Hex.y) == (hexY + GetFieldCoord(hexX, proto, COORD_Y)))) {
                DL("Барьер найден");
                return barier;
            }
        }
    }
    DL("Барьер не найден. " + GetFieldCoord(hexX, proto, COORD_X) + " " + GetFieldCoord(hexY, proto, COORD_Y));
    return null;
}

void _BarierInit(Item barier, bool firstTime)
{
    InitBariers(barier);
}

void _EmitterInit(Item emitter, bool firstTime)
{
    InitEmitter(emitter);
}

bool s_Terminal(Critter player, StaticItem terminal, Item item, any skill)
{
    Map map = player.GetMap();
    if (not valid(map)) {
        return false;
    }

    auto params = terminal.SceneryParams;
    int netNum = params[0].hash;
    int hackBonus = params[1].hash;
    int hitBonus = params[2].hash;

    ident itemId;
    int mode = 0;
    if (valid(item)) {
        if (item.ProtoId == Content::Item::energy_barier_access_card) {
            mode = MODE_USE_CARD;
            itemId = item.Id;
        }
    }
    else {
        if (skill != CritterProperty::SpecialSkillPickOnGround) {
            if (skill == CritterProperty::SkillScience) {
                mode = MODE_HACK;
            }
        }
        else {
            mode = MODE_STANDART;
        }
    }
    if (mode == 0) {
        return false;
    }

    player.EnergyBarierTerminalHex = terminal.Hex;
    player.EnergyBarierNetNum = netNum;
    player.EnergyBarierHackBonus = hackBonus;
    player.EnergyBarierHitBonus = hitBonus;

    synctime timeBrocken = ZERO_SYNCTIME;
    synctime timeAccessDenied = ZERO_SYNCTIME;
    any[] energyBarierTerminalInfo = map.EnergyBarierTerminalInfo[MakeTerminalKey(terminal.Hex, 0)];
    if (energyBarierTerminalInfo.length() > 0) {
        timeBrocken = energyBarierTerminalInfo[0];
        timeAccessDenied = energyBarierTerminalInfo[1];
    }

    any[] data = {};
    data.insertLast(mode);
    data.insertLast(itemId);
    data.insertLast(timeBrocken);
    data.insertLast(timeAccessDenied);
    auto energyBarierTerminal = map.EnergyBarierTerminal.clone();
    energyBarierTerminal[MakeTerminalKey(terminal.Hex, netNum)] = data;
    map.EnergyBarierTerminal = energyBarierTerminal;

    Dialogs::RunDialog(player, Content::Dialog::all_energy_barier_terminal, true);
    return true;
}

void r_GetAccessCard(Critter master, Critter slave)
{
    Item item = master.AddItem(Content::Item::energy_barier_access_card, 1);
    item.Val4 = Game.SynchronizedTime;
    item.Val5 = master.Id;
}

int r_Start(Critter master, Critter slave)
{
    Map map = master.GetMap();
    if (!valid(map)) {
        return 0;
    }

    Location loc = map.GetLocation();
    if (!valid(loc)) {
        return 0;
    }

    CBariersNet net = GetBariersNet(master.EnergyBarierNetNum, loc.Id);
    if (!valid(net)) {
        return 0;
    }

    if (!net.TerminalsDisabled) {
        int key = MakeTerminalKey(master.EnergyBarierTerminalHex, master.EnergyBarierNetNum);
        any[] data = map.EnergyBarierTerminal[key];
        if (data.length() > 0) {
            if (data[2] != 0 && synctime(data[2]) > Game.SynchronizedTime) {
                return FD_TERM_BROKEN;
            }
            else if (data[3] != 0 && synctime(data[3]) > Game.SynchronizedTime) {
                return FD_TERM_NOT_ACCESS;
            }
            else if (BarierOpen(master)) {
                return FD_BARIER_OPEN;
            }
            else if (data[0] == MODE_STANDART) {
                return FD_NORMAL;
            }
            else {
                if (data[0] == MODE_USE_CARD) {
                    Item card = Game.GetItem(data[1]);
                    if (!valid(card)) {
                        Game.Log("Access card not valid");
                        return 0;
                    }
                    if (not CheckCard(card, master)) {
                        return FD_NOT_VALID_CARD;
                    }
                    else {
                        CBariersNet net_ = GetBariersNet(master.EnergyBarierNetNum, loc.Id);
                        if (valid(net_)) {
                            net_.ChangeNetMode(BM_OFF, AE_DEFAULT);
                        }
                        return FD_TURN_OFF_FIELD;
                    }
                }
                else {
                    if (data[0] == MODE_HACK) {
                        return doHack(master, master.EnergyBarierTerminalHex.x, master.EnergyBarierTerminalHex.y);
                    }
                }
            }
        }
    }
    else {
        return FD_DISABLED;
    }
    return 0;
}

int r_doHack(Critter master, Critter slave)
{
    return doHack(master, master.EnergyBarierTerminalHex.x, master.EnergyBarierTerminalHex.y);
}

int doHack(Critter master, int hexX, int hexY)
{
    bool guardNotFound = true;
    Map map = master.GetMap();
    if (not valid(map)) {
        Game.Log("Map not valid");
        return 0;
    }

    Location loc = map.GetLocation();
    if (!valid(loc)) {
        Game.Log("Error: location not valid");
        return 0;
    }

    CBariersNet net = GetBariersNet(master.EnergyBarierNetNum, loc.Id);
    Critter[] npcs = map.GetCritters(master.Hex, RADIUS, CritterFindType::NonDeadNpc);
    for (int i = 0; i < npcs.length(); i++) {
        for (int k = 0; k < net.Bariers.length(); k++) {
            if (npcs[i].NpcRole == int(net.Bariers[k].GetGuardNum())) {
                guardNotFound = false;
            }
        }
        if (guardNotFound) {
            npcs.removeAt(i);
            i--;
        }
    }

    if (npcs.length() != 0) {
        CGuards g = GetGuards(npcs, master.EnergyBarierNetNum, 0);
        if (!valid(g)) {
            return 0;
        }

        if (not g.CritIsAccessible(master)) {
            for (int i = 0; i < npcs.length(); i++) {
                NpcPlanes::AddAttackPlane(npcs[i], AI_PLANE_ATTACK_PRIORITY + 1, master);
            }
        }
    }

    int hRoll = HackRoll(master);
    if (!Stdlib::IsSuccessRvS(hRoll)) {
        if (Stdlib::IsCriticalRvS(hRoll)) {
            any[] adTermInfo = {};
            adTermInfo.insertLast(ZERO_SYNCTIME);
            adTermInfo.insertLast(Game.SynchronizedTime + TIME_NOT_ACCESS);

            auto energyBarierTerminalInfo = map.EnergyBarierTerminalInfo.clone();
            energyBarierTerminalInfo[MakeTerminalKey(mpos(hexX, hexY), 0)] = adTermInfo;
            map.EnergyBarierTerminalInfo = energyBarierTerminalInfo;
            return FD_TERM_NOT_ACCESS;
        }

        return FD_HACK_NOT_SUCC;
    }

    return FD_HACK_SUCC;
}

int r_TurnOfBarier(Critter master, Critter slave)
{
    Map map = master.GetMap();
    if (!valid(map)) {
        Game.Log("map not valid");
        return 0;
    }

    Location loc = map.GetLocation();
    if (!valid(loc)) {
        Game.Log("Error: location not valid");
        return 0;
    }

    CBariersNet net = GetBariersNet(master.EnergyBarierNetNum, loc.Id);
    if (valid(net)) {
        net.ChangeNetMode(BM_OFF, AE_DEFAULT);
    }
    return FD_TURN_OFF_FIELD;
}

void r_CheckHit(Critter master, Critter slave)
{
    // TODO
}

int r_CheckAccessCard(Critter master, Critter slave)
{
    if (CheckInven(master)) {
        Map map = master.GetMap();
        if (!valid(map)) {
            Game.Log("map not valid");
            return 0;
        }

        Location loc = map.GetLocation();
        if (!valid(loc)) {
            Game.Log("Error: location not valid");
            return 0;
        }

        CBariersNet net = GetBariersNet(master.EnergyBarierNetNum, loc.Id);
        if (valid(net)) {
            net.ChangeNetMode(BM_OFF, AE_DEFAULT);
        }
    }
    return FD_TURN_OFF_FIELD;
}

void TurnOnBariers(any[] values)
{
    CBariersNet net = GetBariersNet(values[1], values[0]);
    if (valid(net)) {
        net.ChangeNetMode(values[2], AE_DEFAULT);
    }
}

void TurnOnBarier(any[] values)
{
    for (int i = 0; i < BariersNets.length(); i++) {
        for (int k = 0; k < BariersNets[i].Bariers.length(); k++) {
            CBarier barier = BariersNets[i].Bariers[k];
            if (barier.Barier.Id == values[0]) {
                barier.ChangeMode(BM_ON);
            }
        }
    }
}

void r_StartGuard(Critter master, Critter npc)
{
    // TODO
}

void r_HalfOn(Critter master, Critter npc)
{
    Map map = master.GetMap();
    if (!valid(map)) {
        Game.Log("map not valid");
        return;
    }

    Location loc = map.GetLocation();
    if (!valid(loc)) {
        Game.Log("Error: location not valid");
        return;
    }

    CBariersNet net = GetBariersNet(master.EnergyBarierNetNum, loc.Id);
    if (valid(net)) {
        net.ChangeNetMode(BM_HALF_ON, AE_DEFAULT);
    }
}

void DisableForceField(any[] values)
{
    CBariersNet net = GetBariersNet(values[0], values[1]);
    if (valid(net)) {
        net.ChangeNetMode(BM_OFF_NO_GUARD, AE_OFF);
    }
}

void DisableForceFieldWithGuards(any[] values)
{
    CBariersNet net = GetBariersNet(values[0], values[1]);
    if (valid(net)) {
        net.ChangeNetMode(BM_OFF, AE_OFF);
    }
}

void EnableForceField(any[] values)
{
    CBariersNet net = GetBariersNet(values[0], values[1]);
    if (valid(net)) {
        net.ChangeNetMode(BM_ON, AE_OFF);
    }
}

// реализация охранников
CGuards GetGuards(Critter[] guards, int netNum, int barierId)
{
    if (guards.length() == 0) {
        return null;
    }

    CGuards g;
    Map map = guards[0].GetMap();
    if (!valid(map)) {
        Game.Log("map not valid");
        return null;
    }

    Location loc = map.GetLocation();
    if (!valid(loc)) {
        Game.Log("location not valid");
        return null;
    }

    CBariersNet net = GetBariersNet(netNum, loc.Id);
    if (!valid(net)) {
        Game.Log("Net not valid");
        return null;
    }

    if (loc.ProtoId == Content::Item::throwing_knife) {
        g = CGuards_GeckCity();
    }
    else {
        g = CGuards();
    }
    return g;
}

class CGuards
{
    bool CritIsAccessible(Critter cr)
    {
        return CheckInven(cr);
    }
}

class CGuards_GeckCity : CGuards
{
    bool CritIsAccessible(Critter cr)
    {
        Messaging::SayOnHead(cr, "Hi there!");
        return true;
    }
}

#endif

}
