// FOS Server

void ModuleInit()
{
    Game.OnCritterIdle.Subscribe(OnCritterIdle);
}

void OnCritterIdle(Critter cr)
{
    if (cr.IsControlledByPlayer || cr.IsNoFavoriteItem) {
        return;
    }

    hstring[] pids = cr.FavoriteItemPid.clone();

    if (pids.length() == 0 && NpcPlanes::IsNoPlanes(cr) && !cr.IsMoving()) {
        // IF not any favorite item known, remove current weapon from hands on idle
        auto mainItem = cr.GetItem(ItemProperty::CritterSlot, CritterItemSlot::Main);
        if (mainItem != null) {
            cr.ChangeItemSlot(mainItem.Id, CritterItemSlot::Inventory);
            return;
        }
    }

    for (uint i = 1; i < pids.length(); i++) {
        Item slotItem = cr.GetItem(ItemProperty::CritterSlot, CritterItemSlot(i));
        if (slotItem != null && pids[i] != slotItem.ProtoId) {
            cr.ChangeItemSlot(slotItem.Id, CritterItemSlot::Inventory);
            break;
        }
        else if (slotItem == null && pids[i] != EMPTY_HSTRING) {
            Item item = cr.GetItem(pids[i]);
            if (item != null && item.CritterSlot != CritterItemSlot::Inventory) {
                cr.ChangeItemSlot(item.Id, CritterItemSlot(i));
                break;
            }
        }
    }
}

hstring GetFavoriteItemPid(Critter cr, uint slot)
{
    hstring[] pids = cr.FavoriteItemPid.clone();
    if (slot > 0 && slot < pids.length()) {
        return pids[slot];
    }
    return EMPTY_HSTRING;
}

void SetFavoriteItemPid(Critter cr, int slot, hstring pid)
{
    if (slot > 0) {
        hstring[] pids = cr.FavoriteItemPid.clone();
        int len = pids.length();
        if (len <= slot) {
            pids.resize(slot + 1);
        }
        pids[slot] = pid;
        cr.FavoriteItemPid = pids;
    }
}
