// FOS Server
// Author: cvet
// Script attached to all graves, in protos.
// Graves pids: 344..347, 374..376.

//#include "Tla"

////import bool SwitchLocker0( Item& locker, bool toOpen ) from "Lockers";


void _GraveInit( Item& grave, bool firstTime )
{
    __EventCritterUseSkill.SubscribeToOnItem( grave, _GraveSkill );
    __EventCritterUseItem.SubscribeToOnItem( grave, _GraveUseOnMe );
}

bool _GraveSkill( Critter& cr, CritterProperty skill, Critter@, Item@ grave, const Item@ )
{
    if( skill == SKILL_PICK_ON_GROUND || skill == CritterProperty::SkillLockpick )
    {
        if( grave.Opened )
            Lockers::ShowContainer( cr, null, grave, TransferTypes::HexContDown );
        else
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrUseNothing );
        return false;
    }
    return true;
}

bool _GraveUseOnMe( Critter& cr, Item& usedItem, Critter@ onCritter, Item@ grave, const Item@ onScenery, uint param  )
{
    if( valid( usedItem ) && usedItem.ProtoId == Content::Item::shovel )
    {
        Lockers::SwitchLocker( grave, !grave.Opened );
        if( grave.Opened )
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrGraveUncovered );
            cr.Karma -= 5;
            cr.KarmaPerkGraveDigger = 1;
        }
        else
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrGraveCovered );
        }
        return true;
    }
    return false;
}
