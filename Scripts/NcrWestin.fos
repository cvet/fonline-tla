// FOS Server Sort 2
// Скрипты советника Вейстина
// rifleman17

///@ Property Critter PrivateServer hstring NcrWestinMapPidTo
///@ Property Critter PrivateServer uint NcrWestinHexNumTo
///@ Property Critter PrivateServer ident NcrWestinEveryEveningInvokeId
///@ Property Critter PrivateServer ident NcrWestinEveryMorningInvokeId

///@ Property Game PrivateServer uint8 NcrWestinPositionGlobal

#define PLANE_MOVE_TO_EXIT_GRID    (17) // план: Вейстин идет к выходу из локации для последующего телепорта.
#define MAP_PID_NCR_COUNCIL        (Content::Map::ncr_council) // прототип карты НКР - здание правительства
#define ENT_HEX_ID_COUNCIL_HOME    (17) // номер EN гекса - исходная позиция в здании правительства
#define ENT_HEX_ID_COUNSIL_TRANSIT (18) // номер EN гекса - позиция перед выходом из локации  правительства
#define MAP_PID_RANCH              (Content::Map::ncr_ranch) // прототип карты НКР - Ранчо вейстина
#define ENT_HEX_ID_RANCH_HOME      (17)                      // номер EN гекса - исходная позиция на Ранчо
#define ENT_HEX_ID_RANCH_TRANSIT   (18)                      // номер EN гекса - позиция перед выходом из Ранчо

#define NPC_ROLE_WESTIN            (19) // роль = вейстин

/*
   {2930}{}{Тяжелый день...}
   {2931}{}{Ну вот, опять нужно куда-то идти...}
 */

#define STR_NUM_GO_HOME               (2930)
#define STR_NUM_GO_WORK               (2931)

#define WESTIN_PLANE_MOVE_PRIORITY    (49)
#define WESTIN_PLANE_TRANSIT_PRIORITY (48)

#define WESTIN_POS_VAR_COUNCIL        (0)
#define WESTIN_POS_VAR_RANCH          (1)

void _WestinInit(Critter westin, bool firstTime)
{
    // ~settime 99 2246 11 3 20 50
    if (firstTime) {
        // каждые игровые сутки Вейстин уходит с работы до своего Ранчо в 21:00
        westin.NcrWestinEveryEveningInvokeId =
            Game.SavedDeferredCall(GAME_SECOND_TO_TICK(Time::GetNearFullSecond(0, 0, 0, 21, 0, 0) - Game.GetFullSecond()), EveryEvening, westin.Id);

        // каждые игровые сутки Вейстин пытается дойти до здания правительства в 07:00
        westin.NcrWestinEveryMorningInvokeId =
            Game.SavedDeferredCall(GAME_SECOND_TO_TICK(Time::GetNearFullSecond(0, 0, 0, 7, 0, 0) - Game.GetFullSecond()), EveryMorning, westin.Id);
    }
    westin.NpcRole = NPC_ROLE_WESTIN;
    westin.OnFinish.Subscribe(_WestinFinish);
}

// при удалении нпц удаляются обработчики временных событий, чтобы не было ошибок о null ptr
void _WestinFinish(Critter westin)
{
    Game.CancelDeferredCall(westin.NcrWestinEveryEveningInvokeId);
    Game.CancelDeferredCall(westin.NcrWestinEveryMorningInvokeId);
    // если Вейстин был на карте Ранчо, его нужно телепортировать в консульство
}

// вечернее событие
void EveryEvening(any value)
{
    // Game.Log("вечерняя прогулка вейстина");

    Critter westin = Game.GetCritter(value);
    uint time = GAME_SECOND_TO_TICK(Time::GetNearFullSecond(0, 0, 0, 21, 0, 0) - Game.GetFullSecond());

    if (!valid(westin)) {
        westin.NcrWestinEveryEveningInvokeId = Game.SavedDeferredCall(time, EveryEvening, value);
        return;
    }
    if (!westin.IsAlive()) {
        westin.NcrWestinEveryEveningInvokeId = Game.SavedDeferredCall(time, EveryEvening, value);
        return;
    }

    westin.SayMsg(SAY_NORM_ON_HEAD, TextPackName::Text, STR_NUM_GO_HOME);
    // куда телепортироваться
    westin.NcrWestinMapPidTo = MAP_PID_RANCH;
    westin.NcrWestinHexNumTo = ENT_HEX_ID_RANCH_HOME;
    // координаты гекса, до которого должен дойти Вейстин перед телепортом

    if (westin.GetMap().ProtoId == MAP_PID_RANCH) {
        westin.NcrWestinEveryEveningInvokeId = Game.SavedDeferredCall(time, EveryEvening, value);
        return;
    }

    uint16 hx = westin.HexX;
    uint16 hy = westin.HexY;
    Entrance::GetNearEntry(westin.GetMap(), ENT_HEX_ID_COUNSIL_TRANSIT, hx, hy);
    NpcPlanes::AddWalkPlane(westin, WESTIN_PLANE_MOVE_PRIORITY, PLANE_MOVE_TO_EXIT_GRID, 0, hx, hy, Game.Random(0, 5), false, Game.Random(0, 1));
    NpcPlanes::AddMiscPlane(westin, WESTIN_PLANE_TRANSIT_PRIORITY, 0, NcrWestin::plane_Transit);

    westin.NcrWestinEveryEveningInvokeId = Game.SavedDeferredCall(time, EveryEvening, value);
}

// утреннее событие
void EveryMorning(any value)
{
    // Game.Log("утренняя прогулка вейстина");

    Critter westin = Game.GetCritter(value);
    uint time = GAME_SECOND_TO_TICK(Time::GetNearFullSecond(0, 0, 0, 7, 0, 0) - Game.GetFullSecond());
    if (!valid(westin)) {
        westin.NcrWestinEveryMorningInvokeId = Game.SavedDeferredCall(time, EveryMorning, westin.Id);
        return;
    }
    if (!westin.IsAlive()) {
        westin.NcrWestinEveryMorningInvokeId = Game.SavedDeferredCall(time, EveryMorning, westin.Id);
        return;
    }

    westin.SayMsg(SAY_NORM_ON_HEAD, TextPackName::Text, STR_NUM_GO_WORK);

    westin.NcrWestinMapPidTo = MAP_PID_NCR_COUNCIL;
    westin.NcrWestinHexNumTo = ENT_HEX_ID_COUNCIL_HOME;

    if (westin.GetMap().ProtoId == MAP_PID_NCR_COUNCIL) {
        westin.NcrWestinEveryMorningInvokeId = Game.SavedDeferredCall(time, EveryMorning, westin.Id);
        return;
    }

    // координаты гекса, до которого должен дойти Вейстин перед телепортом
    uint16 hx = westin.HexX;
    uint16 hy = westin.HexY;
    Entrance::GetNearEntry(westin.GetMap(), ENT_HEX_ID_RANCH_TRANSIT, hx, hy);
    NpcPlanes::AddWalkPlane(westin, WESTIN_PLANE_MOVE_PRIORITY, PLANE_MOVE_TO_EXIT_GRID, 0, hx, hy, Game.Random(0, 5), false, Game.Random(0, 1));
    NpcPlanes::AddMiscPlane(westin, WESTIN_PLANE_TRANSIT_PRIORITY, 0, NcrWestin::plane_Transit);

    westin.NcrWestinEveryMorningInvokeId = Game.SavedDeferredCall(time, EveryMorning, westin.Id);
}

// телепортация на карту с прототипом, сохраненным в NcrWestinMapPidTo
void plane_Transit(Critter westin)
{
    // Game.Log(" try transit");
    if (westin.NcrWestinMapPidTo == EMPTY_HSTRING) {
        return;
    }
    Map map = westin.GetMap();
    if (!valid(map)) {
        return;
    }
    if (map.ProtoId == westin.NcrWestinMapPidTo) {
        return;
    }

    Map mapTo = Game.GetMap(westin.NcrWestinMapPidTo);
    if (!valid(mapTo)) {
        return;
    }
    // проверяем, что на карте назначения нет уже такого нпц
    // если есть, удаляем этого.
    auto westins = mapTo.GetCritters(CritterProperty::NpcRole, NPC_ROLE_WESTIN, CritterFindType::Npc);
    if (!westins.isEmpty()) {
        Game.Log("Multiple westins founded. Kill'em all");
        for (uint i = 0; i < westins.length(); i++) {
            auto westin2 = westins[i];
            if (westin2.DialogId == westin.DialogId) {
                Game.Log("Fake Westin successfully terminated!");
                Game.DeleteCritter(westin2);
                return;
            }
        }
    }

    if (Obsolete::CritterTransitToMapEntry(westin, mapTo.Id, HASHI(westin.NcrWestinHexNumTo))) {
        westin.HomeMapId = mapTo.Id;
        westin.HomeHexX = westin.HexX;
        westin.HomeHexY = westin.HexY;
        westin.HomeDir = westin.Dir;
        Game.NcrWestinPositionGlobal = westin.NcrWestinMapPidTo == MAP_PID_NCR_COUNCIL ? WESTIN_POS_VAR_COUNCIL : WESTIN_POS_VAR_RANCH;
    }
}
