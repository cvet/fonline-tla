namespace Effects
{

///@ RemoteCall Client DoFadeScreen(ucolor fromColor, ucolor toColor, timespan time)
///@ RemoteCall Client DoQuakeScreen(int32 noise, timespan time)
///@ RemoteCall Client DoRunEffect(hstring effectPid, mpos hex, int32 radius)
///@ RemoteCall Client DoRunFlyEffect(hstring effectPid, ident crId, ident targetId, mpos hex, mpos targetHex)
///@ RemoteCall Client DoPlaySound(hstring soundName)

#if SERVER

// Default values
const int QuakeNoise = 8;
const timespan QuakeTime = Time::Milliseconds(800);

void FadeScreen(Critter cr, bool fadeOut, timespan time)
{
    cr.PlayerClientCall.DoFadeScreen(fadeOut ? ucolor(0, 0, 0, 255) : ZERO_UCOLOR, fadeOut ? ZERO_UCOLOR : ucolor(0, 0, 0, 255), time);
}

void FadeScreen(Critter cr, ucolor fromColor, ucolor toColor, timespan time)
{
    cr.PlayerClientCall.DoFadeScreen(fromColor, toColor, time);
}

void FadeScreen(Map map, bool fadeOut, timespan time)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoFadeScreen(fadeOut ? ucolor(0, 0, 0, 255) : ZERO_UCOLOR, fadeOut ? ZERO_UCOLOR : ucolor(0, 0, 0, 255), time);
    }
}

void FadeScreen(Map map, ucolor fromColor, ucolor toColor, timespan time)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoFadeScreen(fromColor, toColor, time);
    }
}

void QuakeScreen(Critter cr)
{
    cr.PlayerClientCall.DoQuakeScreen(QuakeNoise, QuakeTime);
}

void QuakeScreen(Critter cr, int noise, timespan time)
{
    cr.PlayerClientCall.DoQuakeScreen(noise, time);
}

void QuakeScreen(Map map)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoQuakeScreen(QuakeNoise, QuakeTime);
    }
}

void QuakeScreen(Map map, int noise, timespan time)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoQuakeScreen(noise, time);
    }
}

void RunEffect(Map map, hstring effectPid, mpos hex, int radius = 0)
{
    Critter[] critters = map.GetCrittersWhoSeeHex(hex, radius, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunEffect(effectPid, hex, radius);
    }
}

void RunFlyEffect(Map map, hstring effectPid, Critter cr, Critter target)
{
    Critter[] critters = map.GetCrittersWhoSeePath(cr.Hex, target.Hex, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunFlyEffect(effectPid, cr.Id, target.Id, cr.Hex, target.Hex);
    }
}

void RunFlyEffect(Map map, hstring effectPid, Critter cr, mpos targetHex)
{
    Critter[] critters = map.GetCrittersWhoSeePath(cr.Hex, targetHex, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunFlyEffect(effectPid, cr.Id, ZERO_IDENT, cr.Hex, targetHex);
    }
}

void RunFlyEffect(Map map, hstring effectPid, mpos hex, mpos targetHex)
{
    Critter[] critters = map.GetCrittersWhoSeePath(hex, targetHex, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoRunFlyEffect(effectPid, ZERO_IDENT, ZERO_IDENT, hex, targetHex);
    }
}

void PlaySound(Map map, hstring soundName, mpos hex, int radius = 0)
{
    Critter[] critters = map.GetCrittersWhoSeeHex(hex, radius, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlaySound(soundName);
    }
}

void PlaySound(Map map, string soundName, mpos hex, int radius = 0)
{
    PlaySound(map, hstring(soundName), hex, radius);
}

void PlaySound(Map map, hstring soundName)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlaySound(soundName);
    }
}

void PlaySound(Map map, string soundName)
{
    PlaySound(map, hstring(soundName));
}

void PlaySound(Critter cr, hstring soundName)
{
    cr.PlayerClientCall.DoPlaySound(soundName);

    Critter[] critters = cr.GetCritters(true, CritterFindType::Players);

    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlaySound(soundName);
    }
}

void PlaySound(Critter cr, string soundName)
{
    PlaySound(cr, hstring(soundName));
}

#endif

#if CLIENT

void DoFadeScreen(ucolor fromColor, ucolor toColor, timespan time)
{
    Game.FadeScreen(fromColor, toColor, time, true);
}

void DoQuakeScreen(int noise, timespan time)
{
    Game.QuakeScreen(noise, time);
}

void DoRunEffect(hstring effectPid, mpos hex, int radius)
{
    CurMap.RunEffect(effectPid, hex);

    for (int i = 0; i < radius; i++) {
        for (uint8 dir = 0; dir < 6; dir++) {
            mpos hex2 = hex;

            if (CurMap.MoveHexByDir(hex2, dir, i + 1) != i + 1) {
                break;
            }

            for (int j = 0; j < i + 1; j++) {
                CurMap.RunEffect(effectPid, hex2);

                if (!CurMap.MoveHexByDir(hex2, (dir + 2) % 6)) {
                    break;
                }
            }
        }
    }
}

void DoRunFlyEffect(hstring effectPid, ident crId, ident targetId, mpos hex, mpos targetHex)
{
    Critter cr = Game.GetCritter(crId);
    Critter target = Game.GetCritter(targetId);

    hex = cr != null ? cr.Hex : hex;
    targetHex = target != null ? target.Hex : targetHex;

    CurMap.RunEffect(effectPid, hex, targetHex, 1.0f);
}

void DoPlaySound(hstring soundName)
{
    Game.PlaySound(soundName);
}

#endif

}
