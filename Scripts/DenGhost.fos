namespace DenGhost // Sort 2
{

#if SERVER

// Author: marvi

///@ Property Game Server bool DenGhostIsDead Mutable Persistent

// dialog string numbers
#define STRNUM_SCREAM       (1)
#define STRNUM_STEAL        (2)
#define STRNUM_STEAL_INJURE (3)

/*
   Critter initialization
 */
void _GhostInit(Critter ghost, bool firstTime)
{
    if (firstTime) {
        // set max possible HP
        ghost.MaxLifeBase = 9999;
        ghost.CurrentHp = 9999;
        // and armor class
        ghost.ArmorClassBase = 90;
    }

    // syncronize ghost actual state with var value
    if (ghost.Condition == CritterCondition::Dead && !Game.DenGhostIsDead) {
        CritterState::ToAlive(ghost);
    }
    else if (ghost.Condition != CritterCondition::Dead && Game.DenGhostIsDead) {
        CritterState::ToDead(ghost, CritterActionAnim::DeadBurnRun, null);
    }

    ghost.OnAttacked.Subscribe(_GhostAttacked);
    ghost.OnDead.Subscribe(_GhostDead);
    ghost.OnRespawn.Subscribe(_GhostRespawn);
    ghost.OnStealing.Subscribe(_GhostStealing);
}

//
// Event Handlers
//

bool _GhostAttacked(Critter ghost, Critter attacker)
{
    // regenerate HP
    ghost.CurrentHp = ghost.MaxLife;

    // scream to scary player
    Messaging::SayShoutOnHead(ghost, TextPackName::Dialogs, DLGSTR(Content::Dialog::den_ghost, STRNUM_SCREAM));

    // do nothing after attack
    return true;
}

void _GhostDead(Critter ghost, Critter killeer)
{
    // ghost killed not through quest dialog
    if (!Game.DenGhostIsDead) {
        // ressurect ghost
        Game.StartTimeEvent(Tla::GameSeconds(60), RessurectGhost, ghost.Id);
    }
}

void _GhostRespawn(Critter ghost)
{
    Game.DenGhostIsDead = false;
}

/*
   Handles stealing from ghost.
   Stealing attempt fails in any case with a chance for player to cripple right hand.
 */
void _GhostStealing(Critter ghost, Critter thief, Item item, int count)
{
    if (thief.ControlledByPlayer) {
        int injureHandChance = 20;
        int injureHandRoll = Game.Random(1, 100);

        if (injureHandRoll <= injureHandChance) {
            thief.IsDamagedRightArm = true;
            Messaging::Info(thief, TextPackName::Dialogs, DLGSTR(Content::Dialog::den_ghost, STRNUM_STEAL_INJURE));
        }
        else {
            Messaging::Info(thief, TextPackName::Dialogs, DLGSTR(Content::Dialog::den_ghost, STRNUM_STEAL));
        }
    }
}

//
// Dialog
//

/*
   Instantly kills ghost
 */
void r_KillGhost(Critter player, Critter ghost, int val)
{
    // confirm that ghost killed through quest dialog
    Game.DenGhostIsDead = true;
    // kill ghost
    CritterState::ToDead(ghost, CritterActionAnim::DeadBurnRun, null);
    // run new dialog through time event
    Game.StartTimeEvent(Tla::GameSeconds(60), RunTreasureDialog, player.Id);
}

//
// Time Event Handlers
//

/*
   Ressurects ghost
 */
void RessurectGhost(any value)
{
    Critter ghost = Game.GetCritter(value);
    if (!valid(ghost)) {
        return;
    }

    // try to ressurect ghost
    if (CritterState::ToAlive(ghost)) {
        return;
    }
    else {
        Game.StartTimeEvent(Tla::GameSeconds(2 * 60), RessurectGhost, value);
    }
}

/*
   Runs dialog about cursed treasure
 */
void RunTreasureDialog(any value)
{
    Critter player = Game.GetCritter(value);
    if (!valid(player)) {
        return;
    }

    Dialogs::RunDialog(player, Content::Dialog::den_ghost_treasure, false);
}

#endif

}
