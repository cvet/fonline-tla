// FOS Server
// Author: rifleman17
// Knights Of The Wasteland special encounter scripts

#define KOTW_HOLODISK (63)
#define BEER_BOTTLES  (20)
#define BEER_WEIGHT   (20000) // 20 кг

void _DeadInit(Critter cr, bool firstTime)
{
    if (!cr.IsDead()) {
        CritterState::ToDead(cr, Anim2Actions::DeadLaser, null);
    }

    cr.ReplicationTime = -1;
    if (firstTime) {
        Item holo = cr.AddItem(Content::Item::holodisk, 1);
        if (valid(holo)) {
            holo.HolodiskNum = KOTW_HOLODISK;
        }
    }
}

void _DungeonInit(Map map, bool firstTime)
{
    if (!firstTime) {
        return;
    }

    Item[] chests = map.GetItems(Content::Item::chest);
    for (uint i = 0; i < chests.length(); i++) {
        for (uint j = 0; j < BEER_BOTTLES - 1; j++) {
            chests[i].AddItem(Content::Item::kotw_beer, 1, j);
        }
    }

    uint num = Game.Random(0, chests.length() - 1);
    Item grail = chests[num].AddItem(Content::Item::kotw_grail, 1, 1);
    grail.SetupScript(_GrailInit);

    for (uint i = 0; i < chests.length(); i++) {
        if (i != num) {
            chests[i].AddItem(Content::Item::kotw_beer, 1, BEER_BOTTLES);
        }
    }
}

void _GrailInit(Item item, bool firstTime)
{
    item.OnCritterUse.Subscribe(_GrailUse);
}

bool _GrailUse(Item item, Critter cr, uint param)
{
    cr.CurrentHp = cr.MaxLife;
    cr.IsDamagedEye = false;
    cr.IsDamagedRightArm = false;
    cr.IsDamagedLeftArm = false;
    cr.IsDamagedRightLeg = false;
    cr.IsDamagedLeftLeg = false;
    Game.DeleteItem(item);
    return true;
}

void r_InfoMessage(Critter player, Critter none, int num)
{
    player.SayMsg(SAY_NETMSG, TEXTMSG_DLG, DLGSTR(Content::Dialog::se_kotw_console1, num));
}
