// FOS Server Client

///@ RemoteCall Server ProcessBarterNpcOffer(ident[] saleItemsIds, uint[] saleItemsCount, ident[] buyItemsIds, uint[] buyItemsCount)

#if CLIENT
enum Collection
{
    ChosenAll,
    Inventory,
    Loot,
    BarterOffer,
    BarterTraderOffer,
    RecievedItems
}

Item @[] ReceivedItems = {};
Item @[] BarterOfferItems = {};
Item @[] BarterTraderOfferItems = {};
Item @[] RemovedItems = {};

void ModuleInit()
{
    Game.OnReceiveItems.Subscribe(OnReceiveItems);
}

void OnReceiveItems(Item[] items, any contextParam)
{
    ReceivedItems.clear();
    BarterOfferItems.clear();
    BarterTraderOfferItems.clear();
    RemovedItems.clear();

    ReceivedItems = items;

    Gui::EngineCallback_ItemChanged(false);

    if (int(contextParam) != 0) {
        return;
    }

    Critter chosen = Game.GetChosen();
    if (chosen is null) {
        return;
    }

    TransferTypes transfer = chosen.TransferType;

    if (transfer != TransferTypes::Close) {
        if (transfer == TransferTypes::CritBarter) {
            Game.ShowScreen(CLIENT_SCREEN_BARTER, dict<string, any> = {{"CritterId", chosen.TransferContainerId}});
        }
        else {
            ident crId;
            ident itemId;

            if (transfer == TransferTypes::CritLoot || transfer == TransferTypes::CritSteal || transfer == TransferTypes::FarCrit) {
                crId = chosen.TransferContainerId;
            }
            else if (transfer == TransferTypes::HexContUp || transfer == TransferTypes::HexContDown || transfer == TransferTypes::SelfCont ||
                     transfer == TransferTypes::FarCont) {
                itemId = chosen.TransferContainerId;
            }

            Game.ShowScreen(CLIENT_SCREEN_PICKUP, dict<string, any> = {{"TargetCritterId", crId}, {"TargetItemId", itemId}});
        }
    }
}

Item[] GetCollection(Collection collection)
{
    Item[] items = {};
    Critter chosen = Game.GetChosen();

    if (collection == Collection::ChosenAll && chosen !is null) {
        items = chosen.GetItems();
    }
    else if (collection == Collection::Inventory && chosen !is null) {
        items = chosen.GetItems(ItemProperty::CritterSlot, CritterItemSlot::Inventory);
    }
    else if (collection == Collection::Loot) {
        items = ReceivedItems;
        /*if (chosen !is null && chosen.TransferContainerId != ZERO_IDENT) {
            Critter targetCr = Game.GetCritter(chosen.TransferContainerId);
            if (targetCr != null) {
                items = targetCr.GetItems(ItemProperty::CritterSlot, CritterItemSlot::Inventory);
                TODO: Уточнить, зачем здесь преметы забираютс¤ из инвентаря
                криттера, ведь они невовремя обновл¤ютс¤. На сервере предмет изъят из инвентаря
                на клиенте нет (если это не chosen)
            }
        }*/
    }
    else if (collection == Collection::BarterOffer) {
        items = BarterOfferItems;
    }
    else if (collection == Collection::BarterTraderOffer) {
        items = BarterTraderOfferItems;
    }
    else if (collection == Collection::RecievedItems) {
        items = ReceivedItems;
    }

    if (collection != Collection::BarterOffer && collection != Collection::BarterTraderOffer) {
        ExcludeItems(items, collection != Collection::Loot ? BarterOfferItems : BarterTraderOfferItems);
    }
    ExcludeItems(items, RemovedItems);

    return items;
}

void RemoveItem(Collection collection, Item& item, uint count)
{
    Assert(count > 0);
    Assert(count <= item.Count);

    RemovedItems.insertLast(item.Clone(count));

    Gui::EngineCallback_ItemChanged(false);
}

void BarterItem(bool offer, Item& item, uint count)
{
    // if( offer )
}

void ExcludeItems(Item[] & items, Item[] & excludeItems)
{
    for (uint i = 0; i < excludeItems.length(); i++) {
        Item excludeItem = excludeItems[i];
        for (uint j = 0; j < items.length(); j++) {
            Item item = items[j];
            if (excludeItem.Id == item.Id) {
                if (excludeItem.Count >= item.Count) {
                    items.removeAt(j);
                }
                else {
                    items[j] = item.Clone(item.Count - excludeItem.Count);
                }
                break;
            }
        }
    }
}

Collection GetCollectionFromType(int collectionType)
{
    Collection collection = Collection::ChosenAll;

    switch (collectionType) {
    case ITEMS_CHOSEN_ALL:
        collection = Collection::ChosenAll;
        break;
    case ITEMS_INVENTORY:
        collection = Collection::Inventory;
        break;
    case ITEMS_BARTER:
    case ITEMS_PICKUP_FROM:
        collection = Collection::Inventory;
        break;
    case ITEMS_PICKUP:
        collection = Collection::Loot;
        break;
    case ITEMS_BARTER_OFFER:
        collection = Collection::BarterOffer;
        break;
    case ITEMS_BARTER_OPPONENT_OFFER:
        collection = Collection::BarterTraderOffer;
        break;
    case ITEMS_BARTER_OPPONENT:
        collection = Collection::Loot;
        break;
    }
    return collection;
}

Item[] RefreshItemsCollection(int collectionType)
{
    Collection collection = GetCollectionFromType(collectionType);
    return GetCollection(collection);
}

void RemoveItemFromCollection(ident itemId, Collection collection)
{
    Item[] items = GetCollection(collection);
    for (uint j = 0; j < items.length(); j++) {
        Item item = items[j];
        if (itemId == item.Id) {
            items.removeAt(j);
            break;
        }
    }
}

void ChangeCollectionItemCount(ident itemId, Collection collection, uint count)
{
    Item[] items = GetCollection(collection);
    for (uint j = 0; j < items.length(); j++) {
        if (itemId == items[j].Id) {
            Item item = items[j].Clone(count);
            items.removeAt(j);
            items.insertAt(j, item);
            break;
        }
    }
}

Item GetCollectionItem(ident itemId, Collection collection)
{
    Item[] items = GetCollection(collection);
    for (uint i = 0; i < items.length(); i++) {
        if (items[i].Id == itemId) {
            return items[i];
        }
    }

    return Game.GetItem(itemId);
}

void BarterOffer()
{
    ident[] saleItemsIds = {};
    uint[] saleItemsCount = {};
    ident[] buyItemsIds = {};
    uint[] buyItemsCount = {};
    Critter chosen = Game.GetChosen();
    if (chosen is null) {
        return;
    }

    Item[] itemsSale = GetCollection(Collection::BarterOffer);
    Item[] itemsBuy = GetCollection(Collection::BarterTraderOffer);

    for (uint i = 0, l = itemsSale.length(); i < l; i++) {
        saleItemsIds.insertLast(itemsSale[i].Id);
        saleItemsCount.insertLast(itemsSale[i].Count);
    }

    for (uint i = 0, l = itemsBuy.length(); i < l; i++) {
        buyItemsIds.insertLast(itemsBuy[i].Id);
        buyItemsCount.insertLast(itemsBuy[i].Count);
    }

    CurPlayer.ServerCall.ProcessBarterNpcOffer(saleItemsIds, saleItemsCount, buyItemsIds, buyItemsCount);
}

void BarterTransfer(ident item_id, int collectionTypeFrom, uint item_count)
{
    Collection collectionFrom;
    Collection collectionTo;
    switch (collectionTypeFrom) {
    case ITEMS_BARTER:
        collectionTo = Collection::BarterOffer;
        collectionFrom = Collection::Inventory;
        break;
    case ITEMS_BARTER_OFFER:
        collectionTo = Collection::Inventory;
        collectionFrom = Collection::BarterOffer;
        break;
    case ITEMS_BARTER_OPPONENT:
        collectionTo = Collection::BarterTraderOffer;
        collectionFrom = Collection::Loot;
        break;
    case ITEMS_BARTER_OPPONENT_OFFER:
        collectionTo = Collection::Loot;
        collectionFrom = Collection::BarterTraderOffer;
        break;
    default:
        return;
    }

    Item @item = GetCollectionItem(item_id, collectionFrom);

    if (item == null) {
        return;
    }

    if (item_count > item.Count) {
        return;
    }

    Item @itemTo = null;
    Item[] itemsTo = GetCollection(collectionTo);

    if (item.Stackable) {
        for (uint i = 0; i < itemsTo.length(); i++) {
            if (itemsTo[i].ProtoId == item.ProtoId) {
                itemTo = itemsTo[i];
                break;
            }
        }
    }
    if (collectionTypeFrom == ITEMS_BARTER || collectionTypeFrom == ITEMS_BARTER_OPPONENT) {
        if (itemTo != null) {
            ChangeCollectionItemCount(item_id, collectionTo, itemTo.Count + item_count);
        }
        else {
            itemsTo.insertLast(item.Clone(item_count));
        }
    }
    else {
        if (item.Count - item_count == 0 || !item.Stackable) {
            RemoveItemFromCollection(item.Id, collectionFrom);
        }
        else {
            ChangeCollectionItemCount(item_id, collectionFrom, item.Count - item_count);
        }
    }

    Gui::EngineCallback_ItemChanged(false);
}

#endif
#if SERVER

void ProcessBarterNpcOffer(Player player, ident[] saleItemsIds, uint[] saleItemsCount, ident[] buyItemsIds, uint[] buyItemsCount)
{
    Barter::ProcessBarterNpcOffer(player.GetControlledCritter(), saleItemsIds, saleItemsCount, buyItemsIds, buyItemsCount);
}

#endif
