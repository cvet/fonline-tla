// FOS Server
// Name:   arroyo_rayders
// Author: Sufir

#include "Tla"

#pragma property Critter PrivateServer uint ArroyoRaydersAttackedId

// Defines
#define ARROYO_RAIDERS_MSG    ( 301 )

void _RaiderInit( Critter& raider, bool firstTime )
{
    raider.ReplicationTime = -1;
    raider.SetEvent( CRITTER_EVENT_SHOW_CRITTER, "_RaiderSaw" );
    raider.SetEvent( CRITTER_EVENT_HIDE_CRITTER, "_RaiderHide" );
    raider.SetEvent( CRITTER_EVENT_IDLE, "_RaiderIdle" );
    raider.SetEvent( CRITTER_EVENT_DEAD, "_RaiderDead" );
    raider.SetEvent( CRITTER_EVENT_MESSAGE, "_RaiderAlarm" );
    raider.ArroyoRaydersAttackedId = 0;   // Id атакуемого игрока.

    // Записываем группу рейдеров. Эти данные используются при выходе с карты, для определения все-ли рейдеры убиты...
    if( firstTime )
        Globals.ArroyoRaidersCount = Globals.ArroyoRaidersCount + 1;
}

void _RaiderSaw( Critter& raider, Critter& player )
{
    // Проверяем свободен ли криттер
    if( raider.ArroyoRaydersAttackedId != 0 )
        return;

    if( not raider.IsNoPlanes() )
        EraseAttackPlane( raider, player );

    if( player.IsPlayer() )
    {
        raider.ArroyoRaydersAttackedId = player.Id;
        raider.SendMessage( ARROYO_RAIDERS_MSG, raider.ArroyoRaydersAttackedId, MESSAGE_TO_WHO_SEES_ME );
        if( AddAttackPlane( raider, 0, player ) )
        {
            if( Random( 0, 8 ) == 0 )
                raider.SayMsg( SAY_SHOUT_ON_HEAD, TEXTMSG_DLG, DLGSTR( raider.DialogId, 0 ) );
            else if( Random( 0, 6 ) == 1 )
                raider.SayMsg( SAY_SHOUT_ON_HEAD, TEXTMSG_DLG, DLGSTR( raider.DialogId, 0 ) );
        }
    }
}

void _RaiderHide( Critter& raider, Critter& player )
{
    // Если игрок пропал из поля зрения, убираем план атаки
    if( player.IsPlayer() && raider.ArroyoRaydersAttackedId != 0 )
    {
        if( Random( 0, 10 ) == 0 )
            raider.SayMsg( SAY_SHOUT_ON_HEAD, TEXTMSG_DLG, DLGSTR( raider.DialogId, 1 ) );
        EraseAttackPlane( raider, player );
        if( raider.IsNoPlanes() )
            raider.ArroyoRaydersAttackedId = 0;
    }
}

void _RaiderIdle( Critter& raider )
{
    // Ищем игроков и атакуем
    Critter@[] players;
    raider.GetCritters( false, FIND_ONLY_PLAYERS | FIND_LIFE_AND_KO, players );
    if( players.length() > 0 )
        AddAttackPlane( raider, 0, players[ 0 ] );
}

void _RaiderDead( Critter& raider, Critter@ player )
{
    Globals.ArroyoRaidersCount = Globals.ArroyoRaidersCount - 1;
}

void _RaiderAlarm( Critter& raider, Critter& fromCrit, int message, int value )
{
    if( raider.Id != fromCrit.Id && raider.IsNoPlanes() )
    {
        raider.AddEnemyToStack( value );
        AddAttackPlane( raider, 0, GetCritter( value ) );
    }
}
