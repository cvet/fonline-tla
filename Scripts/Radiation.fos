// FOS Server
// Author: cvet
// Original Fallout2 system

#define RADIATION_STAGES           ( 6 )
#define RADIATION_STATS            ( 8 )
#define RADIATION_DURATION         ( 10080 * 60 )
#define RADIATION_DAMAGE_VALUE     ( 66 )
#define RADIATION_HIGH_DOSE        ( 66 )
#define RADIATION_DEAD_DOSE        ( 1800 )

#define STR_RADIATION_STAGE        # (stage) ( 12799 + ( stage ) )
#define STR_RADIATION_DIE          ( 12806 )
#define STR_RADIATION_HIGH_DOSE    ( 12807 )

// clang-format off

const int[] RadiationEffects =
{
    1,  100,  200,  400,  600, 1000,                                                                 /*stages values*/
    CritterProperty::Strength,    CritterProperty::StrengthBase,    0,    0,    0,    2,    4,    6, /*stats amount*/
    CritterProperty::Perception,  CritterProperty::PerceptionBase,  0,    0,    1,    0,    3,    5,
    CritterProperty::Endurance,   CritterProperty::EnduranceBase,   0,    0,    0,    1,    3,    5,
    CritterProperty::Charisma,    CritterProperty::CharismaBase,    0,    0,    0,    0,    3,    5,
    CritterProperty::Intellect,   CritterProperty::IntellectBase,   0,    0,    0,    0,    1,    3,
    CritterProperty::Agility,     CritterProperty::AgilityBase,     0,    0,    1,    2,    5,    6,
    CritterProperty::CurrentHp,   CritterProperty::CurrentHp,       0,    1,    3,    5,   15,   20,
    CritterProperty::HealingRate, CritterProperty::HealingRateBase, 0,    0,    0,    5,   10,   10,
};

// clang-format on


// Table offsets
#define TABLE_STAGE_VALUE          # (stage)           RadiationEffects[ ( stage ) - 1 ]
#define TABLE_STAT_INDEX_READ      # (stat)            CritterProperty( RadiationEffects[ 6 + ( stat ) * 8 ] )
#define TABLE_STAT_INDEX_WRITE     # (stat)            CritterProperty( RadiationEffects[ 6 + ( stat ) * 8 + 1 ] )
#define TABLE_STAT_AMOUNT          # ( stat, stage )RadiationEffects[ 6 + ( stat ) * 8 + ( stage ) ]


void AffectRadiation( Critter cr, int value ) // Export
{
    if( cr.IsDead() )
        return;
    if( cr.BodyType == BodyTypes::Robot || cr.BodyType == BodyTypes::Alien )
        return;

    uint[] indexes = {};
    uint[] rates = {};
    bool isActive = cr.GetTimeEvents( Tla::CteRadiation, indexes, null, rates ) != 0;
    int  levelVal = cr.RadiationLevel;
    int  resistVal = cr.DamageResistance[ DamageTypes::Radiation ];

    if( value >= 0 )
        value -= value * resistVal / 100;
    cr.RadiationLevel = CLAMP( levelVal + value, 0, 2000 );
    levelVal = cr.RadiationLevel;

    if( value > RADIATION_HIGH_DOSE )
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_RADIATION_HIGH_DOSE );

    if( levelVal >= RADIATION_DAMAGE_VALUE )
    {
        if( !cr.IsRadiated )
            cr.IsRadiated = true;
    }
    else
    {
        if( cr.IsRadiated )
            cr.IsRadiated = false;
    }

    if( isActive )   // Try begin event with new effects
    {
        uint stage = rates[ 0 ];
        uint newStage = GetRadiationStage( levelVal );
        if( stage != newStage )
            cr.ChangeTimeEvent( indexes[ 0 ], 0, stage );
    }
    else     // Try start new event
    {
        int stage1Val = TABLE_STAGE_VALUE( 1 );
        if( levelVal >= stage1Val )
            cr.AddTimeEvent( cte_Radiation, 0, Tla::CteRadiation );
    }

    if( levelVal >= RADIATION_DEAD_DOSE )
    {
        CritterState::ToDead( cr, Anim2Actions::DeadFused, null  );
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_RADIATION_DIE );
    }
}

void DropRadiation( Critter cr ) // Export
{
    bool isActive = cr.GetTimeEvents( Tla::CteRadiation, null, null, null ) != 0;
    if( isActive )
    {
        uint stage = GetRadiationStage( cr.RadiationLevel );
        ChangeStage( cr, stage, false );
        cr.EraseTimeEvents( Tla::CteRadiation );
    }
    cr.RadiationLevel = 0;
    if( cr.IsRadiated )
        cr.IsRadiated = false;
}

uint cte_Radiation( Critter cr, int identifier, uint& stage )
{
    if( cr.IsDead() )
        return REAL_MINUTE( 5 );

    uint newStage = GetRadiationStage( cr.RadiationLevel );
    uint duration = RADIATION_DURATION;

    if( stage != newStage ) // Set new effects
    {
        ChangeStage( cr, stage, false );
        ChangeStage( cr, newStage, true );
        stage = newStage;
    }
    else     // Erase effects
    {
        ChangeStage( cr, stage, false );
        duration = 0;
    }
    return duration;
}

uint GetRadiationStage( int level )
{
    for( uint i = 0, j = RADIATION_STAGES; i <= j; i++ )
    {
        int valMin = ( i > 0 ? TABLE_STAGE_VALUE( i ) : 0 );
        int valMax = ( i < j ? TABLE_STAGE_VALUE( i + 1 ) : level + 1 );
        if( level >= valMin && level < valMax )
            return i;
    }
    return 0;
}

void ChangeStage( Critter cr, uint stage, bool gain )
{
    if( stage == 0 )
        return;
    if( gain )
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, STR_RADIATION_STAGE( stage ) );

    for( uint i = 0, j = RADIATION_STATS; i < j; i++ )
    {
        CritterProperty statPropRead = TABLE_STAT_INDEX_READ( i );
        CritterProperty statPropWrite = TABLE_STAT_INDEX_WRITE( i );
        int             statAmount = TABLE_STAT_AMOUNT( i, stage );

        if( statAmount != 0 )
        {
            int curVal = cr.GetAsInt( statPropRead );
            if( gain )           // Gain effect
            {
                statAmount = -statAmount;
                if( statPropRead == CritterProperty::CurrentHp )
                {
                    cr.CurrentHp += statAmount;
                    if( cr.CurrentHp <= 0 )
                        CritterState::ToDead( cr, Anim2Actions::DeadFused, null  );
                }
                else
                {
                    cr.SetAsInt( statPropWrite, cr.GetAsInt( statPropWrite ) + statAmount );
                }
            }
            else             // Reduce effect
            {
                if( statPropRead != CritterProperty::CurrentHp )
                    cr.SetAsInt( statPropWrite, cr.GetAsInt( statPropWrite ) + statAmount );
            }
        }
    }
}

/*
   Radiation
   And now for some stuff on radiation, which does play a small part in the Fallout world.
   Sources of radiation include Fruit, golden geckos, and aliens and centaurs in random encounters.
   Whenever you get a huge dose all at once, the message window says "You have received a large dose
   of radiation." As explained in the patch notes, the radiation sign pops up once you've reached 66 rem.

   As far as I can tell radiation damage works like this: some time after you absorb a dose of radiation,
   you'll get one of the messages listed below, and you may temporarily lose stat points, Healing Rate and
   current Hit Points. The severity of the "attack" depends on your total rem count, but also on the amount
   of recent radiation that triggered the message. After 7 days, your stats will return to normal. A little
   confusingly, this is signalled by another message identical to the first one. If you absorb more radiation
   in the meanwhile, your current penalty may be replaced by a more severe one.

   Here's a list of the penalties you may suffer. The rem figures are rough approximations.
   Rem	    Message	                  HR	CHP	ST	PE	EN	CH	IN	AG
   >0	    "very nauseous"
   >150	"slightly fatigued"			    -1
   >300	"vomiting does not stop"	    -3		-1				-1
   >450	"hair is falling out"	  -5	-5	-2		-1			-2
   >600	"skin is falling off"	 -10	-15	-4	-3	-3	-3	-1	-5
   >1000	"intense agony"	         -10	-20	-6	-5	-5	-5	-3	-6


   You will expire from your condition if any of these stats except for Healing Rate (including CH) drops to 0
   or below. This is checked only at the moment when they are lowered, so you may be able to use drugs to stay alive.
   Assuming you don't die in this manner, you can live with any kind of rem count and still run around with no penalty
   whatsoever. In any normal game, radiation is not a significant factor; in fact, in order to reach even 300 rem you'd
   have to run several laps around the Gecko reactor room or purposely hang around the Toxic waste dump. From what I
   can tell, there is never any permanent damage resulting from radiation.

   {12800}{}{You feel very nauseous.}
   {12801}{}{You feel very nauseous, and after some mild vomiting, slightly fatigued.}
   {12802}{}{You are very fatigued, the vomiting does not stop, and your recovery time is impaired.}
   {12803}{}{You are hemorrhaging violently. You are very sick, and your hair is falling out.}
   {12804}{}{You are hemorrhaging violently and continuously. The vomitus is stained red from your blood. Your skin is falling off of your bones.}
   {12805}{}{There is bleeding from your intestines and you have severe diarrhea. You feel bloated and are in intense agony.}
   {12806}{}{You have died from radiation sickness.}
   {12807}{}{You have received a large dose of radiation.}
   {12808}{}{The geiger counter is clicking.}
   {12809}{}{The geiger counter is clicking wildly.}
 */
