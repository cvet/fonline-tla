namespace GlobalMapFog // Sort -1
{

///@ Property Critter Common uint8=>int64 GlobalMapFog Mutable OwnerSync Persistent

#define GM__MAXZONEX      (100)
#define GM__MAXZONEY      (100)
#define GM_ZONES_FOG_SIZE (((GM__MAXZONEX / 4) + ((GM__MAXZONEX % 4) ? 1 : 0)) * GM__MAXZONEY)
#define GM_FOG_FULL       (0)
#define GM_FOG_HALF       (1)
#define GM_FOG_HALF_EX    (2)
#define GM_FOG_NONE       (3)

int GetFog(Critter cr, int x, int y)
{
    if (x >= Settings.GlobalMapWidth || y >= Settings.GlobalMapHeight) {
        return GM_FOG_FULL;
    }

    auto offsetx = x * 2;
    auto mask = cr.GlobalMapFog.get(y, 0);
    auto value = (mask >> offsetx) & 3;
    return value;
}

#if SERVER

void SetFog(Critter cr, int x, int y, int value)
{
    if (value < GM_FOG_FULL || value > GM_FOG_NONE) {
        return;
    }

    auto data = cr.GlobalMapFog.clone();
    if (x >= Settings.GlobalMapWidth || y >= Settings.GlobalMapHeight) {
        return;
    }

    auto offsetx = x * 2;
    auto mask = data.get(y, 0);
    data[y] = (mask & ~(3 << offsetx)) | (value << offsetx);
    cr.GlobalMapFog = data;
}

#endif
#if CLIENT

void DrawFog(ipos offset, float zoneWidth, uint sprite)
{
    Critter cr = Game.GetChosen();
    if (cr != null) {
        for (int x = 0; x < Settings.GlobalMapWidth; x++) {
            for (int y = 0; y < Settings.GlobalMapHeight; y++) {
                auto fog = GlobalMapFog::GetFog(cr, x, y);
                if (fog != GM_FOG_NONE) {
                    auto size = fsize(zoneWidth, zoneWidth);
                    auto pos = fpos(x * zoneWidth + offset.x, y * zoneWidth + offset.y);
                    ucolor color = COLOR_NEUTRAL;
                    color.alpha = (fog == GM_FOG_HALF ? 110 : 220);
                    Game.DrawSprite(sprite, pos, size, color);
                }
            }
        }
    }
}

#endif
}
