namespace DropMenuHandler
{

///@ RemoteCall Server StartDialog(ident npcId)
///@ RemoteCall Server UseSkillOnItem(ident itemId, CritterProperty skill)

#if CLIENT

void ModuleInit()
{
    auto dropMenu = FixedDropMenu::FixedDropMenu(MouseButton::Left, CursorType::Default, Time::Milliseconds(500), 40, "art/intrface/", "IACCUXX1");

    dropMenu.AddButton(20, CanDialog, Dialog, "TALKN.FRM", "TALKH.FRM");
    dropMenu.AddButton(30, CanUse, Use, "USEGETN.FRM", "USEGETH.FRM");
    dropMenu.AddButton(40, CanLook, Look, "LOOKN.FRM", "LOOKH.FRM");
    dropMenu.AddButton(50, CanDrop, Drop, "DROPN.FRM", "DROPH.FRM");
    dropMenu.AddButton(55, CanRotate, Rotate, "ROTATEN.FRM", "ROTATEH.FRM");
    dropMenu.AddButton(60, CanUnload, DoUnload, "UNLOADN.FRM", "UNLOADH.FRM");
    dropMenu.AddButton(70, CanUseSkill, DoUseSkill, "SKILLN.FRM", "SKILLH.FRM");
    dropMenu.AddButton(80, CanFollowGroup, DoFollowGroup, "lmenu_gmfollow_up.png", "lmenu_gmfollow_dn.png");
    dropMenu.AddButton(100, CanCloseMenu, CloseMenu, "CANCELN.FRM", "CANCELH.FRM");

    // INVENH/N - инвентарь кнопка PUSHH/PUSHN - толчок; ROTATEH/ROTATEN - поворот.USEGETH/N - рука использования/LOOKH|LOOKN - просмотр подробный

    Gui::SetDropMenu(dropMenu);
}

bool CanFollowGroup(Critter cr, Item item)
{
    return cr != null && CurMap != null && !cr.IsChosen && cr.IsAlive() && cr.ControlledByPlayer && !cr.IsPlayerOffline;
}

void DoFollowGroup(Critter cr, Item item)
{
    if (cr != null && cr.IsAlive()) {
        GlobalmapGroup::RuleGlobalCommand(5, cr.Id, 0);
    }
}

bool CanUseSkill(Critter cr, Item item)
{
    return valid(cr) || valid(item);
}

void DoUseSkill(Critter cr, Item item)
{
    ident itemId;
    ident critterId;
    int isInventory = 0;

    if (valid(item)) {
        itemId = item.Id;
        if (item.Ownership == ItemOwnership::CritterInventory) {
            isInventory = 1;
        }
    }
    else {
        if (valid(cr)) {
            critterId = cr.Id;
        }
    }
    Gui::ShowScreen(GuiScreen::SkillBox, dict<string, any> = {{"TargetCritterId", critterId}, {"TargetItemId", itemId}, {"IsInventory", isInventory}});
}

bool CanDialog(Critter cr, Item item)
{
    // ToDo - диалог с предметами
    return cr != null && IsChosenAlive() && !cr.ControlledByPlayer && cr.IsAlive() && cr.DialogId != EMPTY_HSTRING && !cr.IsNoTalk;
}

void Dialog(Critter cr, Item item)
{
    // CurPlayer.ServerCall.StartDialog( cr.Id );
    ChosenActions::SetChosenActions(array<any> = {Tla::ChosenTalkNpc, cr.Id, 0, 0, 0, 0, 0});
}

bool IsChosenAlive()
{
    Critter chosen = Game.GetChosen();
    return chosen != null && chosen.IsAlive();
}

// Close menu
bool CanCloseMenu(Critter cr, Item item)
{
    return valid(cr) || valid(item);
}

void CloseMenu(Critter cr, Item item)
{
    //
}

// Look
bool CanLook(Critter cr, Item item)
{
    return valid(cr) || valid(item);
}

void Look(Critter cr, Item item)
{
    if (valid(item)) {
        Messaging::Info(ClientMain::item_description(item, ItemLookTypes::Default));
    }
    else if (valid(cr)) {
        Messaging::Info(ClientMain::critter_description(cr, CritterLookTypes::LookFull));
    }
}

bool CanUse(Critter cr, Item item)
{
    // Loot critter
    if (cr != null && IsChosenAlive() && !cr.IsChosen && cr.IsDead() && !cr.IsNoLoot) {
        return true;
    }

    if (item != null && IsChosenAlive() && item.Ownership == ItemOwnership::MapHex && !item.IsWall &&
        (item.IsCanUse || item.IsCanUseOnSmth || item.IsCanPickUp)) {
        return true;
    }

    if (item != null && IsChosenAlive() && IsChosenItem(item) && (item.IsCanUse || item.IsCanUseOnSmth)) {
        return true;
    }

    return false;
}

void Use(Critter cr, Item item)
{
    // if(item.Ownership != ItemOwnership::MapHex )
    //    return;
    if (valid(item) && item.Ownership == ItemOwnership::MapHex) {
        // CurPlayer.ServerCall.UseSkillOnItem( item.Id, CritterProperty::SpecialSkillPickOnGround );
        ChosenActions::SetChosenActions(array<any> = {Tla::ChosenPickItem, item.ProtoId.hash, item.Hex.x, item.Hex.y, item.Id, 0, 0});
    }

    if (cr != null && cr.IsDead()) {
        ChosenActions::SetChosenActions(array<any> = {Tla::ChosenPickCrit, cr.Id, 0, 0, 0, 0, 0});
    }

    if (item != null && IsChosenAlive() && IsChosenItem(item) && (item.IsCanUse || item.IsCanUseOnSmth)) {
        ChosenActions::SetChosenActions(array<any> = {Tla::ChosenUseItem, item.Id, 0, TARGET_SELF, 0, _WeaponModeMake(USE_USE, 0), 0});
    }
}

bool CanDrop(Critter cr, Item item)
{
    return item != null && IsChosenAlive() && IsChosenItem(item);
}

bool IsChosenItem(Item item)
{
    return item.Ownership == ItemOwnership::CritterInventory && item.CritterId == Game.GetChosen().Id;
}

void Drop(Critter cr, Item item)
{
    if (item.Stackable && item.Count > 1) {
        Gui::ShowScreen(GuiScreen::Split, dict<string, any> = {{"TargetItemId", item.Id}, {"ItemsCollection", ITEMS_CHOSEN_ALL}});
    }
    else {
        ChosenActions::SetChosenActions(array<any> = {Tla::ChosenMoveItem, item.Id, item.Count, CritterItemSlot::Outside, 0, 0, 0});
    }
}

bool CanRotate(Critter cr, Item item)
{
    return cr != null && CurMap != null && cr.IsChosen && cr.IsAlive();
}

void Rotate(Critter cr, Item item)
{
    ChosenActions::SetChosenActions(array<any> = {Tla::ChosenDir, 0, 0, 0, 0, 0, 0});
}

bool CanUnload(Critter cr, Item item)
{
    return item != null && item.Type == ItemType::Weapon && IsChosenAlive() && IsChosenItem(item) && item.AmmoCount > 0;
}

void DoUnload(Critter cr, Item item)
{
    ChosenActions::SetChosenActions(array<any> = {Tla::ChosenUseItem, item.Id, 0, TARGET_SELF_ITEM, int(-1), USE_RELOAD, 0});
}

#endif

#if SERVER

void StartDialog(Player player, ident npcId)
{
    Critter cr = player.GetControlledCritter();
    Critter npc = Game.GetCritter(npcId);
    if (valid(cr) && cr.IsAlive() && valid(npc) && npc.IsAlive() && !npc.ControlledByPlayer && !npc.IsNoTalk) {
        Game.RunDialog(cr, npc, false);
    }
}

void UseSkillOnItem(Player player, ident itemId, CritterProperty skill)
{
    Critter cr = player.GetControlledCritter();
    Item item = Game.GetItem(itemId);
    if (valid(cr) && valid(item)) {
        if (item.Type == ItemType::Container || item.Type == ItemType::Door) {
            Lockers::UseSkillOnLocker(cr, item, CritterProperty::SpecialSkillPickOnGround);
        }
    }
}

#endif

}
