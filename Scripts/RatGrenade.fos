namespace RatGrenade // Sort 2
{

#if SERVER

// Author: rifleman17
// Метательные крысы

///@ Property Critter PrivateServer hstring RatGrenadeProtoId
///@ Property Critter PrivateServer ident RatGrenadeOwnerId

///@ Property Item PrivateServer int32 RatGrenadeInvokeId

#define STR_AWAKE_GLOBAL (30) // Одна из ваших оглушенных крыс пришла в себя и удрала из рюкзака! Черт. Чем это так пахнет, и что это за жидкость?
#define STR_AWAKE_MAP    (31) // Одна оглушенная крыса, по всей видимости очнулась, и выскочила из рюкзака. Прикончите эту тварь немедленно.
#define STR_VENOMED      (32) // Вы вкололи супертоксин оглушенной крысе. Теперь с каждым укусом она может отравить жертву.

// #define DL                     # (s) ( Game.Log( "RATS: " + s ) )
#define DL #(s)

void _RatGrenadeInit(Item item, bool firstTime)
{
    DL("item init");

    if (firstTime) {
        // Есть шанс, что крыса очнется через пять реальных минут
        if (Game.Random(1, 4) < 2) {
            DL("Awaking rat after 5 real minutes");
            item.RatGrenadeInvokeId = Game.StartTimeEvent(Time::GameMinutes(Game.Random(4, 5)), AwakeRat, item.Id);
            DL("Awaking rat after 5 real minutes. EventId = " + item.RatGrenadeInvokeId);
        }
    }

    item.OnCritterAttack.Subscribe(_RatGrenadeAttack);
    item.OnCritterUseOnSelf.Subscribe(_RatGrenadeUseOnMe);
    item.OnFinish.Subscribe(_RatGrenadeFinish);
}

bool _RatGrenadeUseOnMe(Item item, Critter cr, Item usedItem, int param)
{
    hstring pid = item.ProtoId;
    if (pid == Content::Item::rat_grenade && usedItem.ProtoId == Content::Item::supertoxine) {
        // Только при использовании в инвентаре, не на земле
        Item[] items = cr.GetItems();
        for (int i = 0; i < items.length(); i++) {
            Item foundedItem = items[i];
            if (foundedItem.Id == item.Id) {
                Game.DestroyItem(foundedItem);
                cr.AddItem(Content::Item::rat_grenade_venom, 1);
                Messaging::Info(cr, TextPackName::Text, STR_VENOMED);
                return true;
            }
        }
    }
    return false;
}

void AwakeRat(any value)
{
    DL("Rat awaken");
    Item item = Game.GetItem(value);
    if (valid(item)) {
        item.RatGrenadeInvokeId = 0;
        if (item.CritterId != ZERO_IDENT) {
            Critter cr = Game.GetCritter(item.CritterId);
            if (valid(cr)) {
                Map map = cr.GetMap();
                if (valid(map)) {
                    Messaging::Info(cr, TextPackName::Text, STR_AWAKE_MAP);
                    mpos hex = cr.Hex;
                    if (Entrance::GetFreeHex(map, 2, hex)) {
                        uint8 dir = Game.GetDirection(hex, cr.Hex);
                        dict<CritterProperty, any> ratVars = {{CritterProperty::RatGrenadeProtoId, item.ProtoId},
                                                              {CritterProperty::RatGrenadeOwnerId, item.CritterId}};
                        Critter rat = map.AddNpc(Content::Critter::RegularRat, hex, dir, ratVars);
                        rat.SetupScript(_RatInit);
                        if (valid(rat)) {
                            NpcPlanes::AddAttackPlane(rat, 0, cr);
                        }
                    }
                }
                else {
                    Messaging::Info(cr, TextPackName::Text, STR_AWAKE_GLOBAL);
                }
                Game.DestroyItem(item);
            }
        }
    }
}

bool _RatGrenadeAttack(Item item, Critter cr, Critter target, int weaponMode, ProtoItem ammo)
{
    DL("Rat attack");
    if (!target.IsNoPvp) {
        any[] values = {cr.Id, target.Id, item.Id, 1};
        Game.StartTimeEvent(Time::Seconds(1), RatAttack, values); //SavedDeferredCall
    }
    if (target.BodyType == BodyTypes::Floater) {
        switch (cr.ModLourenceFloater) {
        case 1:
            cr.ModLourenceFloater = 2;
            break;
        case 3:
            cr.ModLourenceFloater = 4;
            break;
        }
    }
    return false;
}

void RatAttack(any[] values)
{
    Critter target = Game.GetCritter(values[1]);
    Item item = Game.GetItem(values[2]);
    if (valid(item) && valid(target)) {
        mpos hex = target.Hex;
        if (item.MapId != ZERO_IDENT) {
            Map map = item.GetMapPosition(hex);
            if (valid(map)) {
                if (Entrance::GetFreeHex(map, 2, hex)) {
                    uint8 dir = Game.GetDirection(hex, target.Hex);
                    dict<CritterProperty, any> ratVars = {{CritterProperty::RatGrenadeProtoId, item.ProtoId},
                                                          {CritterProperty::RatGrenadeOwnerId, item.CritterId}};
                    Critter rat = map.AddNpc(Content::Critter::RegularRat, hex, dir, ratVars);
                    rat.SetupScript(_RatInit);
                    if (valid(rat)) {
                        NpcPlanes::AddAttackPlane(rat, 0, target);
                        Game.DestroyItem(item);
                    }
                }
            }
        }
        else {
            if (values[3] < 2) {
                DL("Rat in inventory");
                values[3] = 2;
                Game.StartTimeEvent(Time::Seconds(2), RatAttack, values); //SavedDeferredCall
            }
        }
    }
}

void _RatGrenadeFinish(Item item)
{
    DL("Rat grenade finish");
    if (item.RatGrenadeInvokeId != 0) {
        Game.StopTimeEvent(item.RatGrenadeInvokeId);
    }
}

// Скрипт крысы, вылезающей из гранаты, если она проснулась
void _RatInit(Critter cr, bool firstTime)
{
    Mob::_MobInit(cr, firstTime);
    cr.TeamId = Teams::Mob0;
    cr.ReplicationTime = -1;
    cr.OnDead.Subscribe(_RatDead);
    DL("v7 " + cr.RatGrenadeProtoId);
    if (cr.RatGrenadeProtoId == Content::Item::rat_grenade_venom) {
        cr.Toxic = 120;
    }
    else if (cr.RatGrenadeProtoId == Content::Item::rat_grenade_tnt) {
        cr.StartTimeEvent(Time::Seconds(Game.Random(5, 10)), cte_ExplodeRat);
    }

    cr.StartTimeEvent(Time::Minutes(Game.Random(5, 10)), cte_DeleteRat);
}

void cte_ExplodeRat(Critter cr)
{
    DL("Exploded");
    Map map = cr.GetMap();
    if (valid(map)) {
        Explode::Explode(cr.GetMap(), cr.Hex.x, cr.Hex.y, null, Content::Item::active_dynamite, cr.RatGrenadeOwnerId, Game.Random(0, 50), Game.Random(0, 1));
    }
    else {
        Game.DestroyCritter(cr);
    }
    return;
}

void _RatDead(Critter cr, Critter killer)
{
    DL("Rat dead");
    cr.StartTimeEvent(Time::Minutes(5), cte_DeleteRat);
}

void cte_DeleteRat(Critter cr)
{
    DL("Delete Rat");
    if (!cr.ControlledByPlayer) {
        Game.DestroyCritter(cr);
    }
    return;
}

#endif

}
