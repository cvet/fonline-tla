namespace Radio
{

///@ Setting Common int RadioBroadcastZones1
///@ Setting Common int RadioBroadcastZones2

///@ Enum RadioBroadcastType World = 0
///@ Enum RadioBroadcastType Zone // Distance set in Radio.BroadcastZones
///@ Enum RadioBroadcastType Location
///@ Enum RadioBroadcastType Map

///@ Enum GuiScreen Radio

///@ PropertyComponent Item Radio
///@ Property Item Protected uint16 Radio.Channel
///@ Property Item Protected bool Radio.DisableSend
///@ Property Item Protected bool Radio.DisableRecv
///@ Property Item Protected RadioBroadcastType Radio.Broadcast
///@ Property Item Protected uint8 Radio.BroadcastZones
///@ Property Item Protected bool Radio.FixedChannel
///@ Property Item Protected bool Radio.FixedBroadcast

///@ RemoteCall Client ShowEditScreen(ident itemId)
///@ RemoteCall Server SendRadioMessage(string text)
///@ RemoteCall Server ChangeActivity(ident radioId, bool isSend, bool enable)
///@ RemoteCall Server ChangeChannel(ident radioId, int radioChannel)
///@ RemoteCall Server ChangeBroadcast(ident radioId, RadioBroadcastType broadcast, int broadcastZones)

#if SERVER

Item[] RadioItems = {};

// [[ModuleInit]]
void ModuleInit()
{
    // Todo: use OnCritterUseItem event instead direct call of EditRadioSettings
    // Game.OnCritterUseItem.Subscribe(OnCritterUseItem);
    Game.OnItemInit.Subscribe(OnItemInit);
    Game.OnItemFinish.Subscribe(OnItemFinish);
}

// [[Event]]
void OnItemInit(Item item, bool firstTime)
{
    if (item.Radio != null) {
        RadioItems.insertLast(item);
    }
}

// [[Event]]
void OnItemFinish(Item item)
{
    if (item.Radio != null) {
        RadioItems.remove(item);
    }
}

void EditRadioSettings(Critter cr, Item item)
{
    cr.PlayerClientCall.ShowEditScreen(item.Id);
}

// [[Event]]
void OnCritterUseItem(Critter cr, Item item)
{
    if (item.Radio != null) {
        cr.PlayerClientCall.ShowEditScreen(item.Id);
    }
}

// [[ServerRemoteCall]]
void SendRadioMessage(Player player, string text)
{
    Critter cr = player.GetControlledCritter();
    Item[] items = cr.GetItems(ItemComponent::Radio);
    bool hasActiveRadio = false;

    for (int i = 0; i < items.length(); i++) {
        if (!items[i].Radio.DisableSend) {
            hasActiveRadio = true;
            break;
        }
    }

    Assert(hasActiveRadio);

    text = text.trim();
    Assert(!text.isEmpty());

    SendRadioMessage(cr, text);
}

// [[ServerRemoteCall]]
void ChangeActivity(Player player, ident radioId, bool isSend, bool disable)
{
    Critter cr = player.GetControlledCritter();
    Item item = cr.GetItem(radioId);

    Assert(item != null);
    Assert(item.Radio != null);

    if (isSend) {
        item.Radio.DisableSend = disable;
    }
    else {
        item.Radio.DisableRecv = disable;
    }
}

// [[ServerRemoteCall]]
void ChangeChannel(Player player, ident radioId, int radioChannel)
{
    Critter cr = player.GetControlledCritter();
    Item item = cr.GetItem(radioId);

    Assert(item != null);
    Assert(item.Radio != null);
    Assert(!item.Radio.FixedChannel);
    Assert(radioChannel >= 0 && radioChannel <= 0xFFFF);

    item.Radio.Channel = radioChannel;
}

// [[ServerRemoteCall]]
void ChangeBroadcast(Player player, ident radioId, RadioBroadcastType broadcast, int broadcastZones)
{
    Critter cr = player.GetControlledCritter();
    Item item = cr.GetItem(radioId);

    Assert(item != null);
    Assert(item.Radio != null);
    Assert(!item.Radio.FixedBroadcast);

    item.Radio.Broadcast = broadcast;
    item.Radio.BroadcastZones = broadcastZones == 1 ? Settings.RadioBroadcastZones1 : Settings.RadioBroadcastZones2;
}

void SendRadioMessage(Critter cr, string text)
{
    Messaging::SayWhisper(cr, text);

    Item[] items = cr.GetItems(ItemComponent::Radio);

    for (int i = 0; i < items.length(); i++) {
        Item sendItem = items[i];

        if (!sendItem.Radio.DisableSend) {
            SendRadioMessage(sendItem, text);
        }
    }
}

void SendRadioMessage(Item sendItem, string text)
{
    Map sendMap = sendItem.GetMap();
    Critter sendCr = sendItem.GetCritter();
    ipos sendWorldPos = sendCr != null ? sendCr.WorldPos : sendMap.GetLocation().WorldPos;
    RadioBroadcastType sendBroadcast = sendItem.Radio.Broadcast;

    for (int i = 0; i < RadioItems.length(); i++) {
        Item recvItem = RadioItems[i];

        if (recvItem.Radio.DisableRecv) {
            continue;
        }
        if (sendItem.Id == recvItem.Id) {
            continue;
        }
        if (recvItem.Radio.Channel != sendItem.Radio.Channel) {
            continue;
        }

        RadioBroadcastType broadcast;
        int broadcastZones = 0;

        if (sendBroadcast == RadioBroadcastType::Map || recvItem.Radio.Broadcast == RadioBroadcastType::Map) {
            broadcast = RadioBroadcastType::Map;
        }
        else if (sendBroadcast == RadioBroadcastType::Location || recvItem.Radio.Broadcast == RadioBroadcastType::Location) {
            broadcast = RadioBroadcastType::Location;
        }
        else if (sendBroadcast == RadioBroadcastType::Zone || recvItem.Radio.Broadcast == RadioBroadcastType::Zone) {
            broadcast = RadioBroadcastType::Zone;
            broadcastZones = Math::Min(sendItem.Radio.BroadcastZones, recvItem.Radio.BroadcastZones);
        }
        else if (sendBroadcast == RadioBroadcastType::World || recvItem.Radio.Broadcast == RadioBroadcastType::World) {
            broadcast = RadioBroadcastType::World;
        }

        mpos hex;
        Map recvMap = recvItem.GetMapPosition(hex);
        Critter recvCr = recvItem.GetCritter();
        ipos recvWorldPos = recvCr != null ? recvCr.WorldPos : recvMap.GetLocation().WorldPos;

        if (broadcast == RadioBroadcastType::Zone) {
            if (Math::Distance(sendWorldPos, recvWorldPos) > broadcastZones * Settings.GlobalMapZoneLength) {
                continue;
            }
        }
        else if (broadcast == RadioBroadcastType::Location) {
            if (sendMap == null || recvMap == null || sendMap.GetLocation().Id != recvMap.GetLocation().Id) {
                continue;
            }
        }
        else if (broadcast == RadioBroadcastType::Map) {
            if (sendMap == null || recvMap == null || sendMap.Id != recvMap.Id) {
                continue;
            }
        }

        if (recvCr != null) {
            MapText::AddText(recvCr, COLOR_WHITE, text);
        }
        else {
            MapText::AddText(recvMap, hex, COLOR_WHITE, text);
        }
    }
}

void SendChannelRadioMessage(int channel, string text)
{
    for (int i = 0; i < RadioItems.length(); i++) {
        Item recvItem = RadioItems[i];

        if (recvItem.Radio.DisableRecv) {
            continue;
        }
        if (int(recvItem.Radio.Channel) != channel) {
            continue;
        }

        Critter recvCr = recvItem.GetCritter();

        if (recvCr != null) {
            MapText::AddText(recvCr, COLOR_WHITE, text);
        }
        else {
            mpos hex;
            Map recvMap = recvItem.GetMapPosition(hex);
            MapText::AddText(recvMap, hex, COLOR_WHITE, text);
        }
    }
}

void SendChannelRadioMessage(int channel, TextPackName textPack, uint strNum, string lexems = "")
{
    for (int i = 0; i < RadioItems.length(); i++) {
        Item recvItem = RadioItems[i];

        if (recvItem.Radio.DisableRecv) {
            continue;
        }
        if (int(recvItem.Radio.Channel) != channel) {
            continue;
        }

        Critter recvCr = recvItem.GetCritter();

        if (recvCr != null) {
            MapText::AddText(recvCr, COLOR_WHITE, textPack, strNum);
        }
        else {
            mpos hex;
            Map recvMap = recvItem.GetMapPosition(hex);
            MapText::AddText(recvMap, hex, COLOR_WHITE, textPack, strNum, lexems);
        }
    }
}

#endif

#if CLIENT

void SendRadioMessage(string text)
{
    Critter chosen = Game.GetChosen();

    if (chosen == null) {
        return;
    }

    bool hasActiveRadio = false;
    Item[] items = chosen.GetItems(ItemComponent::Radio);

    for (int i = 0; i < items.length(); i++) {
        if (!items[i].Radio.DisableSend) {
            hasActiveRadio = true;
            break;
        }
    }

    if (hasActiveRadio) {
        CurPlayer.ServerCall.SendRadioMessage(text);
    }
    else {
        Messaging::Info(TextPackName::Game, MsgStr::Get("RadioCantSendMessage"));
    }
}

class ScreenMain : Gui::Screen
{
    ident RadioId;
    TextboxChannel Channel;
    ButtonSendRecv Send;
    ButtonSendRecv Recv;
    ButtonBroadcast[] Broadcast;
    int BroadcastType;

    void OnShow(dict<string, any> params) override
    {
        RadioId = params["ItemId"];
        Item item = GetRadio();

        if (item == null) {
            Gui::HideScreen();
            return;
        }

        Channel.SetChannel(item.Radio.Channel);

        Send.SetState(!item.Radio.DisableSend);
        Recv.SetState(!item.Radio.DisableRecv);

        for (int i = 0; i < Broadcast.length(); i++) {
            Broadcast[i].SetState(false);
        }

        if (item.Radio.Broadcast == RadioBroadcastType::World) {
            Broadcast[0].SetState(true);
        }
        else if (item.Radio.Broadcast == RadioBroadcastType::Zone) {
            Broadcast[item.Radio.BroadcastZones == uint8(Settings.RadioBroadcastZones1) ? 1 : 2].SetState(true);
        }
        else if (item.Radio.Broadcast == RadioBroadcastType::Location) {
            Broadcast[3].SetState(true);
        }
        else if (item.Radio.Broadcast == RadioBroadcastType::Map) {
            Broadcast[4].SetState(true);
        }
    }

    void OnDraw() override
    {
        if (GetRadio() == null) {
            Gui::HideScreen();
        }
    }

    void ChangeChannel()
    {
        Item item = GetRadio();
        Assert(!item.Radio.FixedChannel);

        if (item.Radio.Channel == Channel.RadioChannel) {
            return;
        }

        CurPlayer.ServerCall.ChangeChannel(RadioId, Channel.RadioChannel);
        Messaging::Info(TextPackName::Game, MsgStr::Get("RadioChannelChanged"));
    }

    void ChangeActivity(bool isSend)
    {
        Item item = GetRadio();
        bool disableFlag = isSend ? item.Radio.DisableSend : item.Radio.DisableRecv;
        uint beginStr = isSend ? MsgStr::Get("RadioBroadcastSendBegin") : MsgStr::Get("RadioBroadcastRecvBegin");
        uint endStr = isSend ? MsgStr::Get("RadioBroadcastSendEnd") : MsgStr::Get("RadioBroadcastRecvEnd");

        disableFlag = !disableFlag;
        CurPlayer.ServerCall.ChangeActivity(RadioId, isSend, disableFlag);

        if (disableFlag) {
            Messaging::Info(TextPackName::Game, beginStr);
        }
        else {
            Messaging::Info(TextPackName::Game, endStr);
        }

        if (isSend) {
            Send.SetState(!disableFlag);
        }
        else {
            Recv.SetState(!disableFlag);
        }
    }

    void ChangeBroadcast(int type)
    {
        Item item = GetRadio();

        if (item.Radio.FixedBroadcast) {
            Messaging::Info(TextPackName::Game, MsgStr::Get("RadioCantShiftBcSend"));
            return;
        }

        RadioBroadcastType newBroadcast = RadioBroadcastType::World;
        int newBroadcastZones = 0;

        if (type == 1) {
            newBroadcast = RadioBroadcastType::Zone;
            newBroadcastZones = 1;
        }
        else if (type == 2) {
            newBroadcast = RadioBroadcastType::Zone;
            newBroadcastZones = 2;
        }
        else if (type == 3) {
            newBroadcast = RadioBroadcastType::Location;
        }
        else if (type == 4) {
            newBroadcast = RadioBroadcastType::Map;
        }

        if (item.Radio.Broadcast == newBroadcast) {
            if (item.Radio.Broadcast == RadioBroadcastType::Zone) {
                if (item.Radio.BroadcastZones == uint8(newBroadcastZones == 1 ? Settings.RadioBroadcastZones1 : Settings.RadioBroadcastZones2)) {
                    return;
                }
            }
            else {
                return;
            }
        }

        CurPlayer.ServerCall.ChangeBroadcast(RadioId, newBroadcast, newBroadcastZones);
        Messaging::Info(TextPackName::Game, MsgStr::Get("RadioBroadcastSendChange"));

        for (int i = 0; i < Broadcast.length(); i++) {
            Broadcast[i].SetState(false);
        }

        Broadcast[BroadcastType].SetState(true);
    }

    Item GetRadio()
    {
        Critter chosen = Game.GetChosen();

        if (chosen == null) {
            return null;
        }

        Item item = chosen.GetItem(RadioId);

        if (item == null) {
            return null;
        }

        return item;
    }
}

class TextboxChannel : Gui::TextInput
{
    ScreenMain Instance;
    uint16 RadioChannel;

    TextboxChannel(ScreenMain instance)
    {
        Instance = instance;
        Instance.Channel = this;
    }

    void OnInput(KeyCode key) override
    {
        Item item = Instance.GetRadio();
        string t = Text;

        if (!item.Radio.FixedChannel) {
            if (!t.isEmpty() && t[-1] == "\n") {
                t[-1] = "";
            }

            int channel = t.toInt(0);
            channel = Math::Clamp(channel, 0, 0xFFFF);
            t = "" + channel;

            RadioChannel = channel;

            if (key == KeyCode::Return || key == KeyCode::Numpadenter) {
                Instance.ChangeChannel();
            }
        }
        else {
            t = "" + RadioChannel;
            Messaging::Info(TextPackName::Game, MsgStr::Get("RadioCantShiftChannel"));
        }
    }

    void SetChannel(uint16 channel)
    {
        RadioChannel = channel;
        SetText("" + channel, FONT_DEFAULT, FT_CENTERX | FT_CENTERY);
        SetTextColor(COLOR_GREEN);
    }
}

class ButtonSendRecv : Gui::Button
{
    ScreenMain Instance;
    bool IsSend;

    ButtonSendRecv(ScreenMain instance, bool isSend)
    {
        Instance = instance;
        IsSend = isSend;

        if (IsSend) {
            Instance.Send = this;
        }
        else {
            Instance.Recv = this;
        }
    }

    void OnMouseClick(MouseButton button) override
    {
        if (button == MouseButton::Left) {
            Instance.ChangeActivity(IsSend);
        }
    }

    void SetState(bool state)
    {
        SetSwitch(state);
    }
}

class ButtonBroadcast : Gui::Button
{
    ScreenMain Instance;
    int Type;

    ButtonBroadcast(ScreenMain instance, int type)
    {
        Instance = instance;
        Type = type;
        Instance.Broadcast[type] = this;
    }

    void OnMouseClick(MouseButton button) override
    {
        if (button == MouseButton::Left) {
            Instance.ChangeBroadcast(Type);
        }
    }

    void SetState(bool state)
    {
        SetSwitch(state);
    }
}

void InitRadioScreen()
{
    Gui::RegisterScreen(GuiScreen::Radio, CreateRadioScreen);
}

Gui::Screen CreateRadioScreen()
{
    // Main screen
    ScreenMain screen = ScreenMain();
    screen.SetBackgroundImage("radio.png");
    screen.SetCloseOnMiss(true);

    Gui::Text mainText = Gui::Text();
    mainText.Init(screen);
    mainText.SetPosition(0, 0);
    mainText.SetText(Game.GetText(TextPackName::Game, MsgStr::Get("RadioMain")), FONT_DEFAULT, FT_CENTERX | FT_CENTERY | FT_BORDERED);
    mainText.SetTextColor(COLOR_SAND);

    // Channel textbox
    TextboxChannel textboxChannel = TextboxChannel(screen);
    textboxChannel.Init(screen);
    textboxChannel.SetPosition(29, 29);
    textboxChannel.SetSize(68, 13);
    textboxChannel.SetInputLength(5);

    Gui::Text channelText = Gui::Text();
    channelText.Init(screen);
    channelText.SetPosition(14, 2);
    channelText.SetSize(103, 23);
    channelText.SetText(Game.GetText(TextPackName::Game, MsgStr::Get("RadioChannel")), FONT_DEFAULT, FT_CENTERX | FT_CENTERY | FT_BORDERED);
    channelText.SetTextColor(COLOR_SAND);

    // Broadcast
    Gui::Text broadcastSendText = Gui::Text();
    broadcastSendText.Init(screen);
    broadcastSendText.SetPosition(5, 50);
    broadcastSendText.SetSize(70, 20);
    broadcastSendText.SetText(Game.GetText(TextPackName::Game, MsgStr::Get("RadioBroadcastSend")), FONT_DEFAULT, FT_CENTERY | FT_BORDERED);
    broadcastSendText.SetTextColor(COLOR_SAND);
    Gui::Text broadcastRecvText = Gui::Text();
    broadcastRecvText.Init(screen);
    broadcastRecvText.SetPosition(75, 50);
    broadcastRecvText.SetSize(70, 20);
    broadcastRecvText.SetText(Game.GetText(TextPackName::Game, MsgStr::Get("RadioBroadcastRecv")), FONT_DEFAULT, FT_CENTERR | FT_CENTERY | FT_BORDERED);
    broadcastRecvText.SetTextColor(COLOR_SAND);

    ButtonSendRecv buttonSend = ButtonSendRecv(screen, true);
    buttonSend.Init(screen);
    buttonSend.SetPosition(11, 72);
    buttonSend.SetPressedImage("PRFSLDON.FRM");

    ButtonSendRecv buttonRecv = ButtonSendRecv(screen, false);
    buttonRecv.Init(screen);
    buttonRecv.SetPosition(125, 72);
    buttonRecv.SetPressedImage("PRFSLDON.FRM");

    screen.Broadcast = array<ButtonBroadcast>(5);

    for (int i = 0; i < 5; i++) {
        ButtonBroadcast buttonBroadcast = ButtonBroadcast(screen, i);
        buttonBroadcast.Init(screen);
        buttonBroadcast.SetPosition(13, 87 + 20 * (i + 1));
        buttonBroadcast.SetPressedImage("PRFXIN.FRM");

        Gui::Text broadcastText = Gui::Text();
        broadcastText.Init(screen);
        broadcastText.SetPosition(35, 87 + 20 * (i + 1));
        broadcastText.SetText(Game.GetText(TextPackName::Game, MsgStr::Get("RadioBroadcast") + i + 1), FONT_DEFAULT, FT_CENTERX | FT_CENTERY | FT_BORDERED);
        broadcastText.SetTextColor(COLOR_SAND);
    }

    screen.Init(null);
    return screen;
}

void ShowEditScreen(ident itemId)
{
    Critter chosen = Game.GetChosen();

    if (chosen == null) {
        return;
    }

    Item item = chosen.GetItem(itemId);

    if (item != null) {
        dict<string, any> params = {{"ItemId", item.Id}};
        Gui::ShowScreen(GuiScreen::Radio, params);
    }
}

#endif

}
