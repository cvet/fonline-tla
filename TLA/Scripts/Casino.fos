// FOS Server Sort 2
// Author: cvet, rifleman17
//#include "Tla"

#pragma property Map PrivateServer uint CasinoLimit
#pragma property Map PrivateServer uint CasinoTimeRenew

#define START_BET              ( 5 )
#define MIN_BET                ( 1 )
#define MAX_BET                ( 50 )
#define STR_ONEHANDED_RING     ( 2000 )
#define RENEW_TIME             ( 60 * 60 * 24 * 7 ) // Обновление лимитов карты на автоматы раз в неделю
#define MAX_ONEHANDED_LIMIT    ( 8000 )             // Максимальная сумма, которую можно выжать из автоматов(без учета джекпотов)
#define STR_EMPTY              ( 2001 )             // Реплика "деньги в автомате закончились"

bool s_Onehanded( Critter& cr, const Item& onehanded, CritterProperty skill, Item@ item )
{
    if( skill != SKILL_PICK_ON_GROUND || ( valid( item ) && item.ProtoId != Content::Item::bottle_caps ) )
    {
        cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrUseNothing );
        return true;
    }

    // Сheck map weekly OneHanded limit Expire
    Map@ map = cr.GetMap();
    if( !valid( map ) )
        return true;

    if( map.CasinoTimeRenew < __FullSecond )
    {
        map.CasinoLimit = MAX_ONEHANDED_LIMIT;
        map.CasinoTimeRenew = __FullSecond + RENEW_TIME;
    }

    int onehandedMoney = map.CasinoLimit;
    if( onehandedMoney <= 0 )
    {
        cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_EMPTY );
        return true;
    }

    // Count bet
    uint bet = START_BET;
    if( valid( item ) )
    {
        // Set all money
        bet = item.Count;
    }
    else
    {
        // Check money
        bet = _CritCountItem( cr, Content::Item::bottle_caps );
        if( bet < MIN_BET )
        {
            cr.SayMsg( SAY_NETMSG, TEXTMSG_GAME, MsgStr::StrUseNothing );
            return true;
        }
        if( bet > START_BET )
            bet = START_BET;
    }

    // Check max bet
    if( bet > MAX_BET )
        bet = MAX_BET;

    // Set text
    if( valid( map ) )
        map.SetTextMsg( onehanded.HexX, onehanded.HexY, COLOR_GRAY, TEXTMSG_TEXT, STR_ONEHANDED_RING );

    // Calculate
    int gambling = cr.SkillGambling;
    if( onehandedMoney < 0 )
        gambling += onehandedMoney;
    gambling = CLAMP( gambling, 6, 95 );

    // Win
    if( gambling >= Random( 1, 100 ) )
    {
        // Bonuses
        if( Random( 0, 99 ) == 77 )
            bet *= 10;
        if( Random( 0, 999 ) == 777 )
            bet *= 100;
        if( Random( 0, 9999 ) == 7777 )
            bet *= 1000;

        // Add money
        cr.AddItem( Content::Item::bottle_caps, bet );
        if( bet > MAX_BET )
            cr.Say( SAY_NETMSG, "***JACKPOT***" );
        cr.Say( SAY_NETMSG, "+" + bet + "$" );
        onehandedMoney -= bet;
    }
    // Loose
    else
    {
        // Sub money
        cr.DeleteItem( Content::Item::bottle_caps, bet );
        cr.Say( SAY_NETMSG, "-" + bet + "$" );
        onehandedMoney += bet;
    }

    map.CasinoLimit = onehandedMoney;
    return true;
}
