namespace Hunter // Sort 2
{

#if SERVER

// Author: rifleman17
// Квесты цепочки "Лучший охотник Пустоши."

///@ Property Critter Common uint8 BarterLourensRats1 Mutable OwnerSync Persistent Group = Quests Quest = 5200 Max = 4
///@ Property Critter Common uint8 ModLourenceRatsFlute Mutable OwnerSync Persistent Group = Quests Quest = 3317 Max = 3
///@ Property Critter Server uint8 BarterLourensRatBodycount Mutable Persistent Max = 11
///@ Property Critter Server synctime ModHoughRatsFluteTimeout Mutable Persistent
///@ Property Critter Server synctime ModLourenceToxinTimeout Mutable Persistent
///@ Property Critter Server int32 ModLourenceRatsFluteCounter Mutable Persistent Max = 5
///@ Property Critter Server synctime ModLourenceLureActive Mutable Persistent

///@ Property Item Server int32 QHunterCountFluteUse Mutable Persistent

#define MSG_COM2ME (33)

// #define DL            # (s) ( Game.Log( "" + s ) )
#define DL #(s)

// Часть первая: Тестовое задание

void dlg_Colorize(Critter player, Critter npc, string& lexems)
{
    if (!IS_DIALOG_GENERATED(lexems)) {
        return;
    }
    lexems = "$colorHugh" + "|" + COLOR_TEXT + " $colorLourence" + "|" + COLOR_CONTOUR_YELLOW + " ";
}

void OnCritterKilled(Critter cr, Critter killer)
{
    if (valid(killer) && !cr.ControlledByPlayer && killer.ControlledByPlayer && cr.ModelNameBase == CRTYPE_MOLESKIN_RAT_BIG && killer.BarterLourensRats1 == 1) {
        if (killer.BarterLourensRatBodycount < 10) {
            killer.BarterLourensRatBodycount += 1;
        }
        else {
            killer.BarterLourensRats1 = 2;
        }
    }
}

void r_Explode(Critter player, Critter npc)
{
    if (valid(npc)) {
        SendMessage::ToAllOnMap(npc, MSG_COM2ME, player.Id);
        npc.StartTimeEvent(Time::Seconds(3), cte_Explode);
    }
}

void cte_Explode(Critter cr)
{
    Map map = cr.GetMap();
    if (valid(map)) {
        Explode::Explode(cr.GetMap(), cr.Hex.x, cr.Hex.y, null, Content::Item::active_dynamite, cr.Var4, Game.Random(0, 50), Game.Random(0, 1));
        cr.StartTimeEvent(Time::Seconds(2), cte_Comment);
    }
    return;
}

void cte_Comment(Critter cr)
{
    Messaging::Say(cr, TextPackName::Dialogs, DLGSTR(cr.DialogId, 1));
    return;
}

// Скрипт торговли для Хьюза
void _HoughInit(Critter cr, bool firstTime)
{
    Trader::_TraderInit(cr, firstTime);
    cr.OnBarter.Subscribe(_HoughBarter);
    cr.MaxTalkers = 1;
    cr.IsBarterOnlyCash = true;
    cr.IsNoBarter = false;
    cr.IsNoDrop = true;
    cr.IsNoSteal = true;
    cr.IsNoLoot = true;
    cr.OnMessage.Subscribe(_HoughMessage);
    cr.OnNpcPlaneEnd.Subscribe(_HoughPlaneEnd);
}

void _HoughMessage(Critter cr, Critter fromCr, int message, any value)
{
    if (message == MSG_COM2ME) {
        NpcPlanes::AddWalkPlane(cr, 17, fromCr.Hex, Game.GetDirection(cr.Hex, fromCr.Hex), false, 1);
    }
}

bool _HoughPlaneEnd(Critter cr, int planeId, int reason, Critter someCr, Item someItem)
{
    NpcPlanes::NpcPlane plane = NpcPlanes::GetCritterPlane(cr, planeId);

    if (plane.Priority == 17 && reason == REASON_SUCCESS) {
        DL("moing end");
        NpcPlanes::AddMiscPlane(cr, 0, Game.SynchronizedTime + Time::Seconds(3), null);
    }
    return true;
}

bool _HoughBarter(Critter cr, Critter barterCr, bool attach, int barterCount)
{
    if (attach) {
        any[] values = {cr.Id};
        // Сюда добавить добавление в инвентарь различных девайсов
        // 1. Дудка
        Item flute = cr.GetItem(Content::Item::lourence_flute);
        Item fluteBarter = barterCr.GetItem(Content::Item::lourence_flute);
        if (!valid(flute) && !valid(fluteBarter)) {
            if (barterCr.ModHoughRatsFluteTimeout < Game.SynchronizedTime && barterCr.ModHoughRatsFluteTimeout != ZERO_SYNCTIME) {
                barterCr.ModHoughRatsFluteTimeout = Game.SynchronizedTime + Time::Hours(Game.Random(12, 28) - barterCr.Luck);
                flute = cr.AddItem(Content::Item::lourence_flute, 1);
                if (valid(flute)) {
                    values.insertLast(flute.Id);
                }
            }
        }
        // 2. Cупертоксин
        if (barterCr.ModLourenceFloater > 0) {
            if (barterCr.ModLourenceToxinTimeout < Game.SynchronizedTime && barterCr.ModLourenceToxinTimeout != ZERO_SYNCTIME) {
                if (barterCr.ModLourenceToxinRecipe == 1) {
                    Item toxine = cr.GetItem(Content::Item::danton_poison);
                    Item toxineBarter = barterCr.GetItem(Content::Item::danton_poison);
                    if (!valid(toxine) && !valid(toxineBarter)) {
                        barterCr.ModLourenceToxinTimeout = Game.SynchronizedTime + Time::Hours(Game.Random(12, 28) - barterCr.Luck);
                        toxine = cr.AddItem(Content::Item::danton_poison, 5);
                        if (valid(toxine)) {
                            values.insertLast(toxine.Id);
                        }
                    }
                }
                Item sToxine = cr.GetItem(Content::Item::supertoxine);
                Item sToxineBarter = barterCr.GetItem(Content::Item::supertoxine);
                if (!valid(sToxine) && !valid(sToxineBarter)) {
                    barterCr.ModLourenceToxinTimeout = Game.SynchronizedTime + Time::Hours(CLAMP(Game.Random(10, 15) - barterCr.Luck, 1, 30));
                    sToxine = cr.AddItem(Content::Item::supertoxine, 1);
                    if (valid(sToxine)) {
                        values.insertLast(sToxine.Id);
                    }
                }
            }
        }

        // После всех проверок
        if (values.length() > 1) {
            Game.StartTimeEvent(Time::GameMinutes(10), DestroyItemsHough, values);
        }
    }
    else {
        Item money = cr.GetItem(Content::Item::bottle_caps);
        if (valid(money)) {
            Game.DestroyItem(money);
        }
    }
    return true;
}

void DestroyItemsHough(any[] values)
{
    Critter cr = Game.GetCritter(values[0]);
    if (valid(cr)) {
        for (int i = 1, l = values.length(); i < l; i++) {
            Item item = Game.GetItem(values[i]);
            if (valid(item) && item.CritterId == cr.Id) {
                Game.DestroyItem(item);
            }
        }
    }
}

// Скрипт лоуренса
void _LourenceInit(Critter cr, bool firstTime)
{
    cr.MaxTalkers = 1;
    cr.IsBarterOnlyCash = true;
    cr.IsNoBarter = false;
    cr.IsNoDrop = true;
    cr.IsNoSteal = true;
    cr.IsNoLoot = true;
    hstring[] sellItems = {Content::Item::lighter,
                           Content::Item::water_flask,
                           Content::Item::blue_condom,
                           Content::Item::cosmetic_case,
                           Content::Item::deck_of_cards,
                           Content::Item::mirror_shades,
                           Content::Item::necklace,
                           Content::Item::watch};
    hstring[] buyItems = {Content::Item::brahmin_skin,
                          Content::Item::meat,
                          Content::Item::gecko_pelt,
                          Content::Item::golden_gecko_pelt,
                          Content::Item::radscorpion_parts};
    LimitedBarter::SaveBarterLimit(cr.Id, sellItems, buyItems);
}

// Вертиберд
void _VertiberdInit(Item vertiberd, bool firstTime)
{
    vertiberd.CarIsNoLockpick = true;
    vertiberd.OnCritterUseOnSelf.Subscribe(_VertiberdUse);
    vertiberd.OnCritterUseSkill.Subscribe(_VertiberdSkill);
    vertiberd.Info = 2;
}

bool _VertiberdUse(Item vertiberd, Critter cr, Item usedItem, int param)
{
    Messaging::Info(cr, TextPackName::Game, MsgStr::StrUseNothing);
    return true;
}

bool _VertiberdSkill(Item vertiberd, Critter cr, CritterProperty skill)
{
    Messaging::Info(cr, TextPackName::Game, MsgStr::StrUseNothing);
    return true;
}

// Ультразвуковой свисток
void _FluteInit(Item item, bool firstTime)
{
    if (firstTime) {
        item.QHunterCountFluteUse = 10;
        item.Info = 2;
        item.Lexems = "$count" + item.QHunterCountFluteUse;
    }
    item.OnCritterUseOn.Subscribe(_FluteUse);
}

#define STR_RAT_CATCHED \
    (27) // Вы применили ультразвуковой гипнотизатор на мутировавшую крысу. Теперь ее можно положить себе в рюкзак. Если, конечно, она не очухается.
#define STR_EMPTY    (28) // В свистке закончились заряды. При последнем неаккуратном применении прибор был уничтожен.
#define STR_WRONGRAT (29) // Крыса не реагирует на воздействие гипнотизатора. Попробуйте найти менее устойчивый экземпляр, где-нибудь в Пустоши.

bool _FluteUse(Item item, Critter cr, Critter onCritter, Item onItem, StaticItem onScenery, int param)
{
    if (valid(onCritter) && !onCritter.ControlledByPlayer && onCritter.ModelNameBase == CRTYPE_RAT && onCritter.IsAlive()) {
        item.QHunterCountFluteUse -= 1;
        item.Lexems = "$count" + item.QHunterCountFluteUse;
        Map map = onCritter.GetMap();
        if (valid(map)) {
            Location loc = map.GetLocation();
            if (valid(loc) && loc.AutoGarbage) {
                Item rat = map.AddItem(onCritter.Hex, Content::Item::rat_grenade, 1);
                if (valid(rat)) {
                    rat.SetupScript(RatGrenade::_RatGrenadeInit);
                }
                Game.DestroyCritter(onCritter);
                Messaging::Info(cr, TextPackName::Text, STR_RAT_CATCHED);
                if (cr.ModLourenceRatsFlute == 1 && cr.ModLourenceRatsFluteCounter < 5) {
                    cr.ModLourenceRatsFluteCounter += 1;
                    if (cr.ModLourenceRatsFluteCounter == 5) {
                        cr.ModLourenceRatsFlute = 2;
                    }
                }
            }
            else {
                Messaging::Info(cr, TextPackName::Text, STR_WRONGRAT);
            }
        }
        if (item.QHunterCountFluteUse <= 0) {
            Game.DestroyItem(item);
            Messaging::Info(cr, TextPackName::Text, STR_EMPTY);
        }
        return true;
    }
    return false;
}

// Диалоги, установка переменных, отвечающих за частоту появления разных предметов в инвентаре Хьюза
void r_SetTO(Critter player, Critter npc, int timeoutNum, int realHours)
{
    player.SetAsAny(CritterProperty(timeoutNum), Game.SynchronizedTime + Time::Hours(realHours));
    DL("" + (Game.SynchronizedTime + Time::Hours(realHours)));
}

// Сдача крыс лоуренсу при выполнении квеста "пляши под мою дудку"
void r_AwakeRats(Critter cr, Critter npc)
{
    if (valid(npc) && NpcPlanes::IsNoPlanes(npc)) {
        Map map = npc.GetMap();
        if (valid(map)) {
            for (int i = 0; i < 5; i++) {
                mpos hex = npc.Hex;
                if (Entrance::GetFreeHex(map, 2, hex)) {
                    uint8 dir = Game.GetDirection(hex, npc.Hex);
                    Critter rat = map.AddCritter(Content::Critter::RegularRat, hex, dir);
                    rat.SetupScript(RatGrenade::_RatInit);
                    if (valid(rat)) {
                        NpcPlanes::AddAttackPlane(rat, 0, npc);
                    }
                }
            }
            npc.StartTimeEvent(Time::Seconds(3), cte_Comment1);
        }
    }
}

void cte_Comment1(Critter cr)
{
    Messaging::Say(cr, TextPackName::Dialogs, DLGSTR(cr.DialogId, 2));
    return;
}

// Приманка лоуренса

// Неудачно выбрано место для использования приманки.
#define STR_WRONG_PLACE (33)
// Вы открываете крышку и у вас кружится голова от ужасной вони. Вы выливаете содержимое банки на свою одежду. Теперь остается только ждать, пока
// появятся крысы.
#define STR_SUCCESS (34)
// Фу!!! Чем это от тебя воняет, lex name??!!Ну и вонища!!!Проклятье! lex name!! Отойди от меня на другую сторону улицы. Или лучше вали куда
// подальше!Дерьмо! Кто-то навалил в штаны?Буэ... Меня сейчас вырвет! lex name, пошел вон!Что это за ужасный запах?!Что это за невыносимая
// вонь?*Зажимает ноздри пальцами*Уберите это дерьмо от меня!!
#define STR_FOO      (35)

#define ENTRY_SPAWN  (17)
#define ENTRY_MOVETO (18)

void _LureInit(Item item, bool firstTime)
{
    item.OnCritterUse.Subscribe(_LureUse);
}

bool _LureUse(Item item, Critter cr, int param)
{
    Map map = cr.GetMap();
    if (valid(map) && map.ProtoId == Content::Map::q_rats_out && map.GetStaticItems(cr.Hex, 1, Content::Item::generic_4777).length() > 0) {
        Messaging::Info(cr, TextPackName::Text, STR_SUCCESS);
        Game.DestroyItem(item);
        Game.StartTimeEvent(Time::Seconds(10), MoveRat, map.GetLocation().Id); //SavedDeferredCall
        cr.ModLourenceLureActive = Game.SynchronizedTime + Time::Hours(3);
        cr.StartTimeEvent(Time::Seconds(30), cte_Lure);
    }
    Messaging::Info(cr, TextPackName::Text, STR_WRONG_PLACE);
    return true;
}

void cte_Lure(Critter cr)
{
    if (cr.ModLourenceLureActive > Game.SynchronizedTime) {
        Map map = cr.GetMap();
        if (valid(map)) {
            Critter[] npc = map.GetCritters(cr.Hex, 5, CritterFindType::NonDeadNpc);
            if (!npc.isEmpty()) {
                Messaging::Say(npc[Game.Random(0, npc.length() - 1)], TextPackName::Text, STR_FOO, "$name" + Obsolete::GetPlayerName(cr.Id));
            }
        }
        timespan delayTicks = Time::Minutes(2);
        if (delayTicks != ZERO_TIMESPAN) {
            Game.RepeatCurrentTimeEvent(delayTicks);
        }
    }
    return;
}

void MoveRat(any value)
{
    Location loc = Game.GetLocation(ident(value));
    if (valid(loc)) {
        Map map = loc.GetMapByIndex(1);
        Map mapTo = loc.GetMapByIndex(0);
        if (valid(map) && valid(mapTo)) {
            Critter[] rats = map.GetCritters(CritterFindType::Npc);
            Critter[] allies = map.GetCritters(CritterFindType::Any);
            if (!rats.isEmpty()) {
                for (int i = 0; i < rats.length() && i < 5; i++) {
                    Critter cr = rats[i];
                    mpos hex;
                    Entrance::MapGetEntryCoords(mapTo, ENTRY_SPAWN, 0, hex);
                    if (Entrance::GetFreeHex(mapTo, 3, hex)) {
                        cr.TransferToMap(map, hex, Game.Random(0, 5));
                        cr.IsNoHome = true;
                        cr.StartTimeEvent(Time::Seconds(30), cte_move);
                        for (int j = 0; j < allies.length(); j++) {
                            Critter crit = allies[j];
                            if (crit.ControlledByPlayer || crit.NpcRole == 401) {
                                NpcPlanes::AddAttackPlane(cr, 0, crit);
                                EnemyStack::AddEnemyToStack(cr, crit.Id);
                            }
                        }
                    }
                }
                Game.StartTimeEvent(Time::Seconds(Game.Random(1, 3)), MoveRat, value); //SavedDeferredCall
            }
        }
    }
}

void cte_move(Critter cr)
{
    if (cr.IsDead()) {
        return;
    }
    if (NpcPlanes::IsNoPlanes(cr)) {
        Map map = cr.GetMap();
        mpos hex = cr.Hex;
        if (valid(map) && Entrance::MapGetEntryCoords(map, ENTRY_MOVETO, 0, hex)) {
            if (Entrance::GetFreeHex(map, 15, hex)) {
                NpcPlanes::AddWalkPlane(cr, 17, hex, Game.Random(0, 5), false, 0);
            }
        }
    }
    timespan delayTicks = Time::Seconds(30);
    if (delayTicks != ZERO_TIMESPAN) {
        Game.RepeatCurrentTimeEvent(delayTicks);
    }
}

#endif

}
