namespace PlayerRegistration
{

///@ Property Game Common CritterProperty[] RegProperties Mutable OwnerSync Persistent
///@ Property Critter Common bool IsGenerated Mutable OwnerSync Persistent
///@ RemoteCall Server GeneratePlayer(CritterProperty=>int32 props)

#if SERVER

void ModuleInit()
{
    AddRegistrationProperty(CritterProperty::StrengthBase);
    AddRegistrationProperty(CritterProperty::PerceptionBase);
    AddRegistrationProperty(CritterProperty::EnduranceBase);
    AddRegistrationProperty(CritterProperty::CharismaBase);
    AddRegistrationProperty(CritterProperty::IntellectBase);
    AddRegistrationProperty(CritterProperty::AgilityBase);
    AddRegistrationProperty(CritterProperty::LuckBase);
    AddRegistrationProperty(CritterProperty::TagSkill1);
    AddRegistrationProperty(CritterProperty::TagSkill2);
    AddRegistrationProperty(CritterProperty::TagSkill3);
    for (int i = 0; i < CritterPropertyGroup::Traits.length(); i++) {
        AddRegistrationProperty(CritterPropertyGroup::Traits[i]);
    }
    AddRegistrationProperty(CritterProperty::Age);
    AddRegistrationProperty(CritterProperty::Gender);
}

void AddRegistrationProperty(CritterProperty prop)
{
    auto properies = Game.RegProperties;
    properies.insertLast(prop);
    Game.RegProperties = properies;
}

void GeneratePlayer(Player player, dict<CritterProperty, int> props)
{
    Critter cr = player.GetControlledCritter();

    if (cr.IsGenerated) {
        ThrowException("Player already generated", cr);
    }

    Parameters::CritterGenerate(props);

    for (int i = 0; i < props.length(); i++) {
        cr.SetAsInt(props.getKey(i), props.getValue(i));
    }

    cr.ModelNameBase = (cr.Gender == GenderType::Male ? CRTYPE_DEFAULT_M : CRTYPE_DEFAULT_F);
    Assert(cr.ModelNameBase != EMPTY_HSTRING);
    cr.ModelName = cr.ModelNameBase;
    cr.IsGenerated = true;
    cr.CurrentHp = cr.MaxLife;
    Game.Log("Player generated");
}

#endif

#if CLIENT

void ModuleInit()
{
    Game.OnCritterIn.Subscribe(OnCritterIn);
}

dict<CritterProperty, int> RegProps = {};

void CallRegisterPlayer(string name, string pass, dict<CritterProperty, int> props)
{
    // GetCacheData
    Game.SetCacheText("RegName__", name);
    Game.SetCacheText("RegPassword__", pass);
    RegProps = props.clone();
    Game.Register(name, pass);
    GuiScreensExt::TryExit();

    // Wait disconnection and login
    while (true) {
        Yield(10);

        if (!Game.IsConnected() && !Game.IsConnecting()) {
            Game.Login(Game.GetCacheText("RegName__"), Game.GetCacheText("RegPassword__"));
            break;
        }
    }
}

void OnCritterIn(Critter cr)
{
    if (!cr.IsChosen || cr.IsGenerated) {
        return;
    }

    Assert(RegProps.length() > 0);
    CurPlayer.ServerCall.GeneratePlayer(RegProps.clone());
}

#endif

}
