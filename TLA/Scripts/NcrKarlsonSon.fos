// FOS Server
// Author: rifleman17

//#include "Tla"

#pragma property Critter Protected     uint8 NCRKarlsonSon Group = Quests, Quest = 4266, Max = 9
#pragma property Critter PrivateServer uint  NcrSonCatcherId
#pragma property Critter PrivateServer uint8 NcrSonMovesCounter Max = 9

#define RUN_PLANE_ID         ( 111 )                               // код плана "бежать от игрока"
#define MAX_RUNS             ( 5 )                                 // число перебежек
#define SON_WAIT_PRIORITY    ( 100 )                               // приоритет плана "подожди игрока"
#define SEARCH_RADIUS        ( 3 )                                 // расстояние в гексах до игрока, в котором он должен быть вовремя остановок мальчика
#define RUN_RADIUS           ( 5 )                                 // расстояние, на которое бежит мальчик (20)

#define Q_STAT_FAILED        ( 4 )                                 // статус квеста - провален (не догнал)
#define Q_STAT_CATCHED       ( 5 )                                 // статус квеста - выполнен

#define STR_RUN              ( 1 )                                 // строка, которую произносит человек

////import bool GetFreeHex0( Map& map, uint radius, uint16& hx, uint16& hy ) from "Caravan";

void r_RunKarlsonRun( Critter& player, Critter@ npc )
{
    if( !valid( npc ) || !npc.IsNoPlanes() )
        return;

    npc.NcrSonMovesCounter = 0;
    npc.NcrSonCatcherId = player.Id;

    __EventNpcPlaneEndEx.SubscribeToCritter( npc, _PlaneEnd );

    Run( npc );
}

// Npc misc plane script
void plane_WaitPlayer( Critter& npc )
{
    npc.NcrSonMovesCounter += 1;
    npc.SayMsg( SAY_NORM_ON_HEAD, TEXTMSG_DLG, DLGSTR( npc.DialogId, STR_RUN ) );

    Critter@ player = GetCritter( npc.NcrSonCatcherId );
    if( !_CheckIsPlayerHere( npc ) )
    {
        npc.NcrSonMovesCounter = MAX_RUNS;
        player.NCRKarlsonSon = Q_STAT_FAILED;
        return;
    }

    if( npc.NcrSonMovesCounter <= MAX_RUNS )
        Run( npc );
    else
    {
        player.NCRKarlsonSon = Q_STAT_CATCHED;
        npc.IsNoHome = false;
    }
}

bool _CheckIsPlayerHere( Critter& cr )
{
    Critter@ player = GetCritter( cr.NcrSonCatcherId );
    if( !valid( player ) )
        return false;

    if( !valid( player.GetMap() ) )
        return false;

    if( player.GetMap().Id != cr.GetMap().Id )
        return false;

    return GetCrittersDistantion( cr, player ) <= SEARCH_RADIUS;
}

// CRITTER_EVENT_PLANE_END
bool _PlaneEnd( Critter& cr, NpcPlane& plane, int reason, Critter@ someCr, Item@ someItem )
{
    if( plane.Identifier == RUN_PLANE_ID && reason == REASON_SUCCESS )
    {
        NpcPlanes::AddMiscPlane( cr, SON_WAIT_PRIORITY, __FullSecond + 60, NcrKarlsonSon::plane_WaitPlayer );

    }
    else if( plane.Identifier == RUN_PLANE_ID && reason != REASON_SUCCESS )
    {
        Run( cr );
        return false;
    }
    return true;
}

// ~loadscript ncr_karlson_son
void Run( Critter& cr )
{
    uint16 hX = cr.HexX;
    uint16 hY = cr.HexY;

    Caravan::GetFreeHex( cr.GetMap(), RUN_RADIUS, hX, hY );

    NpcPlanes::AddWalkPlane( cr, AI_PLANE_WALK_PRIORITY, RUN_PLANE_ID, 0, hX, hY, Random( 0, 5 ), true, 1 );
}
