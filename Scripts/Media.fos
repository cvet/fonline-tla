namespace Media
{

// Author: cvet

///@ RemoteCall Client DoPlayVideo(string videoName, bool canInterrupt)
///@ RemoteCall Client DoPlayMusic(int32 pos, int32 repeat, string musicName)
///@ Property Map Common hstring[] Music Mutable PublicSync Persistent Resource
///@ Property Map Common hstring[] AmbientSound Mutable PublicSync Persistent Resource

#if SERVER

void PlayMusic(Critter cr, string musicName, int pos, int repeat)
{
    if (cr.ControlledByPlayer) {
        cr.PlayerClientCall.DoPlayMusic(int(pos), int(repeat), musicName);
    }
}

void PlayMusic(Map map, string musicName, int pos, int repeat)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);
    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlayMusic(int(pos), int(repeat), musicName);
    }
}

void PlayVideo(Critter cr, string videoName, bool canInterrupt)
{
    if (cr.ControlledByPlayer) {
        cr.PlayerClientCall.DoPlayVideo(videoName, canInterrupt);
    }
}

void PlayVideo(Map map, string videoName, bool canInterrupt)
{
    Critter[] critters = map.GetCritters(CritterFindType::Players);
    for (int i = 0; i < critters.length(); i++) {
        critters[i].PlayerClientCall.DoPlayVideo(videoName, canInterrupt);
    }
}

#endif

#if CLIENT

string[] GlobalMapMusic = {"23world.acm", "03 - A Way To Anywhere.ogg", "04 - 12h. Dusk.ogg", "08 - Blue Grass.ogg", "12 - Kill Klesk.ogg"};
nanotime NextAmbient;

void ModuleInit()
{
    Game.OnLoop.Subscribe(OnLoop);
    Game.OnMapLoaded.Subscribe(OnMapLoaded);
}

void OnLoop()
{
    Critter chosen = Game.GetChosen();

    if (chosen != null && CurMap != null && Game.FrameTime > NextAmbient) {
        if (CurMap.AmbientSound != null && CurMap.AmbientSound.length() > 0) {
            bool play = Game.PlaySound(CurMap.AmbientSound[Game.Random(0, CurMap.AmbientSound.length() - 1)]);
            Assert(play);
        }
        NextAmbient = Game.FrameTime + Time::Seconds(Game.Random(30, 60));
    }
}

void OnMapLoaded()
{
    hstring music;
    Critter chosen = Game.GetChosen();
    if (chosen != null) {
        if (CurMap == null) {
            music = hstring("sound/music/" + GlobalMapMusic[Game.Random(0, GlobalMapMusic.length() - 1)]);
        }
        else {
            if (CurMap != null && CurMap.Music != null && CurMap.Music.length() > 0) {
                music = CurMap.Music[Game.Random(0, CurMap.Music.length() - 1)];
            }
        }
    }
    if (music != EMPTY_HSTRING) {
        Game.Log("Playing music: " + music);
        bool play = Game.PlayMusic(music, Time::Milliseconds(1));
        Assert(play);
    }
}

void DoPlayMusic(int pos, int repeat, string musicName)
{
    Game.PlayMusic(musicName, repeat == 0 ? ZERO_TIMESPAN : Time::Milliseconds(repeat));
}

void DoPlayVideo(string videoName, bool canInterrupt)
{
    Game.PlayVideo(videoName, canInterrupt, false);
}

#endif

}
