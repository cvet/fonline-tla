// FOS Server
// Author: Sufir

///@ Property Critter Protected int8 V13DclawEggs Group = Quests Quest = 4902 Min = 0 Max = 7
///@ Property Game PrivateServer uint EndingV13DclawGenocide

#define STR_FOTEXT_SAY_GOOD (3490)
#define STR_FOTEXT_GOOD     (3491)
#define STR_FOTEXT_SAY_BAD  (3492)
#define STR_FOTEXT_BAD      (3493)
#define BONUS_HP            (4)

// Инициализация яйца
void _EggInit(Item egg, bool firstTime)
{
    egg.Info = 2;
    egg.IsCanUse = true;
    egg.OnCritterUseOn.Subscribe(e_EggUse);
    // ToDo: Возможность? при использовании науки, разбить и изучить яйцо, провал квеста при уничтожении яйца
    // egg.OnCritterUseSkill.Subscribe(e_EggScience);
    egg.OnFinish.Subscribe(e_EggFinish);
}

// Действия при использовании яйца.
bool e_EggUse(Item egg, Critter player, Critter target, Item onItem, StaticItem onScenery, uint param)
{
    if (valid(target) || valid(onItem) || valid(onScenery)) {
        return false;
    }

    if (egg.V13GorisEggPlayerId == player.Id) {
        player.MaxLifeBase += BONUS_HP;
        player.Karma -= BONUS_HP * 10;
        Game.DeleteItem(egg);
        player.SayMsg(SAY_NORM, TEXTMSG_TEXT, STR_FOTEXT_SAY_GOOD);
        player.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, STR_FOTEXT_GOOD, "$Hp " + BONUS_HP);
        SetQuestStage(player, 6);
    }
    else {
        Poison::AffectPoison(player, Game.Random(30, 50));
        player.Karma -= 10;
        Game.DeleteItem(egg);
        player.SayMsg(SAY_NORM, TEXTMSG_TEXT, STR_FOTEXT_SAY_BAD);
        player.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, STR_FOTEXT_BAD);
    }
    return true;
}

void e_EggFinish(Item egg)
{
    if (GetQuestStageById(egg.V13GorisEggPlayerId) == 1) {
        SetQuestStageById(egg.V13GorisEggPlayerId, 5);
        Game.EndingV13DclawGenocide += 1;
    }
}

// Вспомогательная функция. Устанавливает стадию квеста.
bool SetQuestStage(Critter player, int stage)
{
    if (!valid(player)) {
        return false;
    }
    if (!player.IsOwnedByPlayer()) {
        return false;
    }
    SetQuestStageById(player.Id, stage);
    return false;
}

// Вспомогательная функция. Устанавливает стадию квеста.
bool SetQuestStageById(int playerId, int stage)
{
    Critter player = Game.GetCritter(playerId);
    if (valid(player) && player.V13DclawEggs != stage) {
        player.V13DclawEggs = stage;
        return true;
    }
    return false;
}

// Вспомогательная функция. Возвращает квеста.
int GetQuestStageById(int playerId)
{
    Critter player = Game.GetCritter(playerId);
    if (valid(player)) {
        return player.V13DclawEggs;
    }
    return -1;
}

/*
 # 4902 - q_v13_dclaweggs
 # Квест "Беглец", Убежище 13

   {4902001}{}{Горис попросил вас вынести из убежища и доставить в Брокен Хиллс Профессору одно из яиц.}
   {4902002}{}{Выполнено. Вы доставили яйцо разумного смертокогта Профессору.}
   {4902003}{}{Выполнено. Вы доставили яйцо и убедили Профессора вырастить и воспитать разумного смертокогта.}
   {4902004}{}{Провалено. Вы отказались помогать Горису.}
   {4902005}{}{Провалено. Вы потеряли яйцо разумного смертокогта.}
   {4902006}{}{Провалено. Вы съели яйцо разумного смертокогта.}

   {4901101}{}{Убежище 13}
   {4901102}{}{Яйца смерти}
*/
